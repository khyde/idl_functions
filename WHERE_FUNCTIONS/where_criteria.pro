; $ID:	WHERE_CRITERIA.PRO,	2020-07-08-15,	USER-KJWH	$
;##############################################################
FUNCTION WHERE_CRITERIA, ARRAY, COUNT, OPERATORS=OPERATORS, RANGE=RANGE, NCOMPLEMENT=NCOMPLEMENT, COMPLEMENT=COMPLEMENT, CRITERIA_TXT=CRITERIA_TXT
;+
;
;	PURPOSE THIS FUNCTION FINDS DATA WITHIN THE CRITERIA RANGE PROVIDED
;
; ARGUMENTS:
; 	ARRAY....... A SCALAR OR ARRAY
; 	OPERATORS... OPERATORS TO USE THE IN THE WHERE STATEMENT (LE, LT, GE, GT - 'EQ' NOT PERMITTED)
;		RANGE....... THE VALID RANGE (MIN,MAX) OF THE DATA TO APPLY THE OPERATORS TO
;
; OUTPUT:
;   COUNT.......... THE NUMBER OF ARRAY ELEMENTS MATCHING ANY ELEMENT IN VALUES
;   NCOMPLEMENT.... THE NUMBER OF ARRAY ELEMENTS NOT MATCHING ANY ELEMENT IN VALUES
;   COMPLEMENT..... THE SUBSCRIPTS OF ARRAY NOT MATCHING ANY VALUE IN VALUES ARRAY
;   CRITERIA_TXT... TEXT STRING DESCRIBING THE CRITERIA OPERATORS AND RANGE
;
; EXAMPLES:
;   DATA = [1,2,3,4,5] & OK= WHERE_CRITERIA(DATA, RANGE=[1,3], CRITERIA_TXT=CRITERIA_TXT)  & PRINT, DATA[OK] & PRINT, CRITERIA_TXT
;   DATA = [1,2,3,4,5] & OK= WHERE_CRITERIA(DATA,OPERATORS=['GT','LT'], RANGE=[1,3])  & PRINT, DATA[OK]
;   DATA = [1,2,3,4,5] & OK= WHERE_CRITERIA(DATA,OPERATORS=['GE','LE'], RANGE=[1,3])  & PRINT, DATA[OK]
;   DATA = [1,2,3,4,5] & OK= WHERE_CRITERIA(DATA,OPERATORS=['GE','LT'], RANGE=[1,3])  & PRINT, DATA[OK]
;
; NOTES:
;		'EQ' NOT ALLOWED
;		PROGRAM EXPECTS OPERATORS AND  RANGE TO BE 2 ELEMENTS EACH
;
; HISTORY:
;	 SEPT 6, 2005	WRITTEN BY:	J.E. O'REILLY, NOAA, 28 TARZWELL DRIVE, NARRAGANSETT, RI 02882
;	 AUG 12, 2012 - JEOR: SQUARE BRACKETS FOR RANGE SUBSCRIPTS; UPPERCASE,FORMATTING
;	                      (_OPERATORS[0] EQ 'LT' AND _OPERATORS[1] EQ 'NE'): BEGIN
;	                      (_OPERATORS[0] EQ 'LE' AND _OPERATORS[1] EQ 'NE'): BEGIN
;                       (_OPERATORS[0] EQ 'GT' AND _OPERATORS[1] EQ 'NE'): BEGIN
;  NOV 13, 2014 - JEOR: REPLACED RETURN, -1 WITH RETURN,[], FORMATTING
;  FEB 13, 2017 - KJWH: UPDATED FORMATTING
;                       REMOVED UNNECESSARY CODE
;                       UPDATED DOCUMENTATION
;                       ADDED A WHERE_IN CHECK OF THE OPERATORS
;  MAR 15, 2017 - KJWH: ADDED STEPS TO PARSE THE RANGE IF PROVIDED AS A TEXT STRING     
;  APR 24, 2018 - KJWH: Added - NCOMPLEMENT = 0 ; Default in case [] is returned prior to the WHERE_IN call                
;#####################################################################################
;-
;*****************************
 ROUTINE_NAME='WHERE_CRITERIA'
;*****************************

  NCOMPLEMENT = 0 ; Default in case [] is returned prior to the WHERE_IN call

  IF N_ELEMENTS(RANGE) EQ 1 AND IDLTYPE(RANGE) EQ 'STRING' THEN BEGIN
    RANGE = REPLACE(RANGE,[';',':',',',' '],['_','_','_','_']) ; CHANGE ANY POSSIBLE DELIMITERS TO '_'
    RANGE = STRSPLIT(RANGE,'_',/EXTRACT)
    RANGE = FLOAT(RANGE)
  ENDIF

  IF N_ELEMENTS(RANGE) NE 2 THEN RETURN,[]

; =====> DETERMINE SIZE,AND IDLTYPES OF ARRAY
  SZ = SIZEXYZ(ARRAY)

; =====> ENSURE THAT BOTH ARRAY AND VALUES ARE NOT EMPTY
  IF SZ.N_ELEMENTS  EQ 0 THEN RETURN, []

;	===> DEFAULT: IF NO OPERATORS PROVIDED THEN DEFAULT IS ['GE','LT']
  VALID_OPERATORS = ['LT','LE','GT','GE']
  IF N_ELEMENTS(OPERATORS) NE 2 THEN OPERATORS = ['GE','LT'] 
  
  OK = WHERE_IN(OPERATORS,VALID_OPERATORS,COUNT,NCOMPLEMENT=NCOMPLEMENT,COMPLEMENT=COMPLEMENT) 
  IF COUNT NE 2 THEN MESSAGE,'ERROR: INVALID OPERATORS'

  CRITERIA_TXT= OPERATORS[0] + ' ' + STRTRIM(RANGE[0],2) + ' AND ' + OPERATORS[1] + ' ' + STRTRIM(RANGE[1],2)
  
;	===> APPLY THE CRITERIA
	CASE 1 OF
		(OPERATORS[0] EQ 'GT' AND OPERATORS[1] EQ 'LE'): RETURN, WHERE(ARRAY GT RANGE[0] AND ARRAY LE RANGE[1], COUNT, NCOMPLEMENT=NCOMPLEMENT, COMPLEMENT=COMPLEMENT)
		(OPERATORS[0] EQ 'GE' AND OPERATORS[1] EQ 'LT'): RETURN, WHERE(ARRAY GE RANGE[0] AND ARRAY LT RANGE[1], COUNT, NCOMPLEMENT=NCOMPLEMENT, COMPLEMENT=COMPLEMENT)
		(OPERATORS[0] EQ 'GE' AND OPERATORS[1] EQ 'LE'): RETURN, WHERE(ARRAY GE RANGE[0] AND ARRAY LE RANGE[1], COUNT, NCOMPLEMENT=NCOMPLEMENT, COMPLEMENT=COMPLEMENT)
		(OPERATORS[0] EQ 'GT' AND OPERATORS[1] EQ 'LT'): RETURN, WHERE(ARRAY GT RANGE[0] AND ARRAY LT RANGE[1], COUNT, NCOMPLEMENT=NCOMPLEMENT, COMPLEMENT=COMPLEMENT)
		(OPERATORS[0] EQ 'LT' AND OPERATORS[1] EQ 'NE'): RETURN, WHERE(ARRAY LT RANGE[0] AND ARRAY NE RANGE[1], COUNT, NCOMPLEMENT=NCOMPLEMENT, COMPLEMENT=COMPLEMENT)
    (OPERATORS[0] EQ 'LE' AND OPERATORS[1] EQ 'NE'): RETURN, WHERE(ARRAY LE RANGE[0] AND ARRAY NE RANGE[1], COUNT, NCOMPLEMENT=NCOMPLEMENT, COMPLEMENT=COMPLEMENT)
    (OPERATORS[0] EQ 'GT' AND OPERATORS[1] EQ 'NE'): RETURN, WHERE(ARRAY GT RANGE[0] AND ARRAY NE RANGE[1], COUNT, NCOMPLEMENT=NCOMPLEMENT, COMPLEMENT=COMPLEMENT)  
  ENDCASE ; CASE 1 OF



END; #####################  END OF ROUTINE ################################
