; $ID:	VALID_ALG_CHECK.PRO,	2020-06-26-15,	USER-KJWH	$

	FUNCTION VALID_ALG_CHECK, IN_PROD, OPRODS=oprods, ALGS=algs, DAY_NIGHT=DAY_NIGHT

;+
; NAME:
;		VALID_ALG_CHECK
;
; PURPOSE:;
;		This function returns the valid ALGS from the SeaDAS generated L2 products
;
; CATEGORY:
;		CATEGORY
;
; CALLING SEQUENCE:
;		RESULT = VALID_ALG_CHECK(IN_PROD, OUT_PROD=OUT_PROD, ALG=ALG)
;
; INPUTS:
;		IN_PROD:  Input product    (e.g. CHLOR_A, CHLOR_A_GIOP, POC_STRAMSKI, APH_412_GIOP, KD_490_LEE)
;
; OUTPUTS:
;   RESULT:   VALID PROD + ALG
;		OUT_PROD:	Output product   (e.g. CHLOR_A, CHLOR_A,      POC,          APH_412, KD_490)
;		ALG:			Output algorithm (e.g. OCI,     OCX,          STRAMSKI,     GIOP,    LEE)
;
;
; MODIFICATION HISTORY:
;			Written Jul 20, 2007 by K.J.W.Hyde, 28 Tarzwell Drive, NMFS, NOAA 02882 (kimberly.hyde@noaa.gov)
;			        Sep 17, 2015 - KJWH: Added the OCI chlorophyll algorithm and changed the OC4/3 to be just OC
;			        Dec 03, 2015 - KJWH: Updated program to work with the new L2 products generated by SeaDAS
;			                             Now looping through the INPUT_PRODS and looking for matching SeaDAS ALGS
;			                             Now returning PROD-ALG instead of a structure 
;			                             Removed SENSOR keyword
;			        Dec 04, 2015 - KJWH: Added L2 SST PROD-ALG info
;			                             Added DAY_NIGHT keyword for the SST imagergy                     
;-
;	****************************************************************************************************
	ROUTINE_NAME = 'VALID_ALG_CHECK'

;	===> Initialize ERROR to a null string. If errors are encountered ERROR will be set to a message.
;			 The calling routine can check error (e.g.IF ERROR NE 0 then there was a problem and do this or that)
	ERROR = ''

  IPRODS      = STRUPCASE(IN_PROD)
  OPRODS      = REPLICATE('',N_ELEMENTS(IPRODS))
  PROD_ALG    = OPRODS
  ALGS        = OPRODS
  IF NONE(DAY_NIGHT) THEN DAY_NIGHT = 'DAY' ; Night time imagery are only for the SST data
  
; ===> Algorithms generated by SeaDAS's l2gen based on R2014.  Last updated 2015-12-03.  
  CHL_ALGS    = ['OCI','OCX','OC2','OC3','OC4','GIOP']
  SST_ALGS    = ['']
  SEADAS_ALGS = ['CARDER','CLARK','GIOP','GSM','HIRATA','HU','LAS','LEE','MGIOP','MOREL','QAA','STRAMSKI','SWIM','UITZ']

; ===> Loop through each input prod  
  FOR N=0, N_ELEMENTS(IPRODS)-1 DO BEGIN
    IPROD = IPRODS(N)
    SPL = STRSPLIT(IPROD,'_',/EXTRACT)
    
    OK = WHERE(SEADAS_ALGS EQ SPL(-1),COUNT,COMPLEMENT=COMPLEMENT)
    IF COUNT EQ 1 THEN BEGIN
      ALGS(N)   = SEADAS_ALGS[OK]
      OPRODS(N) = REPLACE(IPROD,'_'+ALGS(N),'')
    ENDIF ELSE OPRODS(N) = IPROD

; ===> Special Cases    
    IF STRMID(IPROD,0,3) EQ 'CHL' THEN BEGIN
      OPRODS(N) = 'CHLOR_A' ; Find CHL prods make the OPROD CHLOR_A
      ALG = CHL_ALGS[WHERE(CHL_ALGS EQ SPL(-1),/NULL)]         ; Looks for CHL_ALGS
      IF ALG EQ [] THEN BEGIN
        APOS = STRPOS(IPROD,'_',/REVERSE_SEARCH)
        IF APOS GT 0 AND STRMID(IPROD,APOS+1) NE 'A' THEN ALGS(N) = STRMID(IPROD,APOS+1)
      ENDIF ELSE ALGS(N) = ALG
      IF IPROD EQ 'CHLOR_A' THEN ALGS(N) = 'OCI'               ; Default NASA CHLOR_A algorithm as of R2014 
    ENDIF
    
    IF HAS(['SST','SST4','NSST'],IPROD,/EXACT) EQ 1 THEN BEGIN
      OPRODS(N) = 'SST'
      CASE IPROD OF
        'SST4': ALGS(N) = '4UM'
        'NSST': ALGS(N) = '11UM'
        'SST' : ALGS(N) = '11UM'
      ENDCASE
      IF STRUPCASE(DAY_NIGHT) EQ 'NIGHT' THEN ALGS(N) = 'N_' + ALGS(N)
    ENDIF
    IF ALGS(N) NE '' THEN PROD_ALG(N) = OPRODS(N) + '-' + ALGS(N) ELSE PROD_ALG(N) = OPRODS(N) 
  ENDFOR
    
	RETURN, PROD_ALG 

END; #####################  End of Routine ################################
