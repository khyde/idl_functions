; $ID:	MAPS_AVHRR_2MAP.PRO,	2016-08-23,	USER-KJWH	$
;##########################################################################
FUNCTION MAPS_AVHRR_2MAP, AVHRR, MAP_OUT, INIT=INIT

; THIS PROGRAM CONVERTS A 8640 X 4320 AVHRR ARRAY TO A VALID MAP
;
; CATEGORY:
;    MAPPING
;    
; UTILITY:
;    REMAPPING
;    
; CALLING SEQUENCE:
;    NEW = MAPS_AVHRR_2BIN(AVHRR, 'NEC') 
;    
; INPUTS:
;   AVHRR   = A 8640 X 4320 AVHRR DATA ARRAY
;   MAP_OUT = ANY OF THE VALID_MAPS
;
; OPTIONAL INPUTS:
;
; KEYWORDS: 
;
; OUTPUTS:
;
; EXAMPLES:
; 
; NOTES:
;  The AVHRR_8640_4320_LAT(LON).SAV files were created using MAPS_L3B_SUBS
;  
; MODIFICATION HISTORY:
;     MAR 17, 2016  WRITTEN BY: by K.J.W.Hyde, 28 Tarzwell Drive, NMFS, NOAA 02882 (kimberly.hyde@noaa.gov)
;     AUG 19, 2016 - KJWH: Changed !S.MASTER to !S.MAPINFO
;     AUG 23, 2016 - KJWH: Updated the AVHRR LON/LAT files
;               
;- 
  
;********************************* 
  ROUTINE_NAME = 'MAPS_AVHRR_2MAP'
;*********************************
  COMMON MAPS_AVHRR_2MAP_, LONS, LATS, AVHRR_STRUCT
  IF KEY(INIT) THEN BEGIN
    LONS = []
    LATS = []
    AVHRR_STRUCT = []
  ENDIF  
    
  SZ = SIZEXYZ(AVHRR)
  IF SZ.PX NE 8640 AND SZ.PY NE 4320 THEN RETURN, 'ERROR: input array deminsions must be 8640 x 4320'
    
  IF NONE(LONS) THEN LONS = IDL_RESTORE(!S.MAPINFO + 'AVHRR-PXY_8640_4320-LON.SAV')
  IF NONE(LATS) THEN LATS = IDL_RESTORE(!S.MAPINFO + 'AVHRR-PXY_8640_4320-LAT.SAV')
  
  IF IDLTYPE(LONS) EQ 'STRING' OR IDLTYPE(LATS) EQ 'STRING' THEN BEGIN
    LONS = []
    LATS = []
    RETURN, 'ERROR: Unable to read the MASTER LON/LAT files'
  ENDIF
  
  COUNT_MAP_OUT = 0
  IF ANY(AVHRR_STRUCT) THEN BEGIN
    OK = WHERE(TAG_NAMES(AVHRR_STRUCT) EQ MAP_OUT, COUNT_MAP_OUT)
    IF COUNT_MAP_OUT EQ 1 THEN REMAP_STRUCT = AVHRR_STRUCT.(OK) ELSE REMAP_STRUCT = []  
  ENDIF ELSE AVHRR_STRUCT = []
  
  MDATA = MAPS_LONLAT_GRID(AVHRR, MAP_OUT=MAP_OUT, LON=LONS, LAT=LATS, STRUCT=REMAP_STRUCT, INIT=INIT)  
  
; ===> Create COMMON structure based on MAP_OUT  
  IF AVHRR_STRUCT NE [] THEN BEGIN
    IF COUNT_MAP_OUT EQ 0 THEN AVHRR_STRUCT = CREATE_STRUCT(AVHRR_STRUCT,MAP_OUT,REMAP_STRUCT)
  ENDIF ELSE AVHRR_STRUCT = CREATE_STRUCT(MAP_OUT,REMAP_STRUCT)
  
  RETURN, MDATA
  
END
