; $ID:	MAPS_COORDS.PRO,	2015-09-12	$
;+
;;#############################################################################################################
	FUNCTION MAPS_COORDS,X,Y

; PURPOSE: THIS FUNCTION RETURNS A STRUCTURE WITH BOTH DEVICE(X,Y) AND DATA (LONS,LATS) COORDINATES
;           &  FROM CONVERT_COORD
; 
; 
; CATEGORY:	MAPS;		 
;
; CALLING SEQUENCE: RESULT = MAPS_COORDS(X,Y OR LONS,LATS)
;
; INPUTS: PIXEL [DEVICE] COORDINATES X,Y OR LONGITUDES,LATITUDES  

; OPTIONAL INPUTS:
;		
; KEYWORD PARAMETERS:

; OUTPUTS: 
;		
;; EXAMPLE:
;   MAPS_SET,'NEC' & X = FINDGEN(1024) & Y = X & S= MAPS_COORDS(X,Y) & PMM,S.Y & PMM,S.X 
;
; MODIFICATION HISTORY:
;			WRITTEN SEP 12, 2015 J.O'REILLY
;#################################################################################
;-
;****************************
ROUTINE_NAME  = 'MAPS_COORDS'
;****************************
IF !X.TYPE NE 3       THEN MESSAGE,'ERROR: MAP SYSTEM IS NOT ESTABLISHED'
IF NONE(X) OR NONE(Y) THEN MESSAGE,'ERROR: X AND Y ARE REQUIRED'
IF NOF(X) NE NOF(Y)   THEN MESSAGE,'ERROR: X SIZE MUST EQUAL Y SIZE'

;#####################################################
;===> GET THE CORRESPONDING XX AND YY FROM LONS AND LATS
IF MAX([X,Y]) LT 180. OR MIN([X,Y]) LT 0.0 THEN  BEGIN
  XYZ = CONVERT_COORD(X,Y,/DATA,/TO_DEVICE,/DOUBLE)
  XX = ROUND(REFORM(XYZ(0,*)))
  YY = ROUND(REFORM(XYZ(1,*)))
ENDIF ELSE BEGIN
  ;===> GET THE CORRESPONDING LONS AND LATS FROM X AND Y
  XYZ = CONVERT_COORD(X,Y,/DEVICE,/TO_DATA,/DOUBLE)
  LONS = REFORM(XYZ(0,*))
  LATS = REFORM(XYZ(1,*))    
ENDELSE;IF MAX([X,Y]) LT 180. OR MIN([X,Y]) LT 0.0 THEN  BEGIN
;|||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

;===>NOW GET THE COMPLEMENT
IF NONE(LONS) AND NONE(LATS) THEN BEGIN
  XYZ = CONVERT_COORD(XX,YY,/DEVICE,/TO_DATA,/DOUBLE)
  LONS = REFORM(XYZ(0,*))
  LATS = REFORM(XYZ(1,*))
ENDIF;IF NONE(LONS) AND NONE(LATS) THEN BEGIN

IF NONE(XX) AND NONE(YY) THEN BEGIN
  XYZ = CONVERT_COORD(LONS,LATS,/DATA,/TO_DEVICE,/DOUBLE)
  XX = ROUND(REFORM(XYZ(0,*)))
  YY = ROUND(REFORM(XYZ(1,*)))
ENDIF;IF NONE(LONS) AND NONE(LATS) THEN BEGIN


S = CREATE_STRUCT('X',XX,'Y',YY,'LONS',LONS,'LATS',LATS)
ZWIN
RETURN, S
          
	END; #####################  END OF ROUTINE ################################
