; $ID:	MAPS_OISST_SUBSET.PRO,	2020-06-26-15,	USER-KJWH	$
; #########################################################################; 
FUNCTION MAPS_OISST_SUBSET,ARRAY, MAP_OUT=MAP_OUT, INIT=INIT, LONMIN=LONMIN, LONMAX=LONMAX, LATMIN=LATMIN, LATMAX=LATMAX
;+
; PURPOSE:  RETURN AN EXTRACTED ARRAY FROM THE INPUT OISST ARRAY WHICH COVERS THE DOMAIN OF THE MAP_OUT
;
; CATEGORY: MAPS;
;
;
; INPUTS:  ARRAY [OISST DATA ARRAY]
;          PROGRAM ASSUMES THAT THE MAP_IN = OISST
;          PROGRAM GETS LON AND LAT FROM OISST_LON.SAV & OISST_LAT.SAV IN MASTER
;
; KEYWORDS:  
;          MAP_OUT.... NAME OF OUTPUT MAP
;          LONMIN..... MIN LONGITUDE (optional)
;          LONMAX..... MAX LONGITUDE (optional)
;          LATMIN..... MIN LATITUDE (optional)
;          LATMAX..... MAX LATITUDE (optional)
;         

; OUTPUTS: A SUBSET OF THE LARGE OISST ARRAY COVERING THE DOMAIN OF THE MAP_OUT
;    1440 LON and 720 
;; EXAMPLES: SIMULATE THE OISST ARRAY WITH BYTES [FOR SPEED]>
;          HELP, MAPS_OISST_SUBSET(INDGEN([1440,720]),MAP_OUT ='NEC')
;          HELP, MAPS_OISST_SUBSET(INDGEN([1440,720]),MAP_OUT ='EC')
;          HELP, MAPS_OISST_SUBSET(INDGEN([1440,720]),MAP_OUT ='NEC_ECO')
;          HELP, MAPS_OISST_SUBSET(INDGEN([1440,720], LONMIN=-80, LONMAX=-60, LATMIN=35, LATMAX=45)
;
;
; MODIFICATION HISTORY:
;     FEB 07, 2020  Written: by K.J.W.Hyde, 28 Tarzwell Drive, NMFS, NOAA 02882 (kimberly.hyde@noaa.gov)
;     FEB 07, 2020 - KJWH: Adapted from MAPS_AVHRR_SUBSET              
;                                               
; #########################################################################
;-

;*******************************
ROUTINE_NAME  = 'MAPS_OISST_SUBSET'
;*******************************
;===> ENSURE ARRAY AND MAP_OUT ARE PROVIDED
  IF NONE(ARRAY)   THEN MESSAGE,'ERROR: ARRAY IS REQUIRED'
  IF NONE(MAP_OUT) AND NONE(LONMIN) THEN MESSAGE,'ERROR: EITHER MAP_OUT OR LON/LAT MIN/MAX ARE REQUIRED'

;===> GET OISST GLOBAL LAT & LON ARRAYS FROM MASTER

  COMMON MAPS_OISST_SUBSET_, LON, LAT, STRUCT_MAPS_SUBSET
  
  IF KEY(INIT) THEN BEGIN
    LON = []
    LAT = []
    STRUCT_MAPS_SUBSET = []
  ENDIF
  
  SZ = SIZEXYZ(ARRAY)
  IF SZ.PX NE 1440 AND SZ.PY NE 720 THEN RETURN, 'ERROR: input array deminsions must be 1440 x 720'
  
  IF NONE(LON) THEN LON = IDL_RESTORE(!S.MAPINFO + 'OISST-PX_1440-LON.SAV')
  IF NONE(LAT) THEN LAT = IDL_RESTORE(!S.MAPINFO + 'OISST-PY_720-LAT.SAV')
  
  IF IDLTYPE(LONS) EQ 'STRING' OR IDLTYPE(LATS) EQ 'STRING' THEN BEGIN
    LON = []
    LAT = []
    RETURN, 'ERROR: Unable to read the MASTER LON/LAT files'
  ENDIF

  IF NONE(MAP_OUT) THEN MAP_OUT = 'LONLAT_' + ROUNDS(LONMIN) + '_' + ROUNDS(LONMAX) + '_' + ROUNDS(LATMIN) + '_' + ROUNDS(LATMAX) 
  MAP_OUT = REPLACE(MAP_OUT,'-','N') ; REPLACE DASHES ('-') WITH N (FOR NEGATIVE) BECAUSE DASHES ARE NOT ALLOWED IN IDL TAGNAMES
  
; ===> Look for the MAP_OUT tag in STRUCT_MAPS_SUBSET if it is not already present
  IF NONE(STRUCT_MAPS_SUBSET) OR KEY(INIT) THEN STRUCT_MAPS_SUBSET=CREATE_STRUCT('_','')
  OK_TAG = WHERE(TAG_NAMES(STRUCT_MAPS_SUBSET) EQ MAP_OUT,COUNT_MAP)  
  IF COUNT_MAP EQ 0 THEN BEGIN

;===> DETERMINE LON/LAT CORNERS OF MAP_OUT
    IF VALIDS('MAPS',MAP_OUT) EQ STRUPCASE(MAP_OUT) THEN BEGIN
      MAPS_SET,MAP_OUT     
      S=MAPS_LL_BOX() 
      ZWIN
      LONMIN = S.LONMIN
      LONMAX = S.LONMAX
      LATMIN = S.LATMIN
      LATMAX = S.LATMAX
    ENDIF ; IF VALIDS('MAPS',MAP_OUT) EQ MAP_OUT THEN BEGIN

;===> GET NEAREST SUBS FOR LONMIN,LONMAX
    OK = WHERE_NEAREST(LON, LONMIN,NEAR = 0.1,COUNT_LONMIN)
    IF COUNT_LONMIN GE 1 THEN SUB_LONMIN = FIRST[OK]
    OK = WHERE_NEAREST(LON, LONMAX,NEAR = 0.1,COUNT_LONMAX)
    IF COUNT_LONMAX GE 1 THEN SUB_LONMAX = FIRST[OK]

;===> GET NEAREST SUBS FOR LATMIN,LATMAX 
    OK = WHERE_NEAREST(LAT, LATMIN,NEAR = 0.1,COUNT_LATMIN) 
    IF COUNT_LATMIN GE 1 THEN SUB_LATMIN = FIRST[OK]
    OK = WHERE_NEAREST(LAT, LATMAX,NEAR = 0.1,COUNT_LATMAX)
    IF COUNT_LATMAX GE 1 THEN SUB_LATMAX = FIRST[OK]
  
; ===> ADD ANOTHER MAP_OUT-PX_OUT LAYER (TAG) TO THE STRUCT_MAPS_SUBSET STRUCTURE
  STRUCT_MAPS_SUBSET=CREATE_STRUCT(TEMPORARY(STRUCT_MAPS_SUBSET),MAP_OUT,$
    CREATE_STRUCT('MAP_OUT',MAP_OUT,'LONMIN', LONMIN, 'SUB_LONMIN', SUB_LONMIN, $
                                    'LONMAX', LONMAX, 'SUB_LONMAX', SUB_LONMAX, $
                                    'LATMIN', LATMIN, 'SUB_LATMIN', SUB_LATMIN, $
                                    'LATMAX', LATMAX, 'SUB_LATMAX', SUB_LATMAX ))  
  
  ENDIF
 
; ===> LOOK FOR THE MAP_OUT TAG IN STRUCT_MAPS_SUBSET IF IT IS NOT ALREADY PRESENT
  OK_TAG = WHERE(TAG_NAMES(STRUCT_MAPS_SUBSET) EQ MAP_OUT,COUNT_MAP)
  SUB_LONMIN = STRUCT_MAPS_SUBSET.(OK_TAG).SUB_LONMIN
  SUB_LONMAX = STRUCT_MAPS_SUBSET.(OK_TAG).SUB_LONMAX
  SUB_LATMIN = STRUCT_MAPS_SUBSET.(OK_TAG).SUB_LATMIN
  SUB_LATMAX = STRUCT_MAPS_SUBSET.(OK_TAG).SUB_LATMAX

  RETURN,ARRAY(SUB_LONMIN:SUB_LONMAX,SUB_LATMAX:SUB_LATMIN) ; Note, LATS are in order of 90 to -90 so the subscripts are SUB_LATMAX:SUB_LATMIN


END; #####################  END OF ROUTINE ################################
