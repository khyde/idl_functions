; $ID:	PLT_GRIDS.PRO,	2018-05-09-10,	USER-KJWH	$
;#########################################################################
 PRO PLT_GRIDS, OBJ,COLOR=COLOR,THICK = THICK,LINESTYLE=LINESTYLE,SUBX = SUBX,SUBY=SUBY,XTICKV=XTICKV,YTICKV=YTICKV
;+
; NAME:
;       PLT_GRIDS
;
; PURPOSE:
;       OVERPLOT GRIDS ATOP AN EXISTING PLOT
;
; CATEGORY:
;      PLOT
;
; CALLING SEQUENCE:
;       PLT_GRIDS

;
; INPUTS:
;       OBJ REQUIRED.
;
; KEYWORD PARAMETERS:
;        COLOR........ COLOR FOR THE GRIDS
;        THICK ....... THICKNESS OF THE GRID LINES
;        LINESTYLE.... LINESTYLE FOR THE GRIDS
;        SUBX......... DRAW GRIDS AT MINOR X-TICKS 
;        SUBY......... DRAW GRIDS AT MINOR Y-TICKS
;        XTICKV....... X VALUES USED TO DRAW THE GRID USING PLT_LINE
;        YTICKV....... Y VALUES USED TO DRAW THE GRID USING PLT_LINE
;       _EXTRA.....    MAY CONTAIN ANY IDL KEYWORDS WHICH ARE VALID FOR THE PLOT COMMAND.
;
; OUTPUTS:
;
; SIDE EFFECTS:
;       OVERPLOTS A LINE ATOP EXISTING PLOT
;
; RESTRICTIONS:
;       ASSUMES YOU HAVE ALREADY ISSUED A PLOT COMMAND.

;
; MODIFICATION HISTORY:
;       WRITTEN BY:  J.E.O'REILLY, FEB 10, 1997.
;       MAR 13, 2014 - JEOR: COPIED FROM ONE2ONE FORMATTING
;                            ADDED KEYWORD OBJ [FOR NEW GRAPHICS]
;       JAN 20, 2017 - JEOR: COMMENTED ;  OBJ.COLOR = RGBS(COLOR)
;       APR 23, 2018 - JEOR: Added keywords, XTICKV, YTICKV and PLT_LINE to plot the grids in any color [not just black] 
;       MAY 09, 2018 - KJWH: Formatting
;-

; ***************************
  ROUTINE_NAME = 'PLT_GRIDS'
; ***************************

; ===> DEFAULTS
  IF NONE(COLOR) THEN COLOR = 'GREY'
  IF NONE(LINESTYLE) THEN LINESTYLE = 0
  IF NONE(THICK) THEN THICK = 1

; ===> CHECK FOR THE PLOT OBJECT 
  IF N_ELEMENTS(OBJ) EQ 0 THEN MESSAGE,'ERROR: PLOT OBJECT IS REQUIRED'
  IF IDLTYPE(OBJ) NE 'OBJREF' THEN MESSAGE,'ERROR: OBJ MUST BE A PLOT OBJECT'
  
; ===> PLOT THE GRID LINES  
  IF KEY(XTICKV) OR KEY(YTICKV) THEN BEGIN
    IF KEY(XTICKV) THEN FOR I=0, NOF(XTICKV)-1 DO PLT_LINE,X=XTICKV(I), OBJ=OBJ, COLOR=COLOR, THICK=THICK, LINESTYLE=LINESTYLE
    IF KEY(YTICKV) THEN FOR I=0, NOF(YTICKV)-1 DO PLT_LINE,Y=YTICKV(I), OBJ=OBJ, COLOR=COLOR, THICK=THICK, LINESTYLE=LINESTYLE
  ENDIF ELSE BEGIN    
    OBJ.XTICKLEN = 1
    OBJ.YTICKLEN = 1
    OBJ.XSUBTICKLEN = KEY(SUBX)
    OBJ.YSUBTICKLEN = KEY(SUBY)
  ENDELSE;  IF KEY(XTICKV) OR KEY(YTICKV) THEN BEGIN

  DONE:          
END; #####################  END OF ROUTINE ################################

