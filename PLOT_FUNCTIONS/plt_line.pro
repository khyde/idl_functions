; $ID:	PLT_LINE.PRO,	2020-07-08-15,	USER-KJWH	$
;#########################################################################
 PRO PLT_LINE,X=X,Y=Y, OBJ=OBJ,COLOR=COLOR,THICK = THICK, LINESTYLE=LINESTYLE,_EXTRA = _EXTRA
;+
; NAME:
;       PLT_LINE
;
; PURPOSE:
;       OVERPLOT A LINE ATOP AN EXISTING PLOT
;
; CATEGORY:
;      PLOT
;
; CALLING SEQUENCE:
;       PLT_LINE

;
; INPUTS:
;         X  
;         Y  YCORDS OF LINE
;       OBJ: PLOT OBJECT[REQUIRED]
;
; KEYWORD PARAMETERS:
;        X ........... XCORDS OF LINE
;        Y ........... YCORDS OF LINE
;        OBJ.......... PLOT OBJECT
;        COLOR........ COLOR FOR THE GRIDS
;        THICK ....... THICKNESS OF THE  LINE
;        LINESTYLE.... LINESTYLE FOR THE LINE
;        _EXTRA....... MAY CONTAIN ANY IDL KEYWORDS WHICH ARE VALID FOR THE PLOT COMMAND.
;
; OUTPUTS: OVERPLOTS A LINE ATOP EXISTING PLOT
;
;      
;
; RESTRICTIONS:
;       ASSUMES YOU HAVE ALREADY ISSUED A PLOT COMMAND [AND HAVE THE OBJECT].
;       CONSTRAINS X,Y TO OBJ.XRANGE AND OBJ.YRANGE TO PREVENT ANY SCALE CHANGES IN THE ORIGINAL PLOT
;
; EXAMPLE:
;      XTICKNAME = (GET_LOG_TICKS([0.001,1000],COMMA = 0)).TICKNAME &  XTICKV = FLOAT(XTICKNAME)
;      X = XTICKV & Y = X & XTICKNAME = STAGGER(XTICKNAME,/BELOW)
;      PLT = PLOT(X,Y,XTICKNAME=XTICKNAME,XTICKV=XTICKV,/XLOG,/YLOG,COLOR = 'BLUE',THICK = 5,XMINOR = 0,YMINOR = 0,FONT_SIZE = 9)
;      PLT_LINE,X=[1,1],Y = [0.001,1000],OBJ=PLT,COLOR = 'GREEN',THICK = 3  
;
; MODIFICATION HISTORY:
;       APR 23, 2018 WRITTEN BY: J.E.O'REILLY
;       MAY 09, 2018 - KJWH: Formatting
;-

; ********************
  ROUTINE = 'PLT_LINE'
; ********************

;===> DEFAULTS:
  IF NONE(COLOR) THEN COLOR = 'BLACK'
  IF NONE(LINESTYLE) THEN LINESTYLE = 0
  IF NONE(THICK) THEN THICK = 1


; ===> CHECK FOR THE PLOT OBJECT 
  IF N_ELEMENTS(OBJ) EQ 0 THEN MESSAGE,'ERROR: PLOT OBJECT IS REQUIRED'
  IF IDLTYPE(OBJ) NE 'OBJREF' THEN MESSAGE,'ERROR: OBJ MUST BE A PLOT OBJECT'

; ===> CONSTRAIN X AND Y WITHIN OBJ.XRANGE & Y WITHIN OBJ.YRANGE [SO ORIGINAL PLOT SCALING DOES NOT CHANGE]
  IF ANY(X) THEN  X =  OBJ.XRANGE[0]> X <  OBJ.XRANGE[1]  
  IF ANY(Y) THEN  Y =  OBJ.YRANGE[0]> Y <  OBJ.YRANGE[1]  
  
;CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
  CASE [1] OF
    NOF(X) EQ 1 AND NONE(Y):        PLT =  PLOT([X,X],[OBJ.YRANGE],COLOR = COLOR,THICK = THICK,LINESTYLE=LINESTYLE,/CURRENT,/OVERPLOT,_EXTRA = _EXTRA)
    NOF(Y) EQ 1 AND NONE(X):        PLT =  PLOT([OBJ.XRANGE],[Y,Y],COLOR = COLOR,THICK = THICK,LINESTYLE=LINESTYLE,/CURRENT,/OVERPLOT,_EXTRA = _EXTRA)
    NOF(X) EQ 2 AND  NOF(Y) EQ 2 :  PLT =  PLOT([X[0],X[1]],[Y[0],Y[1]],COLOR = COLOR,THICK = THICK,LINESTYLE=LINESTYLE,/CURRENT,/OVERPLOT,_EXTRA = _EXTRA)
    NOF(X) GT 2 AND  NOF(Y) GT 2 :  PLT =  PLOT(X,Y,COLOR = COLOR,THICK = THICK,LINESTYLE=LINESTYLE,/CURRENT,/OVERPLOT,_EXTRA = _EXTRA) 
    ELSE: BEGIN & END
  ENDCASE
;CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

  DONE:          

END; #####################  END OF ROUTINE ################################

