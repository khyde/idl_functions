; $ID:	SUBAREAS_PLOT.PRO,	2020-07-01-12,	USER-KJWH	$
;+
;#############################################################################################################
	PRO SUBAREAS_PLOT,DIR_IN=DIR_IN,DIR_OUT=DIR_OUT,OVERWRITE=OVERWRITE,BUFFER=BUFFER
;
; PURPOSE: PLOTS TIME-SERIES DATA FROM SUBAREAS_EXTRACT 
;          
; CATEGORY:	SUBAREAS FAMILY
;
; CALLING SEQUENCE: SUBAREAS_PLOT,DIR_IN=DIR_IN,DIR_OUT=DIR_OUT,OVERWRITE=OVERWRITE,BUFFER=BUFFER
;
; INPUTS: DIR_IN
;		
; OPTIONAL INPUTS:
;		NONE:	
;		
; KEYWORD PARAMETERS:
;             DIR_IN.......... DIRECTORY FOR THE OUTPUT FROM SUBAREAS_EXTRACT
;             DIR_OUT......... DIRECTORY FOR THE OUTPUT FROM SUBAREAS_PLOT
;             OVERWRITE....... OVERWRITE PNG IF EXISTS
;             BUFFER.......... DRAW PLOT IN BUFFER 
; OUTPUTS: 
;            A PNG PLOT FOR EACH UNIQUE REGION-SUBAREA COMBINATION IN SUBAREAS_EXTRACT.CSV
;   
;		
;
; MODIFICATION HISTORY:
;			JUN 01, 2014,WRITTEN BY J.O'REILLY 
;			JUN 02, 2014 - JEOR:  ADDED COMMON BLOCK TO SPEED UP PROCESSING
;			JUN 21, 2014 - JEOR:  ADDED SUBAREA_DATE
;			JUN 27, 2014 - JEOR:  ADDED KEYWORDS
;			APR 07, 2015 - KJWH: MADE SURE X & Y INPUTS TO PLT_XY WERE FLOAT
;			FEB 02, 2017 - JEOR: MODIFIED TO DEAL WITH NEW TAGS IN SUBAREAS_EXTRACT.CSV
;     FEB 04, 2017 - JEOR: IF PROD EQ 'CHLOR_A' THEN DAT = D.GMEAN ELSE DAT = D.AMEAN
;     FEB 08, 2017 - JEOR: ADDED KEYS OVERWRITE,BUFFER
;     FEB 10, 2017 - JEOR: DOCUMENTATION
;     FEB 18, 2017 - JEOR: ADDED ,/GRID_ADD TO PLT_XY
;						
;			
;			
;#################################################################################
;-
;**************************
ROUTINE  = 'SUBAREAS_PLOT'
;**************************
IF NONE(BUFFER) THEN BUFFER = 1
IF NONE(OVERWRITE) THEN _OVERWRITE = 0  ELSE _OVERWRITE = OVERWRITE
IF NONE(DIR_IN) THEN DIR_IN = !S.IDL_TEMP
IF NONE(DIR_OUT) THEN DIR_OUT = !S.IDL_TEMP
;===> INPUT
CSVFILE = DIR_IN + 'SUBAREAS_EXTRACT' + '.CSV'
DB = CSV_READ(CSVFILE)
SETS = WHERE_SETS(DB.REGION,DB.SUBAREA,/JOIN)


;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
FOR SET_ = 0,N_ELEMENTS(SETS) -1 DO BEGIN
  SET = SETS(SET_)
  SUBS = WHERE_SETS_SUBS(SET)
  D = DB(SUBS)
  F = STRUCT_2FLT(D)
  PROD   = FIRST(D.PROD)  
  MAP    = FIRST(D.MAP)
  REGION = FIRST(D.REGION)
  SUBAREA = FIRST(D.SUBAREA)
  SENSOR = FIRST(D.SENSOR)
  ALG    = FIRST(D.ALG)
  MTIME_SUBAREA = FIRST(D.MTIME_SUBAREA)
  OUTFILE = DIR_OUT + ROUTINE + '-' + SUBAREA +  '.PNG'
  ;===> SKIP ?
  IF  (MTIME_SUBAREA GT GET_MTIME(OUTFILE) AND _OVERWRITE EQ 0) EQ 0 THEN BEGIN
    PFILE,OUTFILE,/K
    CONTINUE;>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
  ENDIF;IF  (MTIME_SUBAREA GT GET_MTIME(OUTFILE) AND NOT KEY(OVERWRITE)) THEN BEGIN
  ;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

  DAT = (PROD EQ 'CHLOR_A') ? D.GMEAN : D.AMEAN  
  DATE    = PERIOD_2DATE(D.PERIOD)
  MONTHS  = DATE_2MONTH(DATE)
  YEARS   = DATE_2YEAR(DATE)
  IF SPAN(YEARS) GT 2  THEN BEGIN
    X = ROUNDS(DATE_2YEAR(DATE),1) 
    XTITLE = 'YEAR'
    AX = DATE_AXIS(DATE,/MONTH)
    XTICKNAME = AX.TICKNAME
    XTICKV = AX.TICKV
    X = AX.JD
  ENDIF ELSE BEGIN
    X = FLOAT(MONTHS)
    XTITLE = 'MONTH'
    XTICKNAME= MONTH_NAMES(MONTHS,/SHORT)+ '!C' + ROUNDS(YEARS)
  ENDELSE;IF SPAN(YEARS) GT 2  THEN BEGIN
  X = FLOAT(X(SORT(X)))
  Y = FLOAT(DAT)
  XRANGE = MM(X)
  TITLE = 'MAP: ' + MAP +',   REGION:   ' + REGION +',  SENSOR: ' + SENSOR + ',   SUBAREA:   ' + SUBAREA $
          + ',   PROD:  '+ PROD + ',   ALG: ' + ALG
  
  PLT_XY,X,Y,XTITLE = XTITLE,YTITLE = PROD,TITLE = TITLE,$
    POSITION = [0.2,0.2,.95,.35],AXES_FONT_SIZE = 6,$
    XRANGE = XRANGE,XTICKV=XTICKV,XTICKNAME = XTICKNAME,/LIN_ADD,LIN_COLOR = 'BLUE',FILE=OUTFILE,$
   BUFFER=BUFFER,APPEND = 1,XMINOR = 0,CLOSE=CLOSE,/GRID_ADD
  PFILE,OUTFILE
  PRINT
ENDFOR;FOR SET_ = 0,N_ELEMENTS(SETS) -1 DO BEGIN
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF




END; #####################  END OF ROUTINE ################################
