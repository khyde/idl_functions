; $Id:	psprint.pro,	September 11 2006, 08:27	$

  PRO PSPRINT, VECTOR=vector, FULL=full,HALF=half, SPECIAL=special,CENTER=center, $
               SCALE=scale,HELP=help,IMAGE=image, _EXTRA=_extra, PNG=PNG

;+
; NAME:
;       psprint
;
; PURPOSE:
;				Initializes Postscript device to defaults in IDL 6.0 AND
;       Directs Graphic Output to a Postscript (or Encapsulated Postscript) File,
;				and Configures Postscript Device according to keywords supplied as well as
;				valid DEVICE commands for postscript device (see IDL Help for the PostScript Device)
;
; CATEGORY:
;       Printing
;
; CALLING SEQUENCE:
;       Call PSPRINT to initialize PostScript Device to defaults in IDL 6.0
;       Issue Graphics Commands
;       Call PSPRINT (with no other keywords) to close Postscript File.
;
;				OPTIONAL:
;       Call PSPRINT with /help to print the Current PostScript Device settings
;
;
; EXAMPLES:
;       PSPRINT & PLOT,[1,2,3,4] & PSPRINT

;       PSPRINT,/COLOR,/EPS,/HELP & PLOT,[1,2,3,4] & PSPRINT

; INPUTS:
;       None Required (default postscript output =idl.ps)
;
; KEYWORD PARAMETERS:
;       Keywords:

;         _EXTRA: (any valid DEVICE commands for postscript device may be provided
;                  see IDL Help for the PostScript Device)
;
;         VECTOR:  Make the default font the IDL vector font instead of this program's
;                  default (/TIMES).
;
;
;
; ===============>
; Examples of some of the additional keyword commands for PostScript Files
; (You can pass any of these commands to this program).
;
;Type of Output Postscript File
; PostScript       is the Default unless ENCAPSULATED is used.
; ENCAPSULATED
;                  Set this keyword to create an encapsulated PostScript file,
;                  suitable for importing into another document.
;
;Name of Output PostScript File:
; FILENAME:      Normally, all generated output is sent to a file named idl.ps
;                unless a file name is provided.
;                If no filename provided and /ENCapsulated then output is idl.eps
;
;Page Orientation:
; PORTRAIT       Is the Default Unless LANDSCAPE is used
; LANDSCAPE:     Orientation (abscissa along the long dimension of the page) is used.
;
;Size and Position of Plot on Page
; XSIZE:         Specifies the  width of output generated by IDL.in centimeters,
; YSIZE:         Specifies the height of output generated by IDL in centimeters,
; XOFFSET:       Specifies the X position, on the page
; YOFFSET:       Specifies the Y position, on the page
;
;Units
; INCHES:        Normally, the XOFFSET, XSIZE, YOFFSET, and YSIZE keywords are specified in centimeters.
;                However, if INCHES is present and non-zero, they are taken to be in inches instead.
;
;Color of Plot:
; COLOR: Enables color and enables the color tables.
;
; BITS_PER_PIXEL:IDL is capable of producing PostScript images with 1, 2, 4, or 8 bits per pixel.
;                (Use 1 for Black&White; 5 For 32 Gray Shades; 8 for 256 color pages.
;                (Can use bits=1 for black and white, bits=5, etc)

;Font Thickness
; LIGHT:         Set this keyword to specify that the light version of the current PostScript font should be used.
; MEDIUM:        Set this keyword to specify that the medium version of the current PostScript font should be used.
; BOLD:          Set this keyword to specify that the bold version of the current PostScript font should be used.
; NARROW;        Set this keyword to specify that the narrow version of the current PostScript font should be used.
; OBLIQUE;       Set this keyword to specify that the oblique version of the current PostScript font should be used.

;Font Styles     (See IDL Help for all available PostScript Fonts)
; AVANTGARDE:    Set this keyword to select the ITC Avant Garde PostScript font.
; COURIER:       Set this keyword to select the Courier PostScript font.
; HELVETICA:     Set this keyword to select the Helvetica PostScript font.
; SYMBOL:        Set this keyword to select the Symbol PostScript font.
; TIMES:         Set this keyword to select the Times-Roman PostScript font.

;Other
; PREVIEW:Set this keyword to add a "device independent screen preview" to the PostScript output file,
;	PRE_XSIZE: Size of preview PRE_XSIZE is specified in centimeters
;	PRE_YSIZE: PRE_YSIZE is specified in centimeters
;
; OUTPUTS:
;      The results from your graphics commands are sent to the PostScript File
;
; SIDE EFFECTS:
;
;      Changes the IDL !PROMPT to 'PS' the first call then to previous !prompt
;      when PSPRINT is called without any other instructions or keywords.
;
;      Sets !P.FONT to 0, making hardware fonts the default.
;      You can make the IDL vector the default font by using the keyword /vector
;
;      These settings you choose for the PS device will remain in effect until you restart a new IDL session
;      
;      Creates !PSPRINT_FILENAME, !PSPRINT_PNG system variables to keep track of desired behavior, /PNG keyword.
;
; NOTES:
;         DEFAULT is 1 bit color (black&white), Times Roman Font
;
; RESTRICTIONS:
;
;
; MODIFICATION HISTORY:
;       J.O'Reilly , August 11,1997
;       jor  June 28,2001  added keyword image
;       05/09/2011 D.W.Moonan add PNG keyword, added !PSPRINT_FILENAME, !PSPRINT_PNG system variables.
;
;-
;-------------------------------------------------------------


; ====================>
; Set output device to PostScript
;
; If current graphics device is not PostScript then SET_PLOT='PS'.
; If it is already PostScript then Close and restore the !PROMPT
; (but if HELP then leave open and display settings)
  IF !D.NAME NE 'PS' THEN BEGIN
    !PROMPT = !D.NAME + '>PSPRINT'
    SET_PLOT,'PS'
;		===> Re SET PostScript Device to Defaults for idl 6.0
; 		 File: <none>
;   Mode: Portrait, Non-Encapsulated, EPSI Preview Disabled, Color Disabled
		DEVICE, /Portrait, ENCAPSULATED=0, PREVIEW =0, Color = 0
;   Offset (X,Y): (1.905,12.7) cm., (0.75,5) in.
    DEVICE, XOFFSET=0.75, YOFFSET=5,/INCH
;   Size (X,Y): (17.78,12.7) cm., (7,5) in.
		DEVICE, XSIZE=7,YSIZE=5,/INCH
;   Scale Factor: 1
		DEVICE,SCALE_FACTOR=1
;   Preview Size (X,Y): (4.51556,4.51556) cm., (1.77778,1.77778) in.
		DEVICE,PRE_XSIZE=1.77778, PRE_YSIZE=1.77778,/INCH
;   Preview Depth: 8 bits per pixel
		DEVICE,PRE_DEPTH=8
;   Font Size: 12
		DEVICE,FONT_SIZE=12
;	  Font: Helvetica	TrueType Font: <default>
		DEVICE,/HELVETICA
;	  # bits per image pixel: 4
    DEVICE,BITS_PER_PIXEL=4
;		Set default font to idl vector
    !P.FONT = -1
  ENDIF ELSE BEGIN
    POS = STRPOS(!PROMPT,">PSPRINT")
    IF POS THEN BEGIN
;     Close the PostScript File only if PSPRINT is given with no qualifiers
;     Check if any commands to device were given or if HELP is used
      done = N_ELEMENTS(VECTOR) + N_ELEMENTS(PAGE) + N_ELEMENTS(HELP) + N_ELEMENTS(_EXTRA) EQ 0
      IF done THEN BEGIN ;If done is 1 (true) then close the postscript file
        EMPTY
        DEVICE,/CLOSE
        SET_PLOT,STRMID(!PROMPT,0,pos)
        !PROMPT = 'IDL>'
        IF !PSPRINT_PNG EQ 1 THEN BEGIN
          print, 'Creating PNG file per request for file ' + !PSPRINT_FILENAME
          PS_2PNG, !PSPRINT_FILENAME
        ENDIF
        RETURN
      ENDIF
    ENDIF
  ENDELSE



  FILENAME= 'IDL.PS'

; ===> Set the Default Font for this program (/TIMES)
; 		or the IDL Vector Font ?
  IF NOT KEYWORD_SET(VECTOR) THEN BEGIN
    !P.FONT = 0
  ENDIF ELSE BEGIN
    !P.FONT = -1
  ENDELSE


; ===> Enable a Full Page
  IF KEYWORD_SET(FULL) THEN BEGIN
    _page 	=	1
    XOFFSET	= .5
    YOFFSET	= .75
    XSIZE		=	8.1   -	XOFFSET
    YSIZE		=	10.6 	-	YOFFSET

    DEVICE,XOFFSET= XOFFSET,/INCH
    DEVICE,YOFFSET= YOFFSET,/INCH  ; centimeters

    DEVICE,XSIZE=XSIZE ,/INCH
    DEVICE,YSIZE=YSIZE ,/INCH
  ENDIF


  IF KEYWORD_SET(SPECIAL) THEN BEGIN
    IF SPECIAL EQ 'TM' THEN DEVICE, xsize=6.0*2.54,ysize=7.776*2.54
  ENDIF

  IF N_ELEMENTS(SCALE) NE 1 THEN _SCALE = 1.0D ELSE _SCALE = SCALE
  DEVICE,SCALE_FACTOR=_SCALE


; ===> Check _EXTRA for Device commands
  IF KEYWORD_SET(_EXTRA) THEN BEGIN
    tags = TAG_NAMES(_EXTRA)

;   color ?
    ok = WHERE(STRMID(tags,0,3) EQ 'COL',count)
    IF count GE 1 THEN BEGIN
      DEVICE, BITS_PER_PIXEL=8,/COLOR
    ENDIF

;   Encapsulated PostScript ?
    ok = WHERE(STRMID(tags,0,3) EQ 'ENC' OR STRMID(tags,0,3) EQ 'EPS',count)
    IF count GE 1 THEN BEGIN
;		PRE_XSIZE,PRE_YSIZE ARE IN CM
    pre_xsize=(!P.CLIP(2)-!P.CLIP(0))/1000.D & pre_Ysize=(!P.CLIP(3)-!P.CLIP(1))/1000.D

    DEVICE, /ENCAPSULATED,BITS_PER_PIXEL=8,/COLOR,PREVIEW=2,PRE_DEPTH=8,$
     				PRE_XSIZE=PRE_XSIZE,PRE_YSIZE=PRE_YSIZE,FILENAME='idl.eps'
    ENDIF

;   PostScript ?
    ok = WHERE(STRMID(tags,0,3) EQ 'PS',count)
    IF count GE 1 THEN BEGIN
     DEVICE, ENC=0,FILENAME='idl.ps'
    ENDIF

;   Encapsulated PostScript ?
    ok = WHERE(STRMID(tags,0,3) EQ 'FIL',count)
    IF count GE 1 THEN BEGIN
     FILENAME = _EXTRA.(OK(0))

     DEVICE,FILENAME=FILENAME
    ENDIF

    DEFSYSV, '!PSPRINT_PNG', EXISTS = exists
    IF EXISTS eq 0 then begin
      DEFSYSV,'!PSPRINT_PNG', 0
    ENDIF ELSE BEGIN
      !PSPRINT_PNG = 0
    ENDELSE
    
    IF KEYWORD_SET(PNG) THEN $ 
      !PSPRINT_PNG = 1 $
    ELSE $
      !PSPRINT_PNG = 0

    DEFSYSV, '!PSPRINT_FILENAME', EXISTS = exists
    IF exists EQ 0 THEN BEGIN
      DEFSYSV, '!PSPRINT_FILENAME', FILENAME
    ENDIF ELSE BEGIN
      !PSPRINT_FILENAME=FILENAME
    ENDELSE
   
;   PORTRAIT ?
    ok = WHERE(STRMID(tags,0,3) EQ 'POR',count)
    IF count GE 1 THEN BEGIN
     DEVICE,PORTRAIT=1
    ENDIF

;   LANDSCAPE ?
    ok = WHERE(STRMID(tags,0,3) EQ 'LAN',count)
    IF count GE 1 THEN BEGIN
     DEVICE,LANDSCAPE=1
    ENDIF

;   XOFFSET ?
    ok = WHERE(STRMID(tags,0,4) EQ 'XOFF',count)
    IF count GE 1 THEN BEGIN
      XOFFSET =  _EXTRA.(OK(0))
      DEVICE,XOFFSET=XOFFSET
    ENDIF

;   YOFFSET ?
    ok = WHERE(STRMID(tags,0,4) EQ 'YOFF',count)
    IF count GE 1 THEN BEGIN
      YOFFSET =  _EXTRA.(OK(0))
      DEVICE,YOFFSET=YOFFSET
    ENDIF

  ENDIF ;  IF KEYWORD_SET(_EXTRA) THEN BEGIN


; ===> Check _Extra and supply any extra instructions to
; the PostScript Device
  IF KEYWORD_SET(_EXTRA) THEN DEVICE, _EXTRA=_extra


	IF KEYWORD_SET(IMAGE) THEN BEGIN
     S = SIZE(IMAGE)  & PX = S(1) & PY = S(2)
    IF PX LT 2 OR PY LT 2 THEN GOTO, SKIP        ; >>>>>>>>>>>>>>>.
    IF N_ELEMENTS(XOFFSET) NE 1 THEN XOFFSET = 0
    IF N_ELEMENTS(YOFFSET) NE 1 THEN YOFFSET = 0
;  	===> Postscript (idl) has 1000/cm resolution
		image_xsize = px*_SCALE*(2.54D/1000.)
		image_ysize = py*_SCALE*(2.54D/1000.)

;  	===> If keyword full then calculate scale without changing x:y aspect ratio
    IF KEYWORD_SET(full) NE 0 THEN BEGIN
      scale_x  = ((((!d. X_SIZE /!d.x_px_cm)/2.54)-xoffset) /image_xsize)
      scale_y  = ((((!d. Y_SIZE /!d.y_px_cm)/2.54)-yoffset) /image_ysize)
      _scale = (scale_x < scale_y)
       xsize =   _SCALE*image_xsize
   	   ysize =   _SCALE*image_xsize
    ENDIF ELSE BEGIN
      xsize =   image_xsize
   	  ysize =   image_xsize
   	ENDELSE
   	DEVICE,XSIZE=xsize,YSIZE=ysize,/inch
   	TV,IMAGE
  ENDIF
  
SKIP:
; ===> Display Current Graphics setings
  PRINT_HELP:
  IF KEYWORD_SET(HELP) THEN BEGIN
    HELP,/DEVICE
  ENDIF ELSE BEGIN
    IF !D.NAME EQ 'PS' THEN  !PROMPT = !PROMPT
  ENDELSE

  END
