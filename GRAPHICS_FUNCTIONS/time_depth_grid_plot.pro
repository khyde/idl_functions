; $ID:	TIME_DEPTH_GRID_PLOT.PRO,	2023-09-21-13,	USER-KJWH	$
  PRO TIME_DEPTH_GRID_PLOT, $
        ; Data inputs
        DATES, DEPTHS, ARRAY, $  
        ; Interpolation parameters
        DATERANGE=DATERANGE, DEPTH_RANGE=DEPTH_RANGE, DEPTH_INTERVAL=DEPTH_INTERVAL, DEPTH_CUTOFF=DEPTH_CUTOFF, $
        NEAR=NEAR,INTERPOLATE=INTERPOLATE, MAKE_MISSING=MAKE_MISSING,DIMENSIONS=DIMENSIONS,$
        ; Plotting parameters
        AX_INTERVAL=AX_INTERVAL, NOXDATES=NOXDATES,$
        YTITLE=YTITLE, YT_ORIENTATION=YT_ORIENTATION,$
        PROD=PROD, LOG=LOG, $
        PAL=PAL, COLOR_RANGE=COLOR_RANGE, MISSING_COLOR=MISSING_COLOR,$
        FONT_SIZE=FONT_SIZE, MARGIN=MARGIN,$
        CURRENT=CURRENT, BUFFER=BUFFER 
                       
;+
; NAME:
;   TIME_DEPTH_GRID_PLOT
;
; PURPOSE:
;   Plot the gridded data generated by TIME_DEPTH_INTERPOLATE
;
; CATEGORY:
;   GRAPHICS_FUNCTIONS
;
; CALLING SEQUENCE:
;   TIME_DEPTH_PLOT, GRID
;
; REQUIRED INPUTS:
;   DATES........... The date array (yyyymmdd or yyyymmddhhmmss)
;   DEPTHS.......... The depth array
;   ARRAY........... The data array
;
; OPTIONAL INPUTS:
;   DATERANGE....... Two element array of the minimum and maximum dates
;   DEPTH_RANGE..... The two element array of the minimum and maximum depths
;   DEPTH_INTERVAL.. The depth interval to fill in the missing data with (0.5 meters, 2 meters, etc.)
;   DEPTH_CUTOFF.... To "cutoff" the top and bottom depths
;   NEAR............ Input variable to WHERE_NEAREST for filling in the new structure
;   DIMENSIONS...... Input variable to GRIDDATA
;   
;   PROD............ The product name (with or without the data range)
;   AX_INTERVAL..... The xaxis interval for DATE_AXIS (e.g. 'DAY, 'MONTH', 'YEAR')
;   YTITLE.......... The text for the Y axis label
;   YT_ORIENTATION.. The orientation for the Y axis lable
;   PAL............. The color palette
;   COLOR_RANGE..... The range of the colorbar scaling (default=[1,250])
;   MISSING_COLOR... The palette color number for "missing" data
;   FONT_SIZE....... The font size for the plots
;
; KEYWORD PARAMETERS:
;   INTERPOLATE..... To interpolate values within the profile
;   MAKE_MISSINGS... Keyword input into INTERP_XTEND
;   LOG............. To plot the data on a log scale
;   NOXDATES........ To omit the XTICK lables
;   CURRENT......... To add the image to an existing graphics window
;   BUFFER.......... To hide the graphics in the background
;   
; OUTPUTS:
;   A plot of the gridded timeseries profile data
;
; OPTIONAL OUTPUTS:
;   None
;
; COMMON BLOCKS: 
;   None
;
; SIDE EFFECTS:  
;   None
;
; RESTRICTIONS:  
;   None
;
; EXAMPLE:
;   N = 10 & DATES = REPLICATE('20210101000000',N) & DEPTH = [] & VAR = []
;   FOR D=1, 365/5-1 DO DATES = [DATES,REPLICATE(JD_2DATE(JD_ADD(DATE_2JD(20210101),D*5,/DAY)),N)]
;   FOR D=1, 365/5 DO DEPTH = [DEPTH,SORTED(FLOAT(100*RANDOMU(SEED, N)))]
;   FOR D=1, 365/5 DO VAR = [VAR,FLOAT(30*RANDOMU(SEED,N))]
;   TIME_DEPTH_GRID_PLOT, DATES, DEPTH, VAR
; 
;
; NOTES:
;   The input data are first interpolated into a grid using TIME_DEPTH_INTERPOLATION
;   
; COPYRIGHT: 
; Copyright (C) 2022, Department of Commerce, National Oceanic and Atmospheric Administration, National Marine Fisheries Service,
;   Northeast Fisheries Science Center, Narragansett Laboratory.
;   This software may be used, copied, or redistributed as long as it is not sold and this copyright notice is reproduced on each copy made.
;   This routine is provided AS IS without any express or implied warranties whatsoever.
;
; AUTHOR:
;   This program was written on January 03, 2022 by Kimberly J. W. Hyde, Northeast Fisheries Science Center | NOAA Fisheries | U.S. Department of Commerce, 28 Tarzwell Dr, Narragansett, RI 02882
;    
; MODIFICATION HISTORY:
;   Jan 03, 2022 - KJWH: Initial code written - adapted from TIME_DEPTH_GRID_NG
;-
; ****************************************************************************************************
  ROUTINE_NAME = 'TIME_DEPTH_GRID_PLOT'
  COMPILE_OPT IDL2
  SL = PATH_SEP()


; ===> Create the interpolated grid  
  GRID = TIME_DEPTH_INTERPOLATION(DATE_ARRAY, DEPTH_ARRAY, ARRAY, $
    DATERANGE=DATERANGE, DEPTH_RANGE=DEPTH_RANGE, DEPTH_INTERVAL=DEPTH_INTERVAL, DEPTH_CUTOFF=DEPTH_CUTOFF, $
    NEAR=NEAR,INTERPOLATE=INTERPOLATE, MAKE_MISSING=MAKE_MISSING,DIMENSIONS=DIMENSIONS)

; ===> Plotting defaults  
  IF N_ELEMENTS(PROD) EQ 1 THEN PRD = PROD ELSE BEGIN
    IF KEYWORD_SET(LOG) THEN PRD = 'LNUM' ELSE PRD = 'NUM'
    PRD = STRJOIN([PRD,ROUNDS(NICE_RANGE(ARRAY),1)],'_')
  ENDELSE
  
  IF ~N_ELEMENTS(AX_INTERVAL) THEN AX_INTERVAL = 'MONTH'
  IF ~N_ELEMENTS(PAL)  THEN PAL = 'PAL_DEFAULT'
  IF ~N_ELEMENTS(MISSING_COLOR) THEN MISSING_COLOR = 255
  IF N_ELEMENTS(COLOR_RANGE) NE 2 THEN COLOR_RANGE = [1,250]
  IF ~N_ELEMENTS(FONT_SIZE) THEN FONT_SIZE = 14

  MDATE = [MIN(DATERANGE),MAX(DATERANGE)]

  CASE AX_INTERVAL OF
    'SECOND'  : AX = DATE_AXIS(DATE_2JD(MDATE),/SECOND)
    'MINUTE'  : AX = DATE_AXIS(DATE_2JD(MDATE),/MINUTE)
    'HOUR'    : AX = DATE_AXIS(DATE_2JD(MDATE),/HOUR)
    'DAY'     : AX = DATE_AXIS(DATE_2JD(MDATE),/DAY)
    'MONTH'   : AX = DATE_AXIS(DATE_2JD(MDATE),/MONTH)
    'YEAR'    : AX = DATE_AXIS(DATE_2JD(MDATE),/YEAR)
  ENDCASE
  IF KEYWORD_SET(FYEAR) AND AX_INTERVAL EQ 'MONTH' THEN AX = DATE_AXIS(DATE_2JD(MDATE),/MONTH,/FYEAR)

  IF ~KEYWORD_SET(NOXDATES) THEN XTICKNAME=AX.TICKNAME ELSE XTICKNAME = REPLICATE('',N_ELEMENTS(AX.TICKNAME))
  XMAJOR=AX.TICKS
  XTICKVALUES=AX.TICKV
  XRANGE = [MIN(AX.JD),MAX(AX.JD)]
    
  IMG = BYTE(GRID) & IMG[*,*]=0
  FIN = FINITE(GRID)
  OK = WHERE(FIN EQ 1,COUNT, COMPLEMENT=COMP, NCOMPLEMENT=NCOMP)
  IF COUNT GE 1 THEN IMG[OK] = PRODS_2BYTE(GRID[OK],PROD=PRD, CB_RANGE=COLOR_RANGE) 
  IF NCOMP GE 1 THEN IMG[COMP] = MISSING_COLOR

  IM = IMAGE(IMG,RGB_TABLE=CPAL_READ(PAL),AXIS_STYLE=2,LAYOUT=LAYOUT,XMAJOR=0,YMAJOR=0,XMINOR=0,YMINOR=0,CURRENT=CURRENT,BUFFER=BUFFER,MARGIN=MARGIN,TITLE=TITLE,YTITLE='',FONT_SIZE=FONT_SIZE)
  XAX = AXIS('X',LOCATION=[0,0],TICKNAME=XTICKNAME[0:-1],MAJOR=XMAJOR,MINOR=XMINOR,TICKVALUES=[JD_2DOY(XTICKVALUES[0:-2]),MAX(IM.XRANGE)],TITLE=XTITLE,TICKLEN=0.02,TARGET=IM,TICKFONT_SIZE=FONT_SIZE)
  IF KEYWORD_SET(YTITLE) THEN TXT = TEXT(MIN(IM.XRANGE)-1,MEAN(IM.YRANGE),YTITLE,ALIGNMENT=1,VERTICAL_ALIGNMENT=0.5,CLIP=0,COLOR='BLACK',FONT_SIZE=FONT_SIZE,/DATA,TARGET=IM,ORIENTATION=YT_ORIENTATION)

END ; ***************** End of TIME_DEPTH_PLOT *****************
