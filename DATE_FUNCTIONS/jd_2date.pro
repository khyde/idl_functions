; $ID:	JD_2DATE.PRO,	2020-06-30-17,	USER-KJWH	$
;##########################################################################################
FUNCTION JD_2DATE ,JDS, ERROR=ERROR
;+
; NAME:
;		JD_2DATE
; INPUTS:
;		JULIAN DAY
; PURPOSE:  THIS FUNCTION CONVERTS JULIAN DAY INTO DATE (YYYYMMDDHHMMSS) 
;
; CATEGORY:
;		DATE FAMILY
;
; CALLING SEQUENCE:
;		RESULT = JD_2DATE(JD)
;
; INPUTS:
;		JD:	JULIAN DATE (SEE IDL HELP)
;
; OUTPUTS:
;		DATE STRING FORMATTED AS YYYYMMDDHHMMSS
;		
;	EXAMPLES:
;	PRINT,(JD_2DATE(DATE_2JD(DATE_NOW())))
;
; OPTIONAL OUTPUTS:
;		ERROR:     ANY ERROR MESSAGES ARE PLACED IN ERROR, IF NO ERRORS THEN ERROR = ''
;
; EXAMPLE:
;		    PRINT, JD_2DATE(DATE_2JD(DATE_NOW()))
;
; MODIFICATION HISTORY:
;       WRITTEN BY:  J.E.O'REILLY, SEPT 29,2000
;				APRIL 8, 2003 REPLACES STRING_FORMAT WITH STRING, JOR
;				FEB 22,2007 JOR CHECKS NOW FOR MISSING JDS
;				JUL 24, 2015 - KJWH: CHANGED JD TO JDS TO AVOID CONFLICT WITH THE JD PROGRAM
;				                     FIXED A BUG IN THE SEARCH FOR VALID JDS.  ORIGINALLY IT WAS LOOKING FOR NON-MISSING DATES INSTEAD OF JDS.
;				                       OK = WHERE(FINITE(JDS) AND DATE NE MISSINGS(JDATE),COUNT) WAS CHANGED TO...
;				                       OK = WHERE(FINITE(JDS) AND JDS  NE MISSINGS(JDS),COUNT)
;				 AUG 04,2015,JOR   : ADDED EXAMPLE, FORMATTING
;###############################################################################################				                         
;-
;**************************
ROUTINE_NAME = 'JD_2DATE'
;**************************
ERROR = ''
IF NONE(JDS) THEN BEGIN
	ERROR = 'MUST PROVIDE JULIAN DATE'
	RETURN,''
ENDIF

;===> MAKE A STRING ARRAY FOR OUTPUT
DATE = REPLICATE('',N_ELEMENTS(JDS))

;===> FIND THE VALID JULIAN DAYS
OK = WHERE(FINITE(JDS) AND JDS NE MISSINGS(JDS),COUNT)

;===> CONVERT JULIAN DAY TO DATE USING IDL FORMAT
IF COUNT GE 1 THEN DATE[OK] = STRING(JDS[OK],FORMAT='(C(CYI4.4,CMOI2.2,CDI2.2,CHI2.2,CMI2.2,CSI2.2))')

IF N_ELEMENTS(DATE) EQ 1 THEN RETURN, DATE[0] ELSE RETURN, DATE


END; #####################  END OF ROUTINE ################################

