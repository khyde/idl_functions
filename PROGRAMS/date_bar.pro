; $ID:	DATE_BAR.PRO,	2017-09-15-13,	USER-KJWH	$
;#########################################################################################3
FUNCTION DATE_BAR, DATE, DB_BKG=DB_BKG,DB_FONT_SIZE= DB_FONT_SIZE,DB_COLOR=DB_COLOR,BKGD_COLOR=BKGD_COLOR,FRAME_COLOR=FRAME_COLOR,TXT_COLOR=TXT_COLOR,DB_THICK=DB_THICK,NO_YEAR=NO_YEAR,NO_DAY=NO_DAY,$
PAL=PAL,PX=PX,PY=PY,DB_POS=DB_POS,DB_X=DB_X,DB_Y=DB_Y,DB_LONG=DB_LONG,DB_SHORT=DB_SHORT,_EXTRA=_EXTRA

;  KEYWORDS:
;           DB_BKG....... BACKGROUND OF THE DATE BAR [DEFAULT = GRAY 254]
;           DB_FONT_SIZE. FONT SIZE OF MONTH LETTERS,YEAR & DAY TEXT IN THE DATE BAR [DEFAULT = 16]
;           DB_COLOR..... COLOR OF THE DATE BAR'S VERTICAL LINE 
;           DB_THICK..... THICKNESS OF THE DATE BAR'S VERTICAL LINE 
;           NO_YEAR...... DO NOT ADD THE YEAR IN THE LEFTMOST BOX IN THE DATE BAR 
;           NO_DAY....... DO NOT ADD THE DAT IN THE RIGHTMOST BOX IN THE DATE BAR
;           PAL.......... PALETTE TO USE FOR THE  THE DATE BAR [DEFAULT = PAL_BR]
;           PX........... WIDTH IN PIXELS FOR THE DATE_BAR
;           PAL.......... HEIGHT IN PIXELS FOR THE DATE BAR 
;           DB_POS....... POSITION OF DATE_BAR IN NORMAL COORDINATES
;           DB_POS.......... NORMAL-COORDINATE POSITION [X,Y,X2,Y2]FOR THE DATEBAR
;           DB_X............ X NORMAL-COORDINATE FOR THE CENTRAL DATEBAR DB_POS [EASIER TO ESTIMATE THAN DB_POS]
;           DB_Y............ Y NORMAL-COORDINATE FOR THE CENTRAL DATEBAR DB_POS [EASIER TO ESTIMATE THAN DB_POS]
;           DB_LONG......... LONG DIMENSION [NORMAL COORDS] FOR THE DATEBAR [USE WITH DB_X,DB_Y]
;           DB_SHORT........ SHORT DIMENSION [NORMAL COORDS] FOR THE DATEBAR [USE WITH DB_X,DB_Y]
;           
; EXAMPLES: [FIRST IS AN OBJECT GRAPHICS, SECOND IS A DIRECT GRAPHICS]
; 
;        W = WINDOW() & D = DATE_BAR(DB_POS = [0.25,0.8,0.85,0.85]) & WAIT,5 & W.CLOSE
;        SLIDEW,DATE_BAR(JULIAN ,DB_BKG=DB_BKG,DB_COLOR=DB_COLOR,PX=1200,PY=90, CHARSIZE=5,CHARTHICK=1)
;        
;        THESE 3 EXAMPLE SSOWS THE USE OF DB_X,DB_Y & DB_LONG,DB_SHORT
;        W = WINDOW() & D = DATE_BAR(DB_X = 0.5,DB_Y = 0.5) & WAIT,5 & W.CLOSE
;        W = WINDOW() & D = DATE_BAR(DB_X = 0.5,DB_Y = 0.5,DB_LONG = 0.95,DB_SHORT = 0.65) & WAIT,5 & W.CLOSE
;        W = WINDOW() & D = DATE_BAR(DB_X = 0.5,DB_Y = 0.5,DB_LONG = 0.95,DB_SHORT = 0.05) & WAIT,5 & W.CLOSE
;        
;	MODIFICATION HISTORY:
;			WRITTEN BY J.O'REILLY, NOAA, FEB 26, 2004
;     SEP 12, 2014 - JEOR: UPDATED CODE ANDFORMATTING 
;                          FIXED BUG :MONTH BAR WAS OFF BY 1 MONTH BECAUSE:
;                          MONTHS = STRMID(MONTH_NAMES(),0,1) & MONTHS=MONTHS(1:*)
;                          FONT_HELVETICA
;     JAN 31, 2017 - JEOR: MADE LONG TO WORK WITH SIZEXYZ:IF NONE(PX)  THEN _PX =  LONG((!D.X_CH_SIZE*CHARSIZE) * (N_CHARS) * 1.66) ELSE _PX=PX;
;     AUG 21, 2017 - JEOR: MODIFIED TO WORK WITH BOTH DIRECT GRAPHICS AND NEW GRAPHICS; ADDED IS_OG FUNCTION
;     AUG 22, 2017 - JEOR: INCORPORATED CODE FROM KIM'S DATE_BAR_NG
;                          FIXED PROBLEM WITH THE YEAR AND DAY NOT DISPLAYING IN THE OG PLOT -120 & + 90 DAYS:
;                          P=PLOT([FIRST(JDS)-120,LAST(JDS)+90],[0,1],/CURRENT,XRANGE=XRANGE,YMAJOR=0,XMAJOR=0,YMINOR=0,XMINOR=0,AXIS_STYLE=2,BACKGROUND_COLOR=_BACKGROUND,DB_POS=DB_POS,XTHICK=OG_THICK,YTHICK=OG_THICK,COLOR='BLACK',/NODATA,DEVICE=DEVICE)
;                          UPDATED  EXAMPLES ,KEYWORDS 
;                          USE POLYGON TO COLOR THE BACKGROUND THE DB_BKG COLOR FOR OG 
;     AUG 28, 2017 - JEOR: ADDED KEYS DB_X,DB_Y,DB_LONG,DB_SHORT
;     SEP 16, 2017 - KJWH: Moved FONT_HELVITICA into the Direct Graphics option
;                          Updated formatting
;                          In the Object Graphics option, removed the POLYGON step - it is not necessary since the datebar is plotted directly on top of the image/window
;     JUL 03, 2018 - KJWH: Added BKGR_COLOR, FRAME_COLOR and TXT_COLOR keywords to control the color of the frame and the text
;                          Updated the Object Graphics section to better determine the location of the plot lines and the colors of the plot
;########################################################################################

; ********************
  ROUTINE = 'DATE_BAR'
; ********************

; ===> DEFAULTS
  CHARSIZE= 5 & CHARTHICK=3
  IF NONE(DB_FONT_SIZE) THEN DB_FONT_SIZE = 16 
  IF NONE(DB_LONG)      THEN DB_LONG      = 0.50
  IF NONE(DB_SHORT)     THEN DB_SHORT     = 0.05
   
  MO_CHARTHICK = 1
  IF NONE(DATE)         THEN DATE = DATE_NOW()
  IF NONE(DB_POS)       THEN DB_POS = [0.25,0.04,0.95,0.08] 
  IF NONE(DB_BKG)       THEN DB_BKG = 254  
  IF NONE(DB_COLOR)     THEN DB_COLOR = 200 
  IF NONE(DB_THICK)     THEN DB_THICK = 6 
  IF NONE(FRAME_COLOR)  THEN FRAME_COLOR = 'BLACK'
  IF NONE(TXT_COLOR)    THEN TXT_COLOR = FRAME_COLOR
  IF NONE(BKGD_COLOR)   THEN BKGD_COLOR = []
  IF NONE(PAL)          THEN PAL = 'PAL_BR' 
  YEAR_OFFSET   = 2.25
  DAY_OFFSET    = 1.25
  THICK_FRAME   = 2

;
;################### DB_X,DB_Y PROVIDED THEN REDEFINE DB_POS ############
  IF NOF(DB_X) EQ 1 AND NOF(DB_Y) EQ 1 THEN DB_POS = [(DB_X-DB_LONG/2.0),(DB_Y-DB_SHORT/2.0),(DB_X+DB_LONG/2.0),(DB_Y+DB_SHORT/2.0)]
 
;	===> IF DATE IS ACTUALLY JULIAN (NON STRING) THEN ASSUME INPUT IS JD AND CONVERT IT TO DATE
  IF IDLTYPE(DATE) NE 'STRING' THEN DATE = JD_2DATE(DATE)

  JULIAN				=	DATE_2JD(DATE)
  YEAR 					= DATE_FORMAT(DATE,/YEAR)
  DAY						=	STRMID(DATE,6,2)
  DPY 					= DATE_DAYS_YEAR(DATE)
  DPM 					= DAYS_MONTH() & DPM=DPM(1:*)
  JDS 					= TIMEGEN(13, START=JULDAY(1,1,YEAR,0,0,0),UNITS='MONTHS')
  
  JDS_MID_MONTH	=	JD_2JD(JDS,/MONTH,/MID)
  MONTHS				=	MONTH_NAMES(/LETTER) ;& MONTHS=MONTHS(1:*)
  
  N_CHARS=   (18) - 4*N_ELEMENTS(NO_YEAR) -2*N_ELEMENTS(NO_DAY)
  DENOM = (12+YEAR_OFFSET+DAY_OFFSET) - 1*N_ELEMENTS(NO_YEAR) -  1*N_ELEMENTS(NO_DAY)

  IF NOT KEY(NO_YEAR) THEN LEFT 	= 0.0 + YEAR_OFFSET	/( DENOM) ELSE LEFT = 0.0
  IF NOT KEY(NO_DAY) 	THEN RIGHT 	= 1.0 - DAY_OFFSET	/( DENOM) ELSE RIGHT = 1.0


  IF N_ELEMENTS(_EXTRA) GE 1 THEN BEGIN
  	NAMES=TAG_NAMES(_EXTRA)
  	OK=WHERE(NAMES EQ 'CHARSIZE',COUNT)
  	IF COUNT EQ 1 THEN CHARSIZE = _EXTRA.CHARSIZE
  	OK=WHERE(NAMES EQ 'CHARTHICK',COUNT)
  	IF COUNT EQ 1 THEN CHARTHICK = _EXTRA.CHARTHICK
  ENDIF

  IF NONE(PX)  THEN _PX =  LONG((!D.X_CH_SIZE*CHARSIZE) * (N_CHARS) * 1.66) ELSE _PX=PX;
  IF NONE(PY)  THEN _PY =  LONG((!D.Y_CH_SIZE*CHARSIZE) + !D.Y_CH_SIZE *.5) ELSE _PY=PY;
  
  GTYPE =  IS_OG()
  IF GTYPE EQ 0 THEN BEGIN
  ;#################################### DIRECT GRAPHICS ###############################################################
    ZWIN,[_PX,_PY]
    ERASE,DB_BKG
    CALL_PROCEDURE,PAL,R,G,B
    FONT_HELVETICA 
    
    PLOT,[FIRST(JDS),LAST(JDS)],[0,1],XSTYLE=5,YSTYLE=5,BACKGROUND= DB_BKG,POS=[LEFT, 0.0, RIGHT, 1.00],$
    	 XTICKS=N_ELEMENTS(JDS)-1,XTICKV=JDS,XTHICK=2,YTHICK=2, COLOR=0,_EXTRA=_EXTRA,/NODATA,YTICKS=1,YTICKNAME=[' ',' ',' ']
    FRAME,COLOR=0,/PLOT,THICK=THICK_FRAME
    FRAME,COLOR=0,THICK=THICK_FRAME,/PAGE
    GRIDS,/NO_Y, X=JDS,/ALL,COLOR=0,THICK=THICK_FRAME
    
    XPOS = JDS_MID_MONTH
    YPOS= .125
    																 XYOUTS, JDS_MID_MONTH,		YPOS,MONTHS,ALIGN=0.5,COLOR=0,/DATA,	CHARSIZE=CHARSIZE,CHARTHICK=CHARTHICK
    IF NOT KEYWORD_SET(NO_YEAR) THEN XYOUTS, LEFT/2.0,				YPOS,YEAR,	ALIGN=0.5,COLOR=0,/NORMAL,CHARSIZE=CHARSIZE,CHARTHICK=CHARTHICK
    IF NOT KEYWORD_SET(NO_DAY) 	THEN XYOUTS, (1.0+RIGHT)/2.0,	YPOS,DAY,		ALIGN=0.5,COLOR=0,/NORMAL,CHARSIZE=CHARSIZE,CHARTHICK=CHARTHICK
    
    
    OPLOT,[JULIAN,JULIAN],[0,1],COLOR=DB_COLOR, THICK=DB_THICK,/NOCLIP
    
    IMAGE=TVRD()
    
    ZWIN
    RETURN,CUTOUT(IMAGE,BKG=DB_BKG)
  
  ENDIF ELSE BEGIN
  ;#################################### OBJECT GRAPHICS ###############################################################
    
    IF N_ELEMENTS(DB_COLOR)   EQ 1 AND IDLTYPE(DB_COLOR)   NE 'STRING' THEN DB_COLOR   = RGBS(DB_COLOR,PAL=PAL)
    IF N_ELEMENTS(FONT_COLOR) EQ 1 AND IDLTYPE(FONT_COLOR) NE 'STRING' THEN FONT_COLOR = RGBS(FONT_COLOR,PAL=PAL)
    IF N_ELEMENTS(TXT_COLOR)  EQ 1 AND IDLTYPE(TXT_COLOR)  NE 'STRING' THEN TXT_COLOR  = RGBS(TXT_COLOR,PAL=PAL)
    IF N_ELEMENTS(BKGD_COLOR) EQ 1 AND IDLTYPE(BKGD_COLOR) NE 'STRING' THEN BKGD_COLOR = RGBS(BKGD_COLOR,PAL=PAL)
    
    IF MAX(DB_POS) LE 1.0 THEN DEVICE = 0 ELSE DEVICE = 1
    
    IF ~KEY(NO_YEAR) THEN RTADD = ((MAX(JDS)-MIN(JDS))/N_ELEMENTS(JDS))*4 ELSE RTADD = 0
    IF ~KEY(NO_DAY)  THEN LFADD = ((MAX(JDS)-MIN(JDS))/N_ELEMENTS(JDS))*3 ELSE LFADD = 0
    PL=PLOT([MIN(JDS)-LFADD,MAX(JDS)+RTADD],[0,1],/CURRENT,XRANGE=XRANGE,YMAJOR=0,XMAJOR=0,YMINOR=0,XMINOR=0,XSTYLE=1,YSTYLE=1,AXIS_STYLE=2,$
      POSITION=DB_POS,XTHICK=THICK_FRAME,YTHICK=THICK_FRAME,XCOLOR=FRAME_COLOR,YCOLOR=FRAME_COLOR,/NODATA,DEVICE=DEVICE)
    YPOS = 0.5
    LF = MIN(JDS)-(LFADD/2) ; MIN(XR)+((MIN(JDS)-MIN(XR))/2.)
    RT = MAX(JDS)+(RTADD/2) ; MAX(JDS)+((MAX(XR)-MAX(JDS))/2.)
    FOR N=0, N_ELEMENTS(JDS)-1    DO P1    = POLYLINE([JDS(N),JDS(N)],[0,1],COLOR=FRAME_COLOR,/DATA,THICK=MO_CHARTHICK,TARGET=PL)
    FOR N=0, N_ELEMENTS(MONTHS)-1 DO MTEXT = TEXT(JDS_MID_MONTH(N),YPOS,MONTHS(N),ALIGNMENT=0.5,VERTICAL_ALIGNMENT=0.5,FONT_COLOR=TXT_COLOR,/DATA,FONT_SIZE=DB_FONT_SIZE,TARGET=PL)
    IF ~KEYWORD_SET(NO_DAY)  THEN    YTEXT = TEXT(LF,              YPOS,DAY,      ALIGNMENT=0.5,VERTICAL_ALIGNMENT=0.5,FONT_COLOR=TXT_COLOR,/DATA,FONT_SIZE=DB_FONT_SIZE,TARGET=PL,CLIP=0)
    IF ~KEYWORD_SET(NO_YEAR) THEN    DTEXT = TEXT(RT,              YPOS,YEAR,     ALIGNMENT=0.5,VERTICAL_ALIGNMENT=0.5,FONT_COLOR=TXT_COLOR,/DATA,FONT_SIZE=DB_FONT_SIZE,TARGET=PL,CLIP=0)
    P2 = POLYLINE([JULIAN,JULIAN],[0,1],COLOR=DB_COLOR,THICK=DB_THICK,/OVERPLOT,TARGET=PL,/DATA)
  
  ENDELSE;IF GTYPE EQ 0 THEN BEGIN
  
END; #####################  END OF ROUTINE ################################
