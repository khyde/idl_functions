; $ID:	STR_COMMA.PRO,	2016-01-01,	USER-JOR	$
;#######################################################################################
	FUNCTION STR_COMMA, VAL

;+
; NAME:
;		STR_COMMA
;
; PURPOSE:;
;		THIS FUNCTION ADDS COMMAS TO LONG/LARGE NUMBERS

; CATEGORY:
;		CATEGORY
;
; CALLING SEQUENCE:

;		RESULT = STR_COMMA(VAL)
;
; INPUTS:
;		VAL:	STRING OR STRING ARRAY
;
; OPTIONAL INPUTS:
;
; KEYWORD PARAMETERS:
;		NONE;
;
; OUTPUTS:
;		A STRING WITH COMMAS EVERY 3 POSITIONS
;

;
; MODIFICATION HISTORY:
;			WRITTEN NOV 1, 2004 BY J.O'REILLY
;			DEC 11,2011, JOR NOW LARGE WHOLE NUMBERS OR NUMERIC STRINGS MAY BE INPUT 
;			DEC 30,2014,JOR:IF MAX(VAL,/NAN) LT 1000 THEN RETURN,VAL
;			NOV 23,2015,JOR:IF MAX(DOUBLE(VAL)) LT 1000 THEN RETURN,VAL
;     JAN 01,2016, JOR FIXED BUG WITH LARGE NEG NUMBERS:TXT=STRTRIM(LONG64(VAL),2)


;#######################################################################################
;-
;	**************************
	ROUTINE_NAME = 'STR_COMMA'
; **************************
	N=N_ELEMENTS(VAL)
	IF N  EQ 0 THEN RETURN,[]
;===> IF VAL LT 1000 THEN RETURN VAL
IF MAX(DOUBLE(VAL)) LT 1000 THEN RETURN,VAL
; ===> CONVERT TO ULONG64 

  TXT=STRTRIM(LONG64(VAL),2)
	NTH = N-1L

	TXT_WHOLE=TXT
  TXT_PART  = REPLICATE('',N)


  POS = STRPOS(TXT,'.')
  OK = WHERE(POS NE -1,COUNT)
  IF COUNT GE 1 THEN BEGIN
    FOR _OK = 0L,COUNT-1L DO BEGIN
    	TXT_WHOLE(OK(_OK)) 	= STRMID(TXT(OK(_OK)),0,(POS(OK(_OK)) > 1))
    	TXT_PART(OK(_OK)) 	= STRMID(TXT(OK(_OK)),POS(OK(_OK)),100)
    ENDFOR;FOR _OK = 0L,COUNT-1L DO BEGIN
  ENDIF;IF COUNT GE 1 THEN BEGIN


;   FFFFFFFFFFFFFFFFFFFFFFF
    FOR I = 0L,NTH DO BEGIN
      ATXT = STRTRIM(STRING(TXT_WHOLE(I)),2)
      LEN = STRLEN(ATXT)
      NEG = STRPOS(ATXT,'-',0) EQ 0
      LEN = LEN - NEG
      ATXT = STRMID(ATXT,NEG)
      LEN_MOD_3 = LEN MOD 3
      TRIPLETS = LEN/3 & PART = LEN - (TRIPLETS*3)
      BTXT=STRMID(ATXT,	0							,LEN_MOD_3)
      IF BTXT EQ '' AND TRIPLETS GE 1 THEN BEGIN
      	BTXT = STRMID(ATXT,0,3)
      	JJ=1
      ENDIF ELSE JJ = 0

;			J=1 (ONLY ADD COMMA IF MORE THAN ONE TRIPLET
      FOR J = JJ,TRIPLETS-1 DO BEGIN
      	BTXT=BTXT+','+STRMID(ATXT, (J*3+LEN_MOD_3)	,	3)
			ENDFOR
;     CLEAN UP CASES OF '-,'
      IF NEG THEN BTXT = '-'+BTXT
      TXT[I] = BTXT+TXT_PART[I]
    ENDFOR;FOR I = 0L,NTH DO BEGIN

   RETURN,TXT


	END; #####################  END OF ROUTINE ################################
