; $ID:	COORD_2PLOT.PRO,	2020-07-08-15,	USER-KJWH	$
;################################################################################################################
  FUNCTION COORD_2PLOT, X, Y,Z,  TO_DATA=TO_DATA,TO_NORMAL=TO_NORMAL,TO_DEVICE=TO_DEVICE, ERROR=ERROR
;+
;	THIS PROGRAM CONVERTS RELATIVE POSITION WITHIN THE PLOT WINDOW 
;	     [(0 TO 1.0),(0 TO 1.0)] TO X,Y COORDINATES 
;
;	EXAMPLES:
;		RESULT = COORD_2PLOT(0.8,0.8)
;		RESULT = COORD_2PLOT(0.8,0.8,/TO_DATA)
;		RESULT = COORD_2PLOT(0.8,0.8,/TO_NORMAL)
;
; OUTPUT:
;	 COORDINATES WITHIN THE PLOT WINDOW
; ARGUMENTS:
; 	X:	X COORDINATE IN RELATIVE UNITS (0 TO 1.0)
;		Y:	Y COORDINATE IN RELATIVE UNITS (0 TO 1.0)
; KEYWORDS:
;		SEE IDL CONVERT_COORD
; HISTORY:
;	FEB 10,2001	WRITTEN BY:	J.E. O'REILLY
;	APR 21,2014,JOR UPDATED
;-
;**************************
ROUTINE_NAME='COORD_2PLOT'
;**************************

ERROR = ''

; ================> GET PLOT WINDOW (NORMAL)
  PLOT_WINDOW_X = !X.WINDOW
  PLOT_WINDOW_Y = !Y.WINDOW

  IF NONE(X) OR NONE(Y) OR N_ELEMENTS(X) NE N_ELEMENTS(Y) THEN BEGIN
    ERROR = 'ERROR: MUST PROVIDE X,Y  OR X,Y,Z NORMAL COORDINATES'
    PRINT,ERROR
    RETURN,ERROR
  ENDIF;IF NONE(X) EQ 0 OR NONE(Y) EQ 0 OR N_ELEMENTS(X) NE N_ELEMENTS(Y) THEN BEGIN


	N = TOTAL( [N_ELEMENTS(TO_DATA),N_ELEMENTS(TO_NORMAL),N_ELEMENTS(TO_DEVICE)])
	IF N GE 2 THEN BEGIN
		ERROR ='ERROR: PROVIDE ONLY ONE OUTPUT UNIT: TO_DATA,TO_NORMAL,TO_DEVICE'
		PRINT,ERROR	
		RETURN,ERROR
	ENDIF;N = TOTAL( [N_ELEMENTS(TO_DATA),N_ELEMENTS(TO_NORMAL),N_ELEMENTS(TO_DEVICE)])


	IF N EQ 0 THEN 	DATA=1 ; BECAUSE DEFAULT FOR XYOUTS IS DATA UNITS

; ===> ADJUST INPUT RELATIVE COORDINATES TO PLOT WINDOW
	XX=(!X.WINDOW[1]-!X.WINDOW[0])*X + !X.WINDOW[0]
  YY=(!Y.WINDOW[1]-!Y.WINDOW[0])*Y + !Y.WINDOW[0]

  IF N_ELEMENTS(Z) EQ N_ELEMENTS(X) THEN BEGIN
  	ZZ=(!Z.WINDOW[1]-!Z.WINDOW[0])*Z + !Z.WINDOW[0]
 		XYZ=CONVERT_COORD(XX,YY,ZZ,/NORMAL ,TO_DATA=TO_DATA,TO_NORMAL=TO_NORMAL,TO_DEVICE=TO_DEVICE)
		XX = REFORM(XYZ(0,*))
  	YY = REFORM(XYZ(1,*))
  	ZZ = REFORM(XYZ(2,*))
  	RETURN, CREATE_STRUCT('X',XX,'Y',YY,'Z',ZZ)
	ENDIF ELSE BEGIN
	 
;		===> CONVERT PLOT WINDOW COORDINATES TO SELECTED OUTPUT UNITS
  	XYZ=CONVERT_COORD(XX,YY,/NORMAL ,TO_DATA=TO_DATA,TO_NORMAL=TO_NORMAL,TO_DEVICE=TO_DEVICE)
  	XX = REFORM(XYZ(0,*))
  	YY = REFORM(XYZ(1,*))
  	RETURN, CREATE_STRUCT('X',XX,'Y',YY )
	ENDELSE
; ===> RETURN A STRUCTURE WITH X,Y IN SELECTED OUTPUT UNITS FOR THE PLOT WINDOW


 DONE:

END; #####################  END OF ROUTINE ################################
