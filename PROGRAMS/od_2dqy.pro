; $ID:	OD_2DQY.PRO,	2019-07-26-19,	USER-JEOR	$
; #########################################################################; EXAMPLE
FUNCTION OD_2DQY,OD,FRACTION = FRACTION,COEFFS = COEFFS
;+
; PURPOSE:  CALCULATE DAILY QUANTUM YIELD FROM OPTICAL DENSITY 
;           [BASED ON A POLYNOMIAL REGRESSION BETWEEN MEDIAN OPTICAL DEPTHS AND MEDIAN DQYS FROM THE MARMAP AND ONDEQUE DATASETS]
;
; CATEGORY: PP
;
;
; INPUTS:
;       OD ......... OPTICAL DENSITY [UNITLESS]
;
;
; KEYWORDS:  
;         FRACTION .......... CONVERT INPUT DATA FROM [FRACTION OF SURFACE LIGHT] TO OPTICAL DENSITY
;         COEFFS ............ USE THE 5 POLYNOMIAL RGRESSION COEFFICIENTS PROVIDED 
;         
; OUTPUTS: DAILY QUANTUM YIELD (MOLE CARBON / MOLE QUANTA M-2 D-1) 
;
;; EXAMPLES: 
;  PRINT,OD_2DQY(1.0,/FRACT) ; = 0 OPTICAL DEPTH = 0.00269000 DQY
;  PRINT,OD_2DQY(0.01,/FRACT) ;  = 4.60517 OPTICAL DEPTH = 0.0221111 DQY
;  PRINT,OD_2DQY([0,2,4.60517]) ; 0.00269000    0.0257900    0.0221111
;  PRINT,OD_2DQY(4.60517),OD_2DQY(0.01,/FRACT); BOTH SHOULD BE 0.0221111
;  PLT = PLOT(INTERVAL([0,6.5],.01),OD_2DQY(INTERVAL([0,4.6],.01)),XTICKV = [0,2,4,6,8],XTITLE = 'OD',YTITLE = 'DQY',COLOR = 'RED',THICK = 3)& PLT_GRIDS,PLT
;  PLT = PLOT(INTERVAL([0,5.0],.01),OD_2DQY(INTERVAL([0,4.6],.01),/OPAL),XTICKV = [0,2,4,6,8],TITLE = 'Daily Quantum Yield vs.Optical Density', XTITLE = 'Optical Density',YTITLE = 'DQY!D443!N',COLOR = 'RED',THICK = 3)& PLT_GRIDS,PLT
;  
; MODIFICATION HISTORY:
;     FEB 27, 2019  WRITTEN BY: J.E. O'REILLY
;     APR 23,2019,JEOR CHANGED OD TO OD_ TO AVOID CHANGING THE INPUT ARRAY
;     MAY 20,2019,JEOR NOW USING FUNCTION BASED ON MARMAP-APH_443 INSTEAD OF APH_440
;     JUN 03,2019,JEOR SIMPLIFIED
;     JUN 04,2019,JEOR ADDED [OPAL] COEFFICIENTS FROM PP_OPAL_OPTIMIZE [0.00822919;    0.0172779;    0.0244022;  -0.00948842;  0.000842472
;     JUL 14,2019,JEOR REMOVED KEY OPAL
;     JUL 26,2019,JEOR   IF IDLTYPE(COEFFS) EQ 'STRING' THEN COEFFS = STR_SEP(COEFFS,';')

; #########################################################################
;-
;******************
ROUTINE = 'OD_2DQY'
;******************


IF NONE(COEFFS) THEN BEGIN
  A =  [0.0082292300,     0.017263560,     0.024428501,   -0.0095004232,   0.00084401781]; FROM 4_STATS 4TH DEG 
ENDIF ELSE BEGIN
  IF IDLTYPE(COEFFS) EQ 'STRING' THEN COEFFS = FLOAT(STR_SEP(COEFFS,';'))
  A = COEFFS  
ENDELSE;IF NONE(COEFFS) THEN BEGIN

OD_ = OD          

IF KEY(FRACTION) THEN BEGIN
  ;===> CONSTRAIN BETWEEN 0.01 AND 1.0 [MARMAP LIGHT-FRACTION]
  OD_ = 0.01 > OD_ < 1.0 
  OD_ = -ALOG(OD_)
ENDIF;IF KEY(FRACTION) THEN BEGIN

;===> CONSTRAIN OD_ BETWEEN 0.0 AND 4.60517 [MARMAP]
 OD_ = 0.0 > OD_ < 4.60517 

RETURN,POLY(OD_,A)

END; #####################  END OF ROUTINE ################################
