; $ID:	FILE_GET.PRO,	2020-06-03-17,	USER-KJWH	$
; #########################################################################; 
PRO FILE_GET, SITE=SITE,FILENAMES=FILENAMES,DIR_LOCAL=DIR_LOCAL,MIN_SIZE=MIN_SIZE,DOWNLIST=DOWNLIST,OVERWRITE=OVERWRITE,VERBOSE=VERBOSE
;+
; PURPOSE:  DOWNLOADS FILES FROM A SITE/SERVER [HTTPS]
;
; CATEGORY: FILES
;
; KEYWORDS:  
;         SITE...........  SERVER SITE FOR THE FILES [DEFAULT:'https://oceandata.sci.gsfc.nasa.gov/cgi/getfile/']
;         FILENAMES .....  FULL FILE NAMES AS FOUND ON THE SITE [E.G. 'A2017310.L3m_DAY_CHL_chl_ocx_9km.nc']
;         DIR_LOCAL......  DIRECTORY STRING CONTAINING THE LOCAL DIRECTORY ON WHICH TO STORE THE DOWNLOADED FILES
;         MIN_SIZE.......  FILES SMALLER THAN MIN_SIZE WILL BE DELETED [ONLY WHEN AN ERROR IS CAUGHT ,DEFAULT = 1,000 BYTES]
;         DOWNLIST.......  LIST OF FILES SUCESSFULY DOWNLOADED [OUTPUT]
;         OVERWRITE......  OVERWRITE FILE IF IT EXISTS
;         VERBOSE........  PRINT PROGRAM PROGRESS
;         
; OUTPUTS: THE DOWNLOADED FILE IS STORED IN THE DIR_LOCAL DIRECTORY
;
; EXAMPLE:
;    NOTE THAT THE FILENAME A2017368 DOES NOT EXIST ON THE SERVER [ SO IT WILL CAUSE CATCH TO ACT AND THE BAD-PARTIAL FILE WILL BE DELETED ON DIR_LOCAL]
;    FILENAMES = ['A2017001.L3m_DAY_CHL_chl_ocx_9km.nc','A2017002.L3m_DAY_CHL_chl_ocx_9km.nc','A2017368.L3m_DAY_CHL_chl_ocx_9km.nc','A2017003.L3m_DAY_CHL_chl_ocx_9km.nc']
;   FILE_GET,DIR_LOCAL =!S.IDL_TEMP,FILENAMES=FILENAMES,DOWNLIST=DOWNLIST,/VERBOSE,/OVERWRITE & PLIST,DOWNLIST


; NOTES:
; IF A TARGET FILE DOES NOT EXIST ON THE SERVER'S SITE  RESULT = ONET.GET(FILENAME=FILENAME, URL=URL)
; WILL STORE A CORRUPTED [SMALL] FILE OF THE FILENAME ON DIR_LOCAL
; WHICH THIS PROGRAM DELETES BASED ON MIN_SIZE
; 
; !ERROR_STATE_CODES:  -1006 = Http Get Request Failed. [AS IN OUR EXAMPLE [A2017368] WHEN DAY OF YEAR = 368!]
; THERE MAY BE OTHER  !ERROR_STATE_CODES THAT SHOULD BE ADDED TO CATCH
;
; MODIFICATION HISTORY:;   
;   NOV 15,2017 WRITTEN BY J.E.O'REILLY 
;   THE FOLOWING 2 LINES OF CODE WERE MODIFIED FROM IDL'S WGET ROUTINE
;            ONET = IDLNETURL()
;          RESULT = [RESULT, ONET.GET(BUFFER=BUFFER, FILENAME=FNAME, STRING_ARRAY=STRINGARRAY, URL=URL[I])]
;   NOV 22,2017, JEOR: ADDED KEY DOWNLIST
;   DEC 15,2017, JEOR: FINAL TESTING
;   
;###################################################################################################################
;-
;*******************
ROUTINE = 'FILE_GET'
;*******************
IF NONE(SITE) THEN SITE  = 'https://oceandata.sci.gsfc.nasa.gov/cgi/getfile/'
BEG = 0L
IF NONE(MIN_SIZE) THEN MIN_SIZE = 1000
MISSING_LIST = []
DOWNLIST     = []
IF NONE(OVERWRITE) THEN OVERWRITE = 0
;#######################################################
CATCH,ERROR_STATUS
 IF ERROR_STATUS EQ -1006L THEN BEGIN
  IF (FILE_INFO(FILENAME)).SIZE LT MIN_SIZE THEN BEGIN
    FILE_DELETE,FILENAME,VERBOSE=VERBOSE
    MISSING_LIST = [MISSING_LIST,FILENAME]
    BEG = NTH+1L ;TO RESUME AT THE NEXT FILENAME
  ENDIF
ENDIF;IF ERROR_STATUS EQ -1006 THEN BEGIN
;#######################################################
;
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
FOR NTH = BEG,NOF(FILENAMES)-1 DO BEGIN
  IF KEY(VERBOSE) THEN POF,NTH,FILENAMES
  FILENAME =  FILENAMES[NTH]
  URL = SITE + FILENAME
  FILENAME = DIR_LOCAL + FILENAME
  IF OVERWRITE EQ 0 AND EXISTS(FILENAME) THEN BEGIN
    IF KEY(VERBOSE) THEN PFILE,FILENAME,/K
    CONTINUE;>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
  ENDIF;IF OVERWRITE EQ 0 AND EXISTS(FILENAME) THEN BEGIN
  ONET = IDLNETURL()
  
  IF KEY(VERBOSE) THEN PFILE,FILENAME,/G
  RESULT = ONET.GET(FILENAME=FILENAME, URL=URL)
  DOWNLIST = [DOWNLIST,RESULT]
ENDFOR;FOR NTH = 0,NOF(FILENAMES)-1 DO BEGIN
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF

;===> WRITE MISSING_LIST TO A TXT FILE
IF ANY(MISSING_LIST) THEN BEGIN
  TXT = !S.IDL_TEMP + ROUTINE +'-MISSING-' + STRTRIM(DATE_NOW() ,2) + '.TXT'
  WRITE_TXT,TXT,MISSING_LIST
ENDIF;IF ANY(MISSING_LIST) THEN BEGIN
;||||||||||||||||||||||||||||||||||||

END; #####################  END OF ROUTINE ################################
