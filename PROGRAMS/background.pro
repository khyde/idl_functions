; $ID:	BACKGROUND.PRO,	2020-07-08-15,	USER-KJWH	$
 PRO BACKGROUND,COLOR=COLOR,PLOT=PLOT,REGION=REGION,PAGE=PAGE,PAL=PAL,_EXTRA=_EXTRA
 ;################################################################################################
;+
;	THIS PROGRAM SETS THE !P.BACKGROUND COLOR OR
;							 FILLS IN THE PAGE, REGION, OR PLOT AREA WITH A COLOR SUPPLIED BY THE USER
;	(MAY BE USED WITH POSTSCRIPT TO PLOT ATOP A BACKGROUND OTHER THAN THE DEFAULT WHITE)
;  PSPRINT,/COLOR
;  PAL_36
;  !P.MULTI=[0,2,2]
;  BACKGROUND
;  BACKGROUND,/PAGE,COLOR=35
;  PLOT,[1,2,3],[1,2,3],/NODATA
;  BACKGROUND,/REGION,COLOR=34
;  BACKGROUND,/PLOT,COLOR=33
;	 SKIPPLOT,-1
;	 PLOT,[1,2,3],[1,2,3],/NODATA
;  PLOTS,[1,2,3],[1,2,3],COLOR=21
;	 PSPRINT
; HISTORY:
;		MARCH 19,2003	WRITTEN BY:	J.E. O'REILLY, NOAA, 28 TARZWELL DRIVE, NARRAGANSETT, RI 02882
;		AUG 29,2017 JEOR: FORMATTING
 ;################################################################################################
;-
; *******************
ROUTINE='BACKGROUND'
; *******************
  IF NONE(COLOR) THEN COLOR = 255 ELSE COLOR = COLOR

  IF NOT KEY(PLOT) AND NOT KEY(REGION) AND NOT KEY(PAGE) THEN BEGIN
    !P.BACKGROUND = COLOR
    RETURN
  ENDIF

;	=====> PLOT
  IF KEY(PLOT) 		THEN BEGIN
  	X = !X.CRANGE & Y = !Y.CRANGE
  	IF !X.TYPE EQ 1 THEN X= 10.0^X
  	IF !Y.TYPE EQ 1 THEN Y= 10.0^Y
  	POLYFILL, [X[1],X[1],X[0],X[0],X[1]],$
  						[Y[1],Y[0],Y[0],Y[1],Y[1]],/DATA,COLOR= COLOR,_EXTRA=_EXTRA
  	RETURN
  ENDIF

;	=====> REGION
	IF KEY(REGION)	THEN BEGIN
		X = !X.REGION & Y = !Y.REGION
		POLYFILL,	[X[1],X[1],X[0],X[0],X[1]],$
  						[Y[1],Y[0],Y[0],Y[1],Y[1]],/NORMAL,COLOR= COLOR,_EXTRA=_EXTRA
  	RETURN
	ENDIF

;	=====> PAGE
  IF KEY(PAGE) 		THEN BEGIN
  	POLYFILL, [1,1,0,0,1],[1,0,0,1,1],/NORMAL,COLOR= COLOR,_EXTRA=_EXTRA
 	ENDIF


DONE:
END; #####################  END OF ROUTINE ################################
