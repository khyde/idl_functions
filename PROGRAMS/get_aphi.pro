; $ID:	GET_APHI.PRO,	2020-06-26-15,	USER-KJWH	$
; #########################################################################; 
FUNCTION GET_APHI,WL,CHL=CHL,INIT=INIT
;+
; PURPOSE:  THIS PROGRAM A STRUCTURE OF LAMBDA,Aphi,Ephi BY WAVELENGTH FROM THE BRICAUD TABLE: BRICAUD_1998_TABLE_AP_APHI.CSV
;              OR RETURNS AN ARRAY OF K_CHL VALUES [IF CHL PROVIDED]
;
; CATEGORY: ABSORPTION;
;
;
; INPUTS: WL..... WAVELENGTH [400 TO 700 NM]
;
;
; KEYWORDS:
;       CHL..... CHLOR_A CONCENTRATION
;       INIT.... INITIALIZE DATABASE [REREAD]

; OUTPUTS:  APHI AND EPHI 
;           IF CHL PRESENT THEN OUTPUT = K_CHL = A_PHI[WL] =  DB[WL].APHI*ACHL^(DB[WL].EPHI)
;
;; EXAMPLES:
;          ST, GET_APHI(440)
;          ST, GET_APHI(443)
;          PRINT, GET_APHI(440,CHL = 1.0)
;          PRINT, GET_APHI(443,CHL = 1.0)
;          PRINT, GET_APHI(440,CHL = [0.01,0.1,1.0,10,100])
;          ST, GET_APHI(840); = [ERROR: WL MUST BE BETWEEN 400 AND 700 NM]
;          PRINT, GET_APHI(CHL = 1.0); =  [ERROR: WL IS REQUIRED]
;
;  REFERENCE:  BRICAUD, A., A. MOREL, M. BABIN, K. ALLALI AND H. CLAUSTRE. 1998. J. GEOPHYS. RES., 103, C13: 31,033-31,044.
;
; MODIFICATION HISTORY:
;     JUL 03, 2016  WRITTEN BY: J.E. O'REILLY
;     JUL 05, 2016,JOR, INTERPOLATED COEFFICIENTS TO EACH WL BETWEEN 400 AND700 NM
;-
; #########################################################################

;**************************
ROUTINE_NAME  = 'GET_APHI'
;**************************
COMMON _GET_APHI,DB
IF KEY(INIT) OR  NONE(DB) THEN BEGIN
  FILE = !S.DATA + 'bricaud_1998_table_ap_aphi.csv'
  DB=CSV_READ(FILE)
  DB =STRUCT_2FLT(DB)
  ;===> INTERPOLATE TO WHOLE WLS
  LAMBDA = INTERVAL([400,700],1)
  APHI = INTERPOL( DB.APHI, DB.LAMBDA,LAMBDA,/LSQUADRATIC)
  EPHI = INTERPOL( DB.EPHI, DB.LAMBDA,LAMBDA,/LSQUADRATIC)
  DB = REPLICATE(CREATE_STRUCT('LAMBDA',0,'APHI',0.0,'EPHI',0.0),NOF(LAMBDA))
  DB.LAMBDA = LAMBDA
  DB.APHI = APHI
  DB.EPHI = EPHI  
ENDIF;IF NONE(DB) THEN BEGIN

IF NONE(WL) THEN MESSAGE,'ERROR: WL IS REQUIRED'
;===> WL MUST BE BETWEEN 400 AND 700 NM
IF WL LT 400 OR WL GT 700 THEN MESSAGE, 'ERROR: WL MUST BE BETWEEN 400 AND 700 NM' 

OK = WHERE(DB.LAMBDA EQ WL,COUNT)
IF COUNT EQ 1 THEN  D =DB[OK]

IF KEY(CHL) THEN BEGIN   
  RETURN, D.APHI*CHL^D.EPHI
ENDIF ELSE BEGIN
  RETURN,D  
ENDELSE;IF KEY(CHL) THEN BEGIN


  

;A_PHI =  DB.APHI*ACHL^(DB.EPHI-1)
A_PHI =  DB.APHI*ACHL^(DB.EPHI)


END; #####################  END OF ROUTINE ################################
