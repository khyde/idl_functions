; $ID:	PLOT_CONTOURS.PRO,	2020-06-30-17,	USER-KJWH	$

 PRO PLOT_CONTOURS, CONTOUR_FILE, PATH_INFO=PATH_INFO, PATH_XY=path_xy, STRUCT=STRUCT, VALUES=VALUES, MIN_NUM=min_num,DELIM=delim, DELAY=DELAY, _EXTRA=_EXTRA

;+
; NAME:
;		PLOT_CONTOURS
;
; PURPOSE:;
;		This FUNCTION READS and PLOTS the Contour Positions generated by IDL's CONTOUR program.
;
; CATEGORY:
;		PLOT
;
; CALLING SEQUENCE:
;		Result = PLOT_CONTOURS('PATH AND NAME OF FILE GENERATED BY IDL'S CONTOUR PROGRAM WITH PATH_FILENAME SPECIFIED')

; INPUTS:
;		CONTOUR_FILE:	Name of file generated by IDL'S CONTOUR routine and when specifiying PATH_FILENAME= ' '

; KEYWORD PARAMETERS:
;		PATH_INFO:	Contour Path Information in a structure obtained from CONTOUR and specifying PATH_INFO
;		PATH_XY:    Contour coordinate information from CONTOUR and specifying PATH_XY.
;
;		STRUCT:	May input a Structure previously read by READ_CONTOURS
;
;		VALUES:	The contour levels to plot
;
;		DELIM:  The delimiter to use to extract the x,y positions from these tags in the CONTOUR_FILE structure
;						(the default delimiter is ';'.
;						If you first obtain a CONTOUR_FILE structure using READ_CONFILE and specify a different delimiter then
;						set DELIM to that value for this routine to properly plot the delimited x,y positions in the STRUCT.
;
;		_EXTRA: Any extra keywords that you would like to pass to the PLOT routine
;
; OUTPUTS:
;		Plots the x,y positions of the contours on the graphic device
;
;	PROCEDURE:
; EXAMPLE:
;
;	NOTES:
;	Note that the idl structure written using keyword PATH_FILENAME does NOT have the OFFSET Tag, but PATH_INFO returned by
;	IDL CONTOUR Routine DOES have OFFSET TAG.
;
;
; MODIFICATION HISTORY:
;			Written Dec 9, 2006 by J.O'Reilly, 28 Tarzwell Drive, NMFS, NOAA 02882 (Jay.O'Reilly@NOAA.GOV)
;-
;	****************************************************************************************************
	ROUTINE_NAME = 'PLOT_CONTOURS'


;	===> CONTOUR_FILE Provided ?
	IF N_ELEMENTS(CONTOUR_FILE) EQ 1 THEN BEGIN
;		===> Determine if the CONTOUR_FILE is a csv or a con
		FN=FILE_PARSE(CONTOUR_FILE)
		IF STRUPCASE(FN.EXT) EQ 'CSV' THEN 	STRUCT=READ_CSV(CONTOUR_FILE)
		IF STRUPCASE(FN.EXT) EQ 'CON' THEN 	STRUCT=READ_CONTOURS(CONTOUR_FILE)
	ENDIF

;	===> PATH_INFO & PATH_XY Provided ?
	IF N_ELEMENTS(PATH_INFO) GE 1 AND N_ELEMENTS(PATH_XY) GE 1 THEN $
	   STRUCT=READ_CONTOURS(PATH_INFO=PATH_INFO,PATH_XY=PATH_XY)

;	===> Structure provided from READ_CONTOURS ?
  IF N_ELEMENTS(STRUCT) GE 1 THEN BEGIN
		SETS = WHERE_SETS(STRUCT.VALUE)
		_VALUES = SETS.VALUE
  	IF N_ELEMENTS(MIN_NUM) NE 1 THEN _MIN_NUM = 1 ELSE _MIN_NUM = MIN_NUM
		IF N_ELEMENTS(VALUES) GE 1 THEN _VALUES = VALUES
;		LLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLL
		FOR NTH=0,N_ELEMENTS(STRUCT)-1 DO BEGIN
			S=STRUCT[NTH]
			IF S.NUM LT _MIN_NUM THEN CONTINUE ; >>>>>>>>>>>>>>
			X = FLOAT(STR_BREAK(S.X,';'))
			Y = FLOAT(STR_BREAK(S.Y,';'))
			PLOTS, [X] ,[Y] , _EXTRA=_EXTRA
			IF N_ELEMENTS(DELAY) EQ 1 THEN WAIT,DELAY
		ENDFOR
	ENDIF
END; #####################  End of Routine ################################



