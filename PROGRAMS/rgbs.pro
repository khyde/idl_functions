; $ID:	RGBS.PRO,	2021-04-14-17,	USER-KJWH	$
;+
;;#############################################################################################################
	FUNCTION RGBS,COLORS,PAL=PAL,RED=RED,GREEN=GREEN,BLUE=BLUE
;
;
; PURPOSE: THIS FUNCTION GETS THE RGB TRIPLETS FROM A PALETTE
; 
; CATEGORY:	RGB		 
;
; CALLING SEQUENCE: RESULT = RGBS(COLORS,PAL=PAL)
;
; INPUTS: COLOR INDEX 

; OPTIONAL INPUTS:
;		NONE:	
;		
; KEYWORD PARAMETERS:  PAL [NAME OF PALETTE PROGRAM]
;                      RED 256 ARRAY FOR RED
;                      GREEN 256 ARRAY FOR GREEN
;                      BLUE 256 ARRAY FOR BLUE
; OUTPUTS: A RGB TRIPLET
;		
;; EXAMPLES:
;   COLOR= RGBS[0] & P,COLOR
;   COLOR= RGBS(22,PAL='PAL_36') & P,COLOR; PURE RED
;   COLOR= RGBS(18,PAL='PAL_36') & P,COLOR; YELLOW
;   RGB_TABLE= RGBS([1,250]) & HELP,RGB_TABLE
;   RGB_TABLE= RGBS([1,251],PAL ='PAL_SW3') & HELP,RGB_TABLE
;   PRINT,RGBS([0,251,252,253,255])
; MODIFICATION HISTORY:
;			WRITTEN FEB 12, 2014 J.O'REILLY
;			JUN 15,2014,JOR COLORS MAY BE EITHER NUMBERS OR STRING
;			JUL 3,2014,JOR ADDED KEYWORDS RED,GREEN,BLUE
;			SEP 12,2014,JOR:COLORS = 0 > COLORS < 255
;     DEC 15,2014,JOR :IF N_ELEMENTS(PAL) NE 1 THEN PAL = 'PAL_BR'
;     NOV 07,2015,JOR DELETED LINE:COLORS = 0 > COLORS < 255
;     JAN 06,2016,JOR: IF N_ELEMENTS(COLORS) GE 3 THEN RETURN,RGB(*,[COLORS])


;#################################################################################
;-
;*********************
ROUTINE_NAME  = 'RGBS'
;*********************
;#####>    DEFAULTS     #######################
IF ANY(RED) AND ANY(GREEN) AND ANY(BLUE) THEN BEGIN
  RGB= TRANSPOSE([[RED],[GREEN],[BLUE]])  
ENDIF ELSE BEGIN
IF NONE(PAL) THEN PAL = 'PAL_DEFAULT'
RGB = CPAL_READ(PAL) 
ENDELSE;IF ANY(RED) AND ANY(GREEN) AND ANY(BLUE) THEN BEGIN

IF NONE(COLORS) THEN COLORS = [0,255]
IF IDLTYPE(COLORS) EQ 'STRING' THEN BEGIN
  NAMES = TAG_NAMES(!COLOR)
  OK = WHERE_MATCH(NAMES,COLORS,COUNT)
  IF COUNT EQ 1 THEN BEGIN
    RETURN,!COLOR.(OK)
  ENDIF ELSE BEGIN
    ARR = BYTARR(3,COUNT)
    ;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
    FOR NTH = 0,N_ELEMENTS(OK) -1 DO BEGIN
      ARR(*,NTH) = !COLOR.(OK[NTH])      
    ENDFOR;FOR NTH = 0,N_ELEMENTS(OK) -1 DO BEGIN
    ;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
    RETURN,ARR
  ENDELSE;IF COUNT EQ 1 THEN BEGIN
ENDIF ELSE BEGIN
  IF N_ELEMENTS(COLORS) EQ 1 THEN RETURN,RGB[*,COLORS[0]:COLORS[0]]
  IF N_ELEMENTS(COLORS) EQ 2 THEN RETURN,RGB[*,COLORS[0]:COLORS[1]]  
  IF N_ELEMENTS(COLORS) GE 3 THEN RETURN,RGB[*,[COLORS]]
ENDELSE;IF IDLTYPE(COLORS) EQ 'STRING' THEN BEGIN




DONE:          
	END; #####################  END OF ROUTINE ################################
