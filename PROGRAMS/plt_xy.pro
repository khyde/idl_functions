; $ID:	PLT_XY.PRO,	2020-07-08-15,	USER-KJWH	$
;#########################################################################################################
PRO PLT_XY, X, Y, Z, $                          ; INPUT PARAMENTERS
            
;           ### PLOT WINDOW SETUP            
            CURRENT=CURRENT,$                    ; ADD PLOT TO CURRENT WINDOW
            DEVICE=DEVICE,$                      ; USE DEVICE COORDINATES
            NORMAL=NORMAL,$                      ; USE NORMAL COORDINATES
            DATA=DATA,$                          ; USE DATA COORDINATES
            LAYOUT=LAYOUT,$                      ; PLOT LAYOUT
            PLT_DIMS=PLT_DIMS,$                  ; PLOT DIMENSIONS
            POSITION=POSITION,$                  ; POSITION OF THE PLOT IN THE WINDOW

;           ### OUTPUT
            FILE=FILE,$                          ; FULL NAME OF THE OUTPUT FILE
            APPEND=APPEND,$                      ; APPEND EACH GRAPHIC
            CLOSE=CLOSE,$                        ; CLOSES THE FILE [SAVE METHOD]
            DELAY=DELAY,$                        ; SECONDS TO DELAY CLOSING PLOT WINDOW
            BUFFER=BUFFER,$                      ; DRAW THE PLOT IN THE BUFFER INSTEAD OF THE SCREEN
            BORDER=BORDER,$                      ; [SEE SAVE METHOD]
            BIT_DEPTH=BIT_DEPTH,$                ; [SEE SAVE METHOD]
            MARGIN=MARGIN,$ 
            OBJ=OBJ,$                            ; TO GET THE PLOT OBJECT                
  
;           ### AXES SETUP
						XLOG=XLOG,$                          ; FOR A LOG10(X) REGRESSION AND  STATISTICS
            YLOG=YLOG,$                          ; FOR A LOG10(Y) REGRESSION AND  STATISTICS
            XRANGE=XRANGE,$                      ; X AXIS RANGE
            YRANGE=YRANGE,$                      ; Y AXIS RANGE
            XTICKV=XTICKV,$
            YTICKV=YTICKV,$
            XMINOR=XMINOR,$
            YMINOR=YMINOR,$
            XSTYLE=XSTYLE,$
            YSTYLE=YSTYLE,$
            XTICKNAME=XTICKNAME,$                ; X AXIS TICK NAMES
            YTICKNAME=YTICKNAME,$                ; Y AXIS TICK NAMES
            AXES_COLOR=AXES_COLOR,$              ; COLOR OF MAIN PLOT AXES
            AXES_THICK=AXES_THICK,$              ; THICKNESS OF MAIN AXES
            AXES_FONT_SIZE=AXES_FONT_SIZE,$      ; SIZE OF THE AXES FONT
            ASPECT_RATIO=ASPECT_RATIO,$          ; RATIO OF Y PLOT DIMENSIONS TO X PLOT DIMENSION
            EXPAND = EXPAND,$                    ; FACTOR FOR EXPANDING NICE_RANGE
;           ##### TITLES    ##### 
            TITLE=TITLE,$                        ; PLOT TITLE
            XTITLE=XTITLE,$                      ; X-AXIS TITLE
            YTITLE=YTITLE,$                      ; Y-AXIS TITLE
  
;           ##### PLOT BACKGROUND  ##### 
            BACKGROUND_COLOR=BACKGROUND_COLOR,$  ; PLOT BACKGROUND COLOR
                     
;						### PASSED TO STATS2.PRO
            MODEL=MODEL,$    		                 ; REGRESSION MODEL
            PARAMS=PARAMS,$                      ; PASSED TO STATS2.PRO
            DECIMALS=DECIMALS,$                  ; PASSED TO STATS2.PRO
            STATS_FILE=STATS_FILE,$              ; FILE FOR APPENDING STATS TO
            
;           ### MISSING DATA AND OUTLIERS 
            MISSINGX=MISSINGX,$                  ; MISSING VALUE FOR X
            MISSINGY=MISSINGY,$                  ; MISSING VALUE FOR Y
            OUTLIERS=OUTLIERS,$                  ; VALUES TO REMOVE FROM REGRESSION
              
;           ### DATA SYMBOL
            SYM_ADD=SYM_ADD,$                    ; DO PLOT ANY SYMBOL
            SYMBOL=SYMBOL,$                      ; SEE PLOT FUNCTION
            SYM_COLOR=SYM_COLOR,$                ; SEE PLOT FUNCTION
            SYM_SIZE=SYM_SIZE,$                  ; SEE PLOT FUNCTION
            SYM_THICK=SYM_THICK,$                ; SEE PLOT FUNCTION
            SYM_FILLED= SYM_FILLED,$             ; SEE PLOT FUNCTION
            SYM_FILL_COLOR=SYM_FILL_COLOR,$      ; SEE PLOT FUNCTION
            SYM_CLIP=SYM_CLIP,$                  ; SEE KEYWORD CLIP IN PLOT FUNCTION

;            ##### DATA LINE  #####
            LIN_ADD=LIN_ADD,$                    ; DO PLOT AN XY LINE
            LIN_COLOR=LIN_COLOR,$                ; COLOR OF THE DATA LINE
            LIN_THICK=LIN_THICK,$                ; THICKNESS OF THE DATA LINE
            LIN_STYLE=LIN_STYLE,$                ; LINESTYLE OF THE DATA LINE;
            LIN_MID_COLOR=LIN_MID_COLOR,$                ; COLOR OF THE MID-DATA LINE
            LIN_MID_THICK=LIN_MID_THICK,$                ; THICKNESS OF THE MID-DATA LINE
            LIN_MID_STYLE=LIN_MID_STYLE,$                ; LINESTYLE OF THE MID-DATA LINE;



;           ### STATISTICS LEGEND PLT_STRUCT
            STATS_ADD=STATS_ADD,$              ; DO PLOT THE REGRESSION LEGEND RESULTS
            STATS_POS=STATS_POS,$                ; POSITION FOR STATS LEGEND (DATA UNITS,X,Y)
            STATS_COLOR=STATS_COLOR,$            ; STATS LEGEND COLOR
            STATS_SIZE=STATS_SIZE,$              ; STATS LEGEND TEXT SIZE
            STATS_ALIGN=STATS_ALIGN,$            ; ALIGNMENT FOR THE STATS LEGEND
            DOUBLE_SPACE=DOUBLE_SPACE,$          ; DOUBLE LINE SPACES BETWEEN STAT LEGEND OUTPUT

;           ### REGRESSION LINE:
            REG_ADD=REG_ADD,$                  ; DO PLOT THE REGRESSION
            REG_COLOR=REG_COLOR,$                ; COLOR OF THE REGRESSION LINE
            REG_THICK=REG_THICK,$                ; THICKNESS OF THE REGRESSION LINE
            REG_LINESTYLE=REG_LINESTYLE,$        ; LINESTYLE OF THE REGRESSION LINE
            
            REG_MID_COLOR=REG_MID_COLOR,$        ; COLOR OF THE OVERPLOTTING REGRESSION LINE
            REG_MID_THICK=REG_MID_THICK,$        ; THICKNESS OF THE OVERPLOTTING REGRESSION LINE
            REG_MID_LINESTYLE=REG_MID_LINESTYLE,$; LINESTYLE OF THE OVERPLOTTING REGRESSION LINE


;           ##### ONE2ONE LINE  #####
            ONE_ADD=ONE_ADD,$                    ; DO PLOT A ONE2ONE LINE
            ONE_COLOR=ONE_COLOR,$                ; COLOR OF THE ONE2ONE LINE
            ONE_THICK=ONE_THICK,$                ; THICKNESS OF THE ONE2ONE LINE
            ONE_LINESTYLE=ONE_LINESTYLE,$        ; LINESTYLE OF THE ONE2ONE LINE

;           ### PLOT MEAN X,Y:
            MEAN_ADD=MEAN_ADD,$                ; DO PLOT THE MEAN X,Y
            MEAN_SYMBOL=MEAN_SYMBOL,$            ; SYMBOL TO USE FOR THE MEAN
            MEAN_SIZE=MEAN_SIZE,$                ; SIZE OF THE MEAN SYMBOL
            MEAN_COLOR=MEAN_COLOR,$              ; COLOR OF THE MEAN SYMBOL
            MEAN_THICK=MEAN_THICK,$              ; THICKNESS OF THE MEAN SYMBOL
            
;           #####    GRID #####
            GRID_ADD=GRID_ADD,$                  ; DO PLOT A GRID
            GRID_COLOR=GRID_COLOR,$              ; COLOR OF THE GRID
            GRID_THICK=GRID_THICK,$              ; THICKNESS OF THE GRID 
            GRID_LINESTYLE=GRID_LINESTYLE,$      ; LINESTYLE OF THE GRID 
            
;           #####    TEXT AT X,Y #####
            TXT_ADD=TXT_ADD ,$                   ; DO PLOT THE TEXT AT THE XY COORDS
            TXT_FONT=TXT_FONT,$                  ; FONT TO USE FOR THE TXT [SEE TEXT FUNCTION]
            TXT_COLOR=TXT_COLOR,$                ; COLOR TO USE FOR THE TXT [SEE TEXT FUNCTION]
            TXT_SIZE=TXT_SIZE,$                  ; SIZE TO USE FOR THE TXT [SEE TEXT FUNCTION]
            TXT_STYLE=TXT_STYLE,$                ; STYLE TO USE FOR THE TXT [SEE TEXT FUNCTION]
            TXT_ALIGN=TXT_ALIGN,$                ; HORIZONATL ALIGNMENT FOR THE TXT [SEE TEXT FUNCTION]
            TXT_VALGIN=TXT_VALIGN,$                ; VERTICAL ALIGNMENT FOR THE TXT [SEE TEXT FUNCTION]


;           #####    LEGEND  #####
            LEG_ADD=LEG_ADD,$                   ; DO PLOT THE TEXT AT THE XY COORDS
            LEG_TXT= LEG_TXT,$                     ; LEGEND TEXT
            LEG_POS= LEG_POS,$                     ; LEGEND TEXT
            LEG_FONT=LEG_FONT,$                  ; FONT TO USE FOR THE LEG [SEE TEXT FUNCTION]
            LEG_COLOR=LEG_COLOR,$                ; COLOR TO USE FOR THE LEG [SEE TEXT FUNCTION]
            LEG_SIZE=LEG_SIZE,$                  ; SIZE TO USE FOR THE LEG [SEE TEXT FUNCTION]
            LEG_STYLE=LEG_STYLE,$                ; STYLE TO USE FOR THE LEG [SEE TEXT FUNCTION]
            LEG_ALGIN=LEG_ALIGN,$                ; HORIZONATL ALIGNMENT FOR THE LEG [SEE TEXT FUNCTION]
            LEG_VALGIN=LEG_VALIGN,$              ; VERTICAL ALIGNMENT FOR THE LEG [SEE TEXT FUNCTION]
            LEG_FILL_BACKGROUND=LEG_FILL_BACKGROUND,$    ; 1=FILL LEG BACKGROUND [SEE TEXT FUNCTION]
            LEG_FILL_COLOR=LEG_FILL_COLOR,$              ; COLOR FOR LEG BACKGROUND [SEE TEXT FUNCTION]
            JAY=JAY
;+;+
; NAME:
;       PLT_XY
;
; PURPOSE:
; PLOT A SCATTER PLOT OF X AND Y ARRAYS AND THE FUNCTIONAL (OR OTHER) LINEAR REGRESSION LINE
;   AND OPTIONALLY PLOT THE REGRESSION STATISTICS AND A ONE2ONE LINE

; EXAMPLES:  SEE PLT_XY_DEMO

;
; INPUTS:
;   AN X AND Y ARRAY OF INTEGER, LONG, FLOAT, DOUBLE
;
; KEYWORD PARAMETERS:
;   SEE DESCRIPTIONS ABOVE
;  
; OUTPUTS:
;       DISPLAYS A SCATTER PLOT IN THE GRAPHICS WINDOW,
;       FUNCTIONAL REGRESSION LINE, AND REGRESSION STATISTICS
;       ALSO PRINTS REGRESSION STATISTICS

; RESTRICTIONS:
;
; NOTES:
;
;
; MODIFICATION HISTORY:
;   MAR 14,2014,JOR COPIED FROM PLOTXY AND MODIFIED FOR IDL NEW GRAPHICS FUNCTIONS
;   APR 11,2014,JOR MADE X,Y DOUBLE 
;   APR 20,2014,JOR ADDED PLT_GRIDS
;   APR 23,2014,JOR PLT_SLOPE & PLT_STRUCT
;   APR 27,2014,JOR REMOVED KEYWORD TXXT , ADDED PARAMETER Z (FOR TEXT INPUT)
;                   REMOVED KEYWORD LOGLOG, ADDED XLOG,YLOG
;   APR 30,2014,JOR,ADDED WINDOW FUNCTION AND FIXED MULTIPLE PLOTS WITHIN A WINDOW
;   MAY 1,2014,JOR, ALL GRAPHICS OBJECTS NOW CALLED OBJ
;                   LEG RENAMED STATS FOR CONSISTENCY WITH PLT_HIST2D AND OLDER PROS
;                   LEG IS RESERVED FOR LEGEND IN FUTURE VERSIONS 
;   MAY 1,2014 KJWH ADDED TXT_ALIGN
;                   ADDED TXT_VALGIN
;                   ADDED STATS_ALIGN
;                   ADDED PLT_DIMS (PLOT DIMENSIONS)
;                   ADDED DEVICE
;                   ADDED NORMAL
;                   ADDED DATA
;                   ADDED POSITION
;   MAY 1,2014,JOR  DEFAULTS ARE TO REQUEST THE REG, ONE2ONE,MEAN,STATS
;                   THE DEFAULT PLOT IS A SIMPLE SCATTER PLOT
;                   ADDED KEYWORD OBJ [TO OBTAIN THE PLT OBJECT] 
;   MAY 4,2014,JOR  ADDED CAPABILITY TO PLOT THE DATA LINE [/LIN_ADD]
;   MAY 20,2014, KJWH ADDED XY RANGE AND TICKNAME KEYWORDS
;                     CHANGED DEFAULT TITLE TO ''
;   JUL 4,2014,JOR:; ===> LOG DATA JUST FOR STATS2
;                         IF KEY(XLOG)THEN XL = ALOG10(XD) ELSE XL = XD
;                         IF KEY(YLOG)THEN YL = ALOG10(YD) ELSE YL = YD
;                         S = STATS2(XL,YL,MODEL=MODEL,PARAMS=PARAMS,DECIMALS=DECIMALS,SHOW=SHOW,FAST=FAST,DOUBLE_SPACE=DOUBLE_SPACE)
;   AUG 5,2014,JOR REMOVED ,/OVERPLOT  FROM IF KEY(LIN_ADD)
;   AUG 31,2014,JOR ADDED KEY JAY
;   SEP 7,2014,JOR,TXT_COLOR MAY NOW BE AN ARRAY OF DIFFERENT COLORS [ NOT JUST ONE]
;   SEP 10,2014,JOR:IF N_ELEMENTS(TXT_COLOR) EQ 1 THEN TXT_COLOR = REPLICATE(TXT_COLOR,N_ELEMENTS(Z))
;   OCT 21,2014,JOR ADDED LIN_MID_COLOR,LIN_MID_THICK,LIN_MID_STYLE
;   OCT 24,2014,JOR ADDED KEYS LEG_FILL_BACKGROUND,LEG_FILL_COLOR
;   NOV 18,2014,JOR ADDED DEFAULTS FOR KEY JAY
;   MAR 11,2015,JOR ADDED FLOORCEIL TO NICE_RANGE:
;                   IF N_ELEMENTS(XRANGE) NE 2 THEN XRANGE=NICE_RANGE(XD,EXPAND = EXPAND,/FLOORCEIL)
;   APR  7, 2015,KJWH: FIXED BUG AT PLOTXY (CHANGED LIN_MID_LINESTYLE TO LIN_MID_STYLE)
;   JUL 12,2015,JOR: REMOVED KEY FLOORCEIL IN NICE_RANGE ;  IF N_ELEMENTS(XRANGE) NE 2 THEN XRANGE=NICE_RANGE(XD,EXPAND = EXPAND,/FLOORCEIL)
;                    NOT SURE WHY BUT MAYBE DIFFERENCES IN 8.4 ??


;                   
;
;#########################################################################################################
;-
;**************************
ROUTINE_NAME =  'PLT_XY'
;**************************
  IF NONE(X) OR NONE(Y) THEN GOTO, DONE ;>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
;===> SET UP DEFAULTS FOR JAY
IF KEY(JAY) THEN BEGIN
  IF NONE(GRID_ADD) THEN GRID_ADD = 1
  IF NONE(REG_ADD) THEN REG_ADD = 1
  IF NONE(STATS_ADD) THEN STATS_ADD = 1
  IF NONE(EXPAND) THEN EXPAND = 1.1
  IF NONE(TXT_SIZE) THEN TXT_SIZE = 16
  LEG_SIZE = 22
  LEG_COLOR = 'BLUE'
  REG_COLOR = 'GREEN'
  REG_MID_COLOR= 'WHITE'
  ONE_COLOR = 'MAGENTA' 
  STATS_COLOR = 'GREEN' 
  IF ANY(Z) THEN SYMBOL = 'NONE'
ENDIF;IF KEY(JAY) THEN BEGIN  
;|||||||||||||||||||||||||||

;===> IS Z PRESENT AND OF TYPE STRING ?
  IF N_ELEMENTS(Z) GE 1 THEN BEGIN
    IF IDLTYPE(Z) NE 'STRING' THEN MESSAGE,'ERROR: THIRD INPUT MUST BE STRING'  
  ENDIF;IF N_ELEMENTS(Z) GE 1 THEN BEGIN

;===> PLOT COLORS,BACKGROUND & TITLES DEFAULT
  RGB_TABLE = RGBS()
  IF NONE(BACKGROUND_COLOR)   THEN BACKGROUND_COLOR = 'WHITE'
  IF NONE(TITLE)              THEN TITLE = ''
  IF NONE(XTITLE)             THEN XTITLE = 'X'
  IF NONE(YTITLE)             THEN YTITLE = 'Y'
  IF NONE(XSTYLE)             THEN XSTYLE = 2 ; SOME ROOM
  IF NONE(YSTYLE)             THEN YSTYLE = 2  
  IF NONE(MARGIN)             THEN MARGIN = [] ; Use IDLs default margin
  IF NONE(PLT_DIMS)           THEN PLT_DIMS = [1024,1024] ; A MODERATE SIZE FOR A PLOT WINDOW.  IF IT IS ANY LARGER, IT MAY NOT WORK ON ALL SCREENS.
  IF NONE(POSITION)           THEN POSITION = [] ; USE IDLS DEFAULT POSITION

;===> AXES DEFAULTS
  IF NONE(AXES_COLOR)         THEN AXES_COLOR = 'BLACK'
  IF NONE(AXES_FONT_SIZE)     THEN AXES_FONT_SIZE = 16
  IF NONE(AXES_THICK)         THEN AXES_THICK = 1
  IF NONE(ASPECT_RATIO)       THEN ASPECT_RATIO = 0
  IF NONE(EXPAND)             THEN EXPAND = 1.0

;===> GRID DEFAULTS
  IF NONE(GRID_ADD)          THEN GRID_ADD= 0
  IF NONE(GRID_COLOR)         THEN GRID_COLOR= 'BLACK'
  IF NONE(GRID_THICK)         THEN GRID_THICK= 2
  IF NONE(GRID_LINESTYLE)     THEN GRID_LINESTYLE= 0

;===> PLOT WINDOW DISPLAY DEFAULTS
  IF NONE(BUFFER)             THEN BUFFER= 0
  IF NONE(LAYOUT)             THEN LAYOUT= 0
  IF NONE(DELAY)              THEN DELAY= 0
  
  ;===> DATA IS THE  DEFAULT
  IF NONE(DATA) AND NONE(NORMAL) AND NONE(DEVICE) THEN DATA = 1
;===> SYMBOL DEFAULTS
  IF NONE(SYM_ADD)            THEN SYM_ADD= 0
  IF NONE(SYMBOL)             THEN SYMBOL = '*'
  IF NONE(SYM_SIZE)           THEN SYM_SIZE = 1
  IF NONE(SYM_THICK)          THEN SYM_THICK = 2
  IF NONE(SYM_COLOR)          THEN SYM_COLOR = 'MIDNIGHT BLUE'
  IF NONE(SYM_FILLED)         THEN SYM_FILLED = 0
  IF NONE(SYM_FILL_COLOR)     THEN SYM_FILL_COLOR = 'MIDNIGHT BLUE'
  IF NONE(SYM_CLIP)           THEN SYM_CLIP = 1   ; IDL DEFAULT
  
;===> DATA LINE DEFAULTS
  IF NONE(LIN_ADD)            THEN LIN_ADD = 0
  IF NONE(LIN_COLOR)          THEN LIN_COLOR = 'BLACK'
  IF NONE(LIN_STYLE)          THEN LIN_STYLE = 0
  IF NONE(LIN_THICK)          THEN LIN_THICK = 3
  IF NONE(LIN_MID_COLOR)      THEN LIN_MID_COLOR = 'BLACK'
  IF NONE(LIN_MID_STYLE)      THEN LIN_MID_STYLE = 0
  IF NONE(LIN_MID_THICK)      THEN LIN_MID_THICK = 1
;===> DEFAULT IS TO PLOT SYMBOLS IF :
  IF LIN_ADD EQ 0  AND SYM_ADD EQ 0 THEN SYM_ADD= 1
  IF SYM_ADD EQ 0                   THEN SYMBOL = 'NONE'; USE WHEN PLOTTING TXT AT X,Y

;===> MEAN [X,Y] DEFAULTS
  IF NONE(MEAN_ADD)           THEN MEAN_ADD= 0
  IF NONE(MEAN_SYMBOL)        THEN MEAN_SYMBOL = '+'
  IF NONE(MEAN_COLOR)         THEN MEAN_COLOR = 'GOLD'
  IF NONE(MEAN_SIZE)          THEN MEAN_SIZE = 5
  IF NONE(MEAN_THICK)         THEN MEAN_THICK = 5

;===> ONE2ONE LINE DEFAULTS
  IF NONE(ONE_ADD)            THEN ONE_ADD = 0
  IF NONE(ONE_COLOR)          THEN ONE_COLOR = 'BLACK'
  IF NONE(ONE_LINESTYLE)      THEN ONE_LINESTYLE = 0
  IF NONE(ONE_THICK)          THEN ONE_THICK = 3

;===> REGRESSION DEFAULTS [PLT_SLOPE]
  IF NONE(REG_ADD)           THEN REG_ADD = 0
  IF NONE(MODEL)              THEN MODEL = 'RMA' 
  IF NONE(DECIMALS)           THEN DECIMALS= 3 
  IF NONE(PARAMS)             THEN PARAMS= [1,5,6,7,11] 
  IF NONE(REG_FONT_SIZE)      THEN REG_FONT_SIZE = 21
  IF NONE(REG_COLOR)          THEN REG_COLOR = 'BLACK'
  IF NONE(REG_THICK)          THEN REG_THICK = 7
  IF NONE(REG_LINESTYLE)      THEN REG_LINESTYLE = 0
  IF NONE(REG_MID_COLOR)      THEN REG_MID_COLOR = 'BLACK' ; Should be the same as the regression line unless otherwise specified
  IF NONE(REG_MID_THICK)      THEN REG_MID_THICK = 3
  IF NONE(REG_MID_LINESTYLE)  THEN REG_MID_LINESTYLE = 0

;===> STATISTICAL LEGEND DEFAULTS [FOR PLT_STRUCT]
  IF NONE(STATS_ADD)         THEN STATS_ADD = 0
  IF NONE(STATS_POS)          THEN STATS_POS = [0.10,0.87]
  IF NONE(STATS_COLOR)        THEN STATS_COLOR = 'BLACK'
  IF NONE(STATS_SIZE)         THEN STATS_SIZE = 16
  IF NONE(STATS_ALIGN)        THEN STATS_ALIGN = 0
  IF NONE(DOUBLE_SPACE)       THEN DOUBLE_SPACE = 0

;===>  DEFAULTS FOR STATS TEXT
  IF NONE(TXT_ADD)            THEN TXT_ADD = 0
  IF NONE(TXT_FONT)           THEN TXT_FONT = "HELVETICA"
  IF NONE(TXT_COLOR)          THEN TXT_COLOR = 'MIDNIGHT_BLUE'
  IF NONE(TXT_SIZE)           THEN TXT_SIZE = 16
  IF NONE(TXT_STYLE)          THEN TXT_STYLE = 2
  IF NONE(TXT_ALIGN)          THEN TXT_ALIGN = 0.5
  IF NONE(TXT_VALIGN)         THEN TXT_VALIGN = 0.5

  
  ;===>  DEFAULTS FOR LEGEND TEXT
  IF NONE(LEG_ADD)                THEN LEG_ADD = 0
  IF NONE(LEG_TXT)                THEN LEG_TXT = 'Legend'

  IF NONE(LEG_POS)                THEN LEG_POS = [0.75,0.75]
  IF NONE(LEG_FONT)               THEN LEG_FONT = "HELVETICA"
  IF NONE(LEG_COLOR)              THEN LEG_COLOR = 'BLUE'
  IF NONE(LEG_SIZE)               THEN LEG_SIZE = 16
  IF NONE(LEG_STYLE)              THEN LEG_STYLE = 2
  IF NONE(LEG_ALIGN)              THEN LEG_ALIGN = 0.5
  IF NONE(LEG_VALIGN)             THEN LEG_VALIGN = 0.5
  IF NONE(LEG_FILL_BACKGROUND)    THEN LEG_FILL_BACKGROUND = 1
  IF NONE(LEG_FILL_COLOR)         THEN LEG_FILL_COLOR = 'WHITE_SMOKE'

  

; ===> COPY X AND Y INTO NEW VARIABLES, AND CONVERT TO DOUBLE PRECISION
  XD = DOUBLE(X)
  YD = DOUBLE(Y)

; ===> ELIMINATE INFINITE DATA OR DATA EQUAL TO MISSING CODE
  IF NONE(MISSINGX)          THEN MISSINGX = MISSINGS(XD)
  IF NONE(MISSINGY)          THEN MISSINGY = MISSINGS(YD)
  
  OK = WHERE(XD NE MISSINGX AND YD NE MISSINGY,COUNT)
  IF COUNT LT 2 THEN  MESSAGE, 'NOT ENOUGH OBSERVATIONS'
  XD = XD[OK] & YD = YD[OK]

; ===> GENERATE A NICE XRANGE AND YRANGE
;  IF N_ELEMENTS(XRANGE) NE 2 THEN XRANGE=NICE_RANGE(XD,EXPAND = EXPAND,/FLOORCEIL)
;  IF N_ELEMENTS(YRANGE) NE 2 THEN YRANGE=NICE_RANGE(YD,EXPAND = EXPAND,/FLOORCEIL)
  IF N_ELEMENTS(XRANGE) NE 2 THEN XRANGE=NICE_RANGE(XD,EXPAND = EXPAND)
  IF N_ELEMENTS(YRANGE) NE 2 THEN YRANGE=NICE_RANGE(YD,EXPAND = EXPAND)
;  ;  
; ===> CHECK IF OUTLIERS IS SET (ELIMINATE OUTLIER RATIOS OF 10:1, 1:10 ETC.)
  IF KEY(OUTLIERS) THEN BEGIN
    OUTLIERS = DOUBLE(OUTLIERS)
    OK = WHERE( (YD/XD) LT  OUTLIERS AND (YD/XD) GT (1.0D/OUTLIERS), COUNT  )
    XD = XD[OK] & YD = YD[OK]
    IF COUNT LT 2 THEN BEGIN
      PRINT, 'NOT ENOUGH OBSERVATIONS'
      GOTO, DONE
    ENDIF
  ENDIF
 
; ===> LOG DATA JUST FOR STATS2
 IF KEY(XLOG)THEN XL = ALOG10(XD) ELSE XL = XD
 IF KEY(YLOG)THEN YL = ALOG10(YD) ELSE YL = YD
  S = STATS2(XL,YL,MODEL=MODEL,PARAMS=PARAMS,DECIMALS=DECIMALS,DOUBLE_SPACE=DOUBLE_SPACE)
  
; ===> PLOT DATA SYMBOLS ?
; IF KEY(SYM_ADD) THEN PLT = PLOT(XD,YD,$
  IF KEY(SYM_ADD) THEN PLT = PLOT(XD,YD,$
    BACKGROUND_COLOR=BACKGROUND_COLOR, ASPECT_RATIO=ASPECT_RATIO,$
    BUFFER=BUFFER ,CURRENT=CURRENT,$
    DIMENSIONS=PLT_DIMS, LAYOUT=LAYOUT, POSITION=POSITION, MARGIN=MARGIN,$
    TITLE=TITLE, XTITLE=XTITLE, YTITLE=YTITLE,$
    XLOG=XLOG, YLOG=YLOG, XRANGE=XRANGE, YRANGE=YRANGE, XSTYLE=XSTYLE, YSTYLE=YSTYLE,XTICKNAME=XTICKNAME,YTICKNAME=YTICKNAME,$
    XTICKV=XTICKV,$
    YTICKV=YTICKV,$
    XMINOR=XMINOR,$
    YMINOR=YMINOR,$
    FONT_SIZE=AXES_FONT_SIZE, LINESTYLE='NONE',$
    SYMBOL=SYMBOL, SYM_FILLED=SYM_FILLED, SYM_COLOR=SYM_COLOR, SYM_FILL_COLOR=SYM_FILL_COLOR, SYM_SIZE=SYM_SIZE, SYM_THICK=SYM_THICK, CLIP=SYM_CLIP,$
    XTICKFORMAT='(G0)', XCOLOR=AXES_COLOR, XTHICK=AXES_THICK,$
    YTICKFORMAT='(G0)', YCOLOR=AXES_COLOR, YTHICK= AXES_THICK)
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

;##########################################################################
; ===> PLOT DATA AS A LINE/CURVE ?
  IF KEY(LIN_ADD) THEN BEGIN
    PLT = PLOT(XD,YD,/NO_TOOLBAR,$
    BACKGROUND_COLOR=BACKGROUND_COLOR, ASPECT_RATIO=ASPECT_RATIO,$
    BUFFER=BUFFER ,CURRENT=CURRENT,$
    DIMENSIONS=PLT_DIMS, LAYOUT=LAYOUT, POSITION=POSITION, MARGIN=MARGIN,$
    TITLE=TITLE, XTITLE=XTITLE, YTITLE=YTITLE,$
    XTICKNAME = XTICKNAME,YTICKNAME = YTICKNAME,$
    XTICKV=XTICKV,$
    YTICKV=YTICKV,$
    XMINOR=XMINOR,$
    YMINOR=YMINOR,$
    XSTYLE=XSTYLE,$
    YSTYLE=YSTYLE,$
    XLOG=XLOG, YLOG=YLOG, XRANGE=XRANGE, YRANGE=YRANGE,$
    FONT_SIZE=AXES_FONT_SIZE, SYMBOL ='NONE',$
    COLOR=LIN_COLOR,THICK=LIN_THICK,$
    XTICKFORMAT='(G0)', XCOLOR=AXES_COLOR, XTHICK=AXES_THICK,$
    YTICKFORMAT='(G0)', YCOLOR=AXES_COLOR, YTHICK= AXES_THICK)
    IF LIN_THICK GE 3 THEN  PLT=PLOT(X,Y,/OVERPLOT,COLOR = LIN_MID_COLOR,THICK = LIN_MID_THICK,LINESTYLE =LIN_MID_STYLE)

ENDIF;IF KEY(LIN_ADD) THEN BEGIN
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||


; ===> ADD GRIDLINES ?
  IF  KEY(GRID_ADD) THEN PLT_GRIDS, PLT, COLOR=GRID_COLOR, THICK=GRID_THICK, LINESTYLE=GRID_LINESTYLE
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||


; ===> ADD SLOPE ?
 ;; IF  KEY(REG_ADD) THEN PLT_SLOPE, PLT, STRUCT=S,XLOG=XLOG,YLOG=YLOG,$
  IF  KEY(REG_ADD) THEN BEGIN
    PLT_SLOPE, PLT,STRUCT=S,$
   REG_COLOR=REG_COLOR,REG_THICK=REG_THICK,REG_LINESTYLE=REG_LINESTYLE,$
   REG_MID_COLOR=REG_MID_COLOR,REG_MID_THICK=REG_MID_THICK,REG_MID_LINESTYLE=REG_MID_LINESTYLE

  ENDIF
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

; ===> ADD ONE TO ONE LINE ?
  IF  KEY(ONE_ADD) THEN PLT_ONE2ONE, PLT, COLOR=ONE_COLOR, LINESTYLE=ONE_LINESTYLE, THICK=ONE_THICK
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||


; ===> ADD MEAN ?
  IF  KEY(MEAN_ADD) THEN PLT_MEAN, PLT, SYMBOL=MEAN_SYMBOL, SYM_SIZE=MEAN_SIZE, SYM_COLOR=MEAN_COLOR, SYM_THICK=MEAN_THICK, XLOG=XLOG, YLOG=YLOG
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||


; ===> ADD STATS LEGEND
  IF  KEY(STATS_ADD) THEN PLT_STRUCT, PLT, STRUCT=S, POS=STATS_POS, COLOR=STATS_COLOR, FONT_SIZE=STATS_SIZE, THICK=3
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||


; ===> PLOT Z AT X,Y LOCATIONS 
  IF  N_ELEMENTS(Z) GE 1 THEN BEGIN
    IF N_ELEMENTS(TXT_COLOR) EQ 1 THEN TXT_COLOR = REPLICATE(TXT_COLOR,N_ELEMENTS(Z))
    SETS=WHERE_SETS(TXT_COLOR)
    
    ;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
    FOR S = 0,N_ELEMENTS(SETS)-1 DO BEGIN
      SET = SETS(S)
      SUBS = WHERE_SETS_SUBS(SET)
      COLOR = SET.VALUE
      T = TEXT(XD(SUBS), YD(SUBS), STRTRIM(Z(SUBS),2), COLOR=COLOR, FONT_SIZE=TXT_SIZE, FONT_STYLE=TXT_STYLE,$
      FONT_NAME=TXT_FONT, TARGET=PLT, DEVICE=DEVICE, DATA=DATA, NORMAL=NORMAL, ALIGNMENT=TXT_ALIGN, VERTICAL_ALIGNMENT=TXT_VALIGN)
    ENDFOR;FOR S = 0,N_ELEMENTS(SETS)-1 DO BEGIN
    ;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
  ENDIF;IF  N_ELEMENTS(Z) GE 1 THEN BEGIN
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||

;===> PLOT THE LEGEND ?
IF KEY(LEG_ADD) THEN BEGIN
  T = TEXT(LEG_POS[0],LEG_POS[1],/RELATIVE,LEG_TXT,FONT_SIZE = LEG_SIZE,COLOR = LEG_COLOR, TARGET=PLT, ALIGNMENT=LEG_ALIGN, VERTICAL_ALIGNMENT=LEG_VALIGN,FILL_BACKGROUND = LEG_FILL_BACKGROUND,FILL_COLOR=LEG_FILL_COLOR)
  
ENDIF;IF KEY(LEG_ADD) THEN BEGIN
;||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||


WAIT,DELAY
;===> CONSERVE THE PLT OBJECT IN OBJ
OBJ = PLT

;##########################
  IF KEY(FILE) THEN PLT_WRITE,PLT,FILE=FILE,BORDER=BORDER,BIT_DEPTH=BIT_DEPTH,APPEND=APPEND,CLOSE=CLOSE
  IF KEY(CLOSE) THEN BEGIN
   ; WAIT,DELAY    
    ;OBJ.CLOSE
  ENDIF;IF KEY(CLOSE) THEN BEGIN

  
DONE:   ; DONE HERE IF INSUFFICIENT NUMBER OF OBSERVATIONS
 
END; #####################  END OF ROUTINE ################################

