; $ID:	MAPS_OLD_VS_NEW.PRO,	2020-07-01-12,	USER-KJWH	$
; #########################################################################; 
PRO MAPS_OLD_VS_NEW, MAPS
;+
; PURPOSE:  COMPARE THE OLD MAPS [E.G. USING THE MAP_NEC.PRO] WITH THE NEW MAPS_SET [USING THE MAPS_MASTER.CSV]
;
; CATEGORY: MAPS;
;
; CALLING SEQUENCE: 
;
; INPUTS: MAPS..... MAP NAMES [E.G. 'NEC'] ASSUMES THE MAP_*.PRO'S ARE IN !S.OBSOLETE
;
; KEYWORDS:
;         ..... NONE

; EXAMPLES:
;          MAPS_OLD_VS_NEW, 'EC'
;          MAPS_OLD_VS_NEW, 'SMI'
;          MAPS_OLD_VS_NEW, 'NEC'
;          MAPS_OLD_VS_NEW, 'LME_SOMALI_COASTAL_CURRENT'
;          MAPS_OLD_VS_NEW, ['NEA']

; OUTPUTS: A PNG IMAGE [PER MAP] SHOWING OLD AND NEW COASTLINES
;          A CSV FILE WITH MAP NAME,PROJECTION,MSTR_SCALE AND PCT [OVERLAP]
;          
;
;
; MODIFICATION HISTORY:
;     JAN 30, 2016  WRITTEN BY: J.E. O'REILLY
;     JAN 31,2016,JOR ADDED KEY USE_SCALE
;     FEB 21,2016,JOR ADDED PLOTGRAT 
;     MAR 06,2016,JOR ADDED MSTR_SCALE TO OUTPUT STRUCT
;     MAR 09,2016,JOR RENAMED PCT_OVERLAP TO PCT [TO AGREE WITH OTHER PROS]
;     MAR 18,2016,JOR REMOVED KEY USE_SCALE
;-
;
; #########################################################################

;*********************************
ROUTINE_NAME  = 'MAPS_OLD_VS_NEW'
;*********************************
;===> INITIALIZE THE MAPS_MASTER
M = MAPS_READ(/INIT)
OLD_COLOR = 26
NEW_COLOR = 34
DEG = 1.0/6.0
FONT_HELVETICA
PAL_36,R,G,B
DO_TRACK = 0 ; USES NEW TRACK PROGRAM TO INVESTIGATE WHICH LINES OF CODE ARE OR ARE NOT EXECUTED
DIR_OUT = !S.DEMO + ROUTINE_NAME + PATH_SEP()& DIR_TEST,DIR_OUT

;===> GET ALL THE MAPS THAT USE MAP_SET [/TRUE]
IF NONE(MAPS) THEN MAPS =  MAPS_READ(/NAMES,/INIT,/TRUE)

;===> MAKE A DATABASE TO HOLD THE PERCENT COINCIDENCE BETWEEN OLD AND NEW MAPS
DB = CREATE_STRUCT('MAP','','PROJ','','MSTR_SCALE',0D,'PCT',0D)
DB = REPLICATE(DB,NOF(MAPS))
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
FOR NTH = 0,N_ELEMENTS(MAPS)-1 DO BEGIN
  MAPP = MAPS[NTH] & PFILE,MAPP,/U
  M = MAPS_SIZE(MAPP) & PX = M.PX  & PY = M.PY
  ZWIN,[PX,PY]
  FILE = !S.OBSOLETE + 'MAP_' + MAPP + '.pro'
  IF FILE_TEST(FILE) EQ 0 THEN REPORT,'ERROR CAN NOT FIND: ' + FILE
  ;===> CALL OLD MAP_SET PROGRAM 
  MAP_PRO = 'MAP_' + MAPP
  CD,!S.OBSOLETE
  RESOLVE_ROUTINE,MAP_PRO,/EITHER,/COMPILE_FULL_FILE
  CALL_PROCEDURE,MAP_PRO,SCALE = PRO_SCALE
  CD,!S.PROGRAMS
  ERASE,0
  MAP_CONTINENTS,/HIRES,/COASTS,COLOR = OLD_COLOR
  PLOTGRAT,DEG,COLOR = OLD_COLOR
  OLD = TVRD()
  OK_OLD = WHERE(OLD EQ OLD_COLOR,COUNT_OLD)
  ZWIN
  ;===> TRACK MAP_SET ?
  IF DO_TRACK THEN TRACK,'MAP_SET'
  IF DO_TRACK THEN TRACK,'MAP_PROJ_INFO'
  ;===> NOW MAKE THE NEW MAP 
  MSTR_SCALE = (MAPS_READ(MAPP)).SCALE
  PROJ       = (MAPS_READ(MAPP)).PROJ
  MAPS_SET,MAPP,BKG_COLOR=255 
  
  ;===> TRACK MAP_SET ?
  IF DO_TRACK THEN TRACK,'MAP_SET'
  IF DO_TRACK THEN TRACK,'MAP_PROJ_INFO'
  
  MAP_CONTINENTS,/HIRES,/COASTS,COLOR = NEW_COLOR
  PLOTGRAT,DEG,COLOR = NEW_COLOR
  NEW = TVRD()
  OK_NEW = WHERE(NEW EQ NEW_COLOR,COUNT_NEW)
  ZWIN
  

  OK = WHERE(OLD EQ OLD_COLOR AND NEW EQ NEW_COLOR,COUNT)
  PCT= (100. *COUNT/ COUNT_OLD )
  PREP,'PERCENT OVERLAP : ' + ROUNDS(PCT,1),5
  DB[NTH].MAP = MAPP
  DB[NTH].PROJ = PROJ
  DB[NTH].MSTR_SCALE = MSTR_SCALE
  DB[NTH].PCT = ROUNDS(PCT,1)
  
  BOTH = REPLICATE(255,[PX,PY]) & BOTH(OK_OLD) = OLD_COLOR & BOTH(OK_NEW) = NEW_COLOR
  ZWIN,BOTH
  TV,BOTH
  XYOUTS,/NORMAL,0.55,0.55,'NEW',CHARSIZE = 4,COLOR = NEW_COLOR,CHARTHICK = 2
  XYOUTS,/NORMAL,0.50,0.50,'OLD',CHARSIZE = 4,COLOR = OLD_COLOR,CHARTHICK = 2
  XYOUTS,/NORMAL,0.40,0.40,' % OVERLAP : ' + ROUNDS(PCT,4),COLOR = 0,CHARSIZE = 2,CHARTHICK = 2
  BOTH = TVRD()
  ZWIN
  PAL_36,R,G,B
  PNGFILE = DIR_OUT + MAPP + '-OLD-VS-NEW' +  '.PNG'
  WRITE_PNG,PNGFILE,BOTH,R,G,B  & PFILE,PNGFILE,/W  
ENDFOR;FOR NTH = 0,N_ELEMENTS(MAPS)-1 DO BEGIN
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF

;===> SORT ASCENDING ON PCT
S = SORT(DB.PCT) & DB = DB(S)
CSV = DIR_OUT + ROUTINE_NAME + '.CSV'
IF NOF(MAPS) EQ 1 THEN  CSV = DIR_OUT+ ROUTINE_NAME +'-' + MAPP + '.CSV'
CSV_WRITE,CSV,DB 
;===> JOIN & COMPARE TRACK RESULTS?
IF DO_TRACK EQ 1 THEN BEGIN
  TRACK_COMPARE,'MAP_SET'
  TRACK_COMPARE,'MAP_PROJ_INFO'
ENDIF;IF DO_TRACK EQ 1 THEN BEGIN



END; #####################  END OF ROUTINE ################################
