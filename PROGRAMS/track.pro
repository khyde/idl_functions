; $ID:	TRACK.PRO,	2020-06-03-17,	USER-KJWH	$
; #########################################################################; 
PRO TRACK,ROUTINE
;+
; PURPOSE:  TRACKS WHICH LINES OF A PROGRAM WERE AND WERE NOT EXECUTED [USING IDL'S CODE_COVERAGE]
;
; CATEGORY: UTILITY;
;
;
; INPUTS: ROUTINE..... THE NAME OF ROUTINE TO TRACK [AFTER IT HAS RUN]
;
;
; KEYWORDS:  NONE
;          
; OUTPUTS:  CSV [WITH DATE STAMP] OF ROUTINE NAME 
;           & LINES WITH CODES FOR EXECUTED [1] AND NOT EXECUTED [0] 
;
;; EXAMPLES:
;           TRACK,'MAP_SET'
;
;
;  NOTES:
;  RUN TRACK AFTER THE NAMED ROUTINE HAS RUN [SEE IDL'S HELP ON CODE_COVERAGE]
;MOTIVATION FOR TRACK WAS TO BE ABLE TO DETERMINE WHY MAP_SET 
;BEHAVES DIFFERENTLY WHEN USING OLD MAP_ PROS VERSUS MAPS_SET
;
; MODIFICATION HISTORY:
;     FEB 28, 2016  WRITTEN BY: J.E. O'REILLY
;     MAR 02,2016,JOR ADDED NOTE TO OUTPUT STRUCT,ADDED WAIT,2 
;                    [TO ENSURE A NEWER TIME STAMP WHEN TRACK IS CALLED TWICE IN THE SAME PROGRAM]
;     MAR 10,2016,JOR NOW USING SORTED FUNCTION
;-
; #########################################################################

;**********************
ROUTINE_NAME  = 'TRACK'
;**********************

NOT_EX = CODE_COVERAGE( ROUTINE, EXECUTED=EX, /INCLUDE_END, NLINES=NLINES )

S = REPLICATE(CREATE_STRUCT('ROUTINE',ROUTINE,'LINE',0,'EXE',0,'NOTE',''),NLINES)
;===> FILL S WITH THE EXECUTED INFO
START = 0 & LST = NOF(EX)-1
S(START:LST).LINE = EX
S(START:LST).EXE = 1
;===> FILL S WITH THE NOT_EXECUTED INFO
START = NOF(EX) & LST = NOF(S)-1
S(START:LST).LINE = NOT_EX
S(START:LST).EXE = 0  
;===> SORT BY LINE #
SUBS = SORTED(S.LINE,/SUBS) & S = S(SUBS) 

;===> CLEAR MEMORY RELATED TO ROUTINE
NOT_EX = CODE_COVERAGE( ROUTINE, /CLEAR )
FILE = !S.IDL_TEMP + ROUTINE_NAME + '-' + ROUTINE + '-' + DATE_NOW()  + '.CSV'
CSV_WRITE,FILE,S
WAIT,2
END; #####################  END OF ROUTINE ################################
