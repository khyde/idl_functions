; $ID:	RADTRAN.PRO,	2020-06-26-15,	USER-KJWH	$
 
  PRO RADTRAN,LON=LON,LAT=LAT,DOY=DOY,HR=HR,SUNZEN=SUNZEN,ED=ED,PAR=PAR,STRUCT=STRUCT,WAVELENGTHS=WAVELENGTHS,QUIET=QUIET

;  PURPOSE: COMPUTES SPECTRAL SOLAR IRRADIANCE JUST BELOW THE SURFACE GIVEN
;  METEOROLOGICAL DATA.
;    DIRECT AND DIFFUSE COMPONENTS ARE BROKEN OUT SEPARATELY AND RETURNED AS A STRUCTURE
;    IF RUN INTERACTIVELY, THE PARAMETERS ARE WRITTEN TO DATA FILE
; 
;  WATSON W. GREGG, NASA/GODDARD SPACE FLIGHT CENTER (301) 614-5711.

;  REFERENCE: GREGG, W.W. AND K.L. CARDER, 1990.  A SIMPLE SPECTRAL SOLAR IRRADINACE MODEL FOR CLOUDLESS MARITIME ATMOSPHERES.
;  LIMNOLOGY AND OCEANOGRAPHY 35: 1657-1675.

;  HISTORY:
;  ORIGINAL PROGRAM WRITTEN BY WATSON GREGG (GSFC,NASA) IN FORTRAN.
;  
;  THE FORTRAN VERSION BY WATSON GREGG IS A SOPHISTICATED, LONG (454 LINES) PROGRAM WITH 5 SUBROUTINES:
;    LIDATA 
;    ATMODD 
;    NAVAER 
;    SFCRFL 
;    SUNANG
;  
; RADTRAN.F AND THESE 5 SUBROUTINES IN RADTRAN WERE TRANSLATED TO IDL PROGRAMS BY JAY O'REILLY (NOAA,NMFS) JULY 28, 2010

; REQUIREMENTS:
;   A DIRECTORY:  D:\IDL\RADTRAN
;   THREE INPUT DATA FILES MUST BE IN THE RADTRAN DIRECTORY:
;     ETLIGHT.DAT
;     OZPAR.DAT
;     WVPAR.DAT
; THE FOLLOWING ADDITTIONAL IDL PROGRAMS ARE NECESSARY TO RUN RADTRAN!
;   LIDATA.PRO 
;   ATMODD.PRO 
;   NAVAER.PRO 
;   SFCRFL.PRO 
;   SUNANG.PRO
; 
; KEYWORDS:
;    LON=LONGITUDE (IN WHOLE OR DECIMAL DEGREES)
;    LAT=LATITUDE  (IN WHOLE OR DECIMAL DEGREES)
;    DOY=DAY OF YEAR (1-366)
;    HR=HOUR OF DAY (GMT IN DECIMAL HOURS)
;    SUNZEN=SOLAR ZENITH ANGLE (IN WHOLE OR DECIMAL DEGREES,-99 WILL MAKE RADTRAN CALCULATE SUNZEN FROM LON,LAT,DOY,HR)
;    ED=TOTAL SOLAR IRRADIANCE JUST BELOW THE SURFACE
;    PAR= PHOTOSYNTHETICALL ACTICE RADIATION JUST BELOW THE SURFACE
;    STRUCT A STRUCTURE WITH EDIF EDIR AND ED BY WAVELENGTH FROM 350-700 NANOMETERS
;  
;  
;  OUTPUTS:
;    SFCIRR.DAT IS WRITTEN TO DEFAULT IDL DIRECTORY UNLESS KEYWORD QUIET IS USED
;    SFCIRR.DAT CONTAINS A LISTING/TABLE OF EDIR[DIRRECT],EDIF[DIFFUSE],ED[ED+EDIF], BY WAVELENGTH, (LAM,EDIR,EDIF,ED)
;      EDIF,EDIR ED,PAR ( SEE KEYWORDS ABOVE )
;    IF KEYWORD STRUCT IS PROVIDED AND RADTRAN IS RUN IN THE NON-INTERACTIVE MODE THEN THE DATA IN SFCIRR.DAT IS CONTAINED IN STRUCT
;      
;  EXAMPLES:
;    RADTRAN (YOU WILL BE PROMPTED FOR ALL INPUTS AND SFCIRR.DAT WILL BE WRITTEN TO THE DEFAULT IDL DIRECTORY)
;    
;    IF YOU PROVIDE THE ESSENTIAL INPUTS THE OTHER VARIABLES WILL BE SET TO THE DEFAULT VALUES, WITHOUT PROMPTING    
;    RADTRAN, LON=LON,LAT=LAT,DOY=DOY,HR=HR,SUNZEN=SUNZEN
;    
;    SUNZEN WILL BE CALCULATED FROM LON,LAT,DOY AND HOUR NO PROMPTING FOR INPUTS
;     RADTRAN, LON= -70,LAT=44.0,DOY=172,HR=17.0, 
;     RADTRAN, LON= -70,LAT=44.0,DOY=172,HR=17.0, SUNZEN= -99,ED=ED& HELP,ED

  ROUTINE_NAME= 'RADTRAN'
  
;  IF ALL ESSENTIAL INPUTS ARE PROVIDED SET INTERACTIVE MODE OFF (NO PROMPTING,SET OTHER VARIABLES TO THE DEFAULTS >>>>>
  IF N_ELEMENTS(LON) GE 1 AND N_ELEMENTS(LAT) GE 1 AND N_ELEMENTS(DOY) EQ 1 AND N_ELEMENTS(HR) EQ 1 THEN BEGIN 
    DO_PROMPT = 0 
    VIS=15.0
    AM=1.0
    WSM = 4.0
    WS = 6.0
    PRESS=1013.25
    RH=80.0
    WV=1.5
    IF N_ELEMENTS(SUNZEN) EQ 0 THEN THE = -99 ELSE THE = SUNZEN
  ENDIF ELSE DO_PROMPT = 1 ; INTERACTIVE MODE
  
; SET UP ARRAYS FOR THE PARAMETERS (ED AND PAR ARE DEFINED FROM (350-700) NANOMETERS (INCLUSIVE) SO WE NEED 351 ELEMENTS IN THE ARRAYS).
  NLT=351
  LAM   = DBLARR(NLT)
  FOBAR = DBLARR(NLT)
  FO    = DBLARR(NLT)
  OZA   = DBLARR(NLT)
  AG    = DBLARR(NLT)
  AW    = DBLARR(NLT)
  
  
; CONSTANTS
  PI=!PI
  PI2 = 2.0D *PI
  RAD = 180.0D /PI
  H = 6.6256E-34
  C = 2.998E8
  HC = 1.0/(H*C)
  
  IF DO_PROMPT EQ 1 THEN BEGIN
    DOY = 1.5
    HR  = 1.5
    LON = 0.0
    LAT = 0.0
    THE = -99.0
    VIS = 15.0
    AM  = 1.0
    WSM = 4.0
    WE  = 6.0
    PRESS = 1013.25
    RH  = 80.0
    WV  = 1.5
    
    READ,DOY,PROMPT='ENTER DAY OF YEAR (0-365 OR 366)'
    READ,HR,PROMPT='ENTER TIME IN HOURS GMT'
    READ,LON,PROMPT='ENTER LONGITUDE (WEST IS NEGATIVE)'
    READ,LAT,PROMPT='ENTER LATITUDE (NORTH IS POSITIVE)'
    READ,THE,PROMPT='ENTER SOLAR ZENITH ANGLE OR ENTER -99 TO  CALCULATE FROM DAY AND EARTH POSITION' ; METEOROLOGICAL DATA
    READ,VIS,PROMPT='ENTER VISIBILITY IN KM OR ENTER 15 FOR DEFAULT' 
    READ,AM,PROMPT='ENTER AIR MASS (1-10) OR ENTER 1 FOR DEFAULT'   
    READ,WSM,PROMPT='ENTER MEAN WIND SPEED, OR ENTER 4.0,  FOR DEFAULT'  
    READ,WS,PROMPT='ENTER CURRENT WIND SPEED, CURRENT WIND SPEED (M/S) OR ENTER 6.0 FOR DEFAULT'  
    READ,PRESS,PROMPT='ENTER PRESSURE IN MILLIBARS OR ENTER 1013.25 FOR STANDARD'  
    READ,RH, PROMPT='ENTER RELATIVE HUMIDITY OR ENTER 80 FOR DEFAULT'
    READ,WV,PROMPT='ENTER WATER VAPOR IN CM OR ENTER 1.5 FOR DEFAULT'   
  ENDIF ; IF DO_PROMPT EQ 1 THEN BEGIN

; SET UP ARRAYS FOR THE CONSTANTS USED IN THE OZONE SCALE HEIGHT CALCULATION 
  NLAT   = N_ELEMENTS(LAT)
  AVH    = DBLARR(NLAT)
  CVH    = DBLARR(NLAT)
  FVH    = DBLARR(NLAT)
  HVH    = DBLARR(NLAT)
  BETAVH = DBLARR(NLAT)
  RIVH   = DBLARR(NLAT)
    
; COMPUTE OZONE SCALE HEIGHT IN CM (FROM VAN HEUKLON, 1979)
  OK_LAT = WHERE(LAT GT 0.0,COUNT_LAT,COMPLEMENT=COMPLEMENT,NCOMPLEMENT=NCOMPLEMENT) 
  IF COUNT_LAT GT 0 THEN BEGIN
    AVH(OK_LAT) = 150.0
    CVH(OK_LAT) = 40.0
    FVH(OK_LAT) = -30.0
    HVH(OK_LAT) = 3.0
    BETAVH(OK_LAT) = 1.28
  ENDIF  
  IF NCOMPLEMENT GT 0 THEN BEGIN      
    AVH(COMPLEMENT) = 100.0
    CVH(COMPLEMENT) = 30.0
    FVH(COMPLEMENT) = 152.625
    HVH(COMPLEMENT) = 2.0
    RIVH(COMPLEMENT) = -75.0
    BETAVH(COMPLEMENT) = 1.5
  ENDIF
  OK = WHERE(LAT GT 0.0 AND LON LT 0.0,COUNT) & IF COUNT GE 1 THEN RIVH[OK] = 20.0
  OK = WHERE(LAT GT 0.0 AND LON GE 0.0,COUNT) & IF COUNT GE 1 THEN RIVH[OK] = 0.0
  
  TO3 = 235.0 + (AVH+CVH*SIN(0.9865*(DOY+FVH))+20.0*SIN(HVH*(LON+RIVH)/RAD))*SIN(BETAVH*LAT/RAD)*SIN(BETAVH*LAT/RAD)
  SCO3 = TO3*1.0E-3
  IF THE EQ -99 THEN BEGIN
    SUNANG,DOY,HR,RAD,LON,LAT,SUNZ ; CALL_PROCEDURE,'SUNANG', DOY,HR,RAD,LON,LAT,SUNZ
    THETA = SUNZ
  ENDIF ELSE THETA = THE  
  IF NOT KEYWORD_SET(QUIET) THEN BEGIN
    PRINT,'OZONE CLIMATOLOGICAL VALUES WERE COMPUTED FROM THE DAY AND EARTH POSITION'  
    PRINT,'THETA       = ',NUM2STR(THETA,DECIMALS=3)
    PRINT,'AIR MASS    = ',NUM2STR(AM,DECIMALS=3)           
    PRINT,'VISIBILITY  = ',NUM2STR(VIS,DECIMALS=3),' KM'
    PRINT,'RH          = ',NUM2STR(RH,DECIMALS=3),' %'
    PRINT,'PRESSURE    = ',NUM2STR(PRESS,DECIMALS=3),' MB'
    PRINT,'WSBAR       = ',NUM2STR(WSM,DECIMALS=3)
    PRINT,'WS          = ',NUM2STR(WS,DECIMALS=3),' M/S'
    PRINT,'WATER VAPOR = ',NUM2STR(WV,DECIMALS=3),' CM'      
    PRINT,'CLIMATOLOGICAL OZONE = ',NUM2STR(TO3,DECIMALS=3), ' DU'
  ENDIF  

; READ LIGHT DATA, WHICH INCLUDES OXYGEN AND WATER VAPOR ABSORPTION
  LIDATA, LAM, FOBAR, OZA, AG, AW   ; CALL_PROCEDURE, 'LIDATA',LAM,FOBAR,OZA,AG,AW    
  RDAY = DOY + HR/24.0  ; CORRECT FOR EARTH-SUN DISTANCE
  FOR  NL = 0, NLT-1 DO BEGIN
    FO(NL) = FOBAR(NL)* (1.0+1.67E-2*COS(PI2*(RDAY-3.0)/365.0))^ 2
  ENDFOR

; IF MULTIPLE LAT/LON POINTS ARE PROVIDED, THEN LOOP ON WAVELENGTH  
  IF N_ELEMENTS(THETA) GT 1 THEN BEGIN
    EDIR  = DBLARR(N_ELEMENTS(THETA))
    EDIF  = DBLARR(N_ELEMENTS(THETA))
    ED    = DBLARR(N_ELEMENTS(THETA))
      
;   IF WAVELENGTHS ARE PROVIDED, THEN JUST CALCULATE ED FOR THOSE SPECIFIC WAVELENGTHS
    IF N_ELEMENTS(WAVELENGTHS) GE 1 THEN WAVES = WAVELENGTHS ELSE WAVES = LAM    
    FOR LTH=0L, N_ELEMENTS(WAVES)-1 DO BEGIN
      OK_WAVE = WHERE(LAM EQ WAVES(LTH),COUNT_WAVE)
      IF COUNT_WAVE EQ 0 THEN CONTINUE      
      TAG = 'W' + NUM2STR(LAM(OK_WAVE))
      ATMODD, RAD,LAM(OK_WAVE),THETA,OZA(OK_WAVE),AG(OK_WAVE),AW(OK_WAVE),SCO3,PRESS,WV, RH,AM,WSM,WS,VIS,FO(OK_WAVE),/WAVELENGTH_SPECIFIC,EDIR,EDIF,ED  ; CALL_PROCEDURE, 'ATMODD', RAD,LAM,THETA,OZA,AG,AW,SCO3,PRESS,WV, RH,AM,WSM,WS,VIS,FO,EDIR,EDIF,ED
      OK_THETA = WHERE(THETA GT 90.0, COMPLEMENT=COMPLEMENT, NCOMPLEMENT=NCOMPLEMENT, COUNT_THETA)
      IF COUNT_THETA GE 1 THEN BEGIN
        EDIR(OK_THETA) = 0.0 ; SET EDIR TO ZERO
        EDIF(OK_THETA) = 0.0 ; SET EDIF TO ZERO
        ED(OK_THETA)   = 0.0 ; SET ED TO ZERO        
        PRINT,'THE SUN IS BELOW HORIZON (IRRADIANCE = 0) FOR ' + NUM2STR(COUNT_THETA) + ' LOCATIONS'
      ENDIF
      IF NCOMPLEMENT GT 0 THEN BEGIN
        EDIR(COMPLEMENT) = EDIR ; FILL IN EDIR
        EDIF(COMPLEMENT) = EDIF ; FILL IN EDIF
        ED(COMPLEMENT)   = ED   ; FILL IN ED         
      ENDIF   
      IF LTH EQ 0 THEN STRUCT = CREATE_STRUCT(TAG,CREATE_STRUCT('EDIR',EDIR,'EDIF',EDIF,'ED',ED)) $
                  ELSE STRUCT = CREATE_STRUCT(STRUCT,CREATE_STRUCT(TAG,CREATE_STRUCT('EDIR',EDIR,'EDIF',EDIF,'ED',ED)))
    ENDFOR
  ENDIF ELSE BEGIN
    ATMODD, RAD,LAM,THETA,OZA,AG,AW,SCO3,PRESS,WV, RH,AM,WSM,WS,VIS,FO,EDIR,EDIF,ED
    PAR = TOTAL((LAM*1.0e-9)*ED)*HC/6.023e17
    IRR = TOTAL(ED)  
    IF NOT KEYWORD_SET(QUIET) THEN PRINT,'TOTAL IRRADIANCE = ',NUM2STR(IRR,DECIMALS=3),' (W/M2)'
    STRUCT = CREATE_STRUCT('WAVELENGTH',LAM,'EDIR',EDIR,'EDIF',EDIF,'ED',ED)
  ENDELSE     
      
  IF DO_PROMPT EQ 1 THEN BEGIN ; WRITE TO DATA FILE
    OPENW,4,'SFCIRR.DAT'
    FOR  NL = 0,NLT-1 DO BEGIN
      PRINTF,4,LAM(NL),EDIR(NL),EDIF(NL),ED(NL),FORMAT='(I4,3F9.4)'
    ENDFOR
    CLOSE,4
  ENDIF 

; 
;;***************************************************************
;CALL_PROCEDURE,'LIDATA',LAM,FOBAR,OZA,AG,AW
;;***************************************************************
;;  OPENS AND READS LIGHT DATA. INCLUDES OXYGEN AND WATER VAPOR ABSORPTION.

; SURFACE REFLECTANCE
;  SFCRFL,RAD,THETA,WS,ROD,ROS ; CALL_PROCEDURE,'SFCRFL',RAD,THETA,WS,ROD,ROS

  DONE:
;  IF N_ELEMENTS(EDIR) NE 1 THEN EDIR = TOTAL(EDIR)  ;  A WORKAROUND
;  IF N_ELEMENTS(EDIF) NE 1 THEN EDIF = TOTAL(EDIF)  ;  A WORKAROUND  
;  IF N_ELEMENTS(ED)   NE 1 THEN ED = TOTAL(ED)  ;  A WORKAROUND


END ; END OF PROGRAM
