; $ID:	CSV_WRITE.PRO,	2020-06-30-17,	USER-KJWH	$
;#######################################################################################################
	PRO CSV_WRITE, FILE, STRUCT, TAGNAMES=TAGNAMES,LOOP=LOOP, TRANSP=TRANSP,APPEND = APPEND,NO_HEADING=NO_HEADING
;+
;	THIS PROGRAM WRITES A COMMA-DELIMITED CSV FILE FROM AN IDL SIMPLE STRUCTURE
; SYNTAX:
;	CSV_WRITE,FILE, STRUCT
; OUTPUT:
;		AN ASCII CSV COMMA-DELIMITED FILE WITH HEADING
; ARGUMENTS:
; 	FILE:	COMPLETE NAME FOR THE OUTPUT CSV FILE
;	STRUCT:	THE SIMPLE STRUCTURE TO BE WRITTEN
; KEYWORDS:
;		TAGNAMES: IF A LIST OF TAGNAMES IS PROVIDED THEN OUTPUT WILL BE JUST THE TARGET TAGNAMES.
;		LOOP:	FORCES PROGRAM TO LOOP, PROCESSING ONE RECORD AT A TIME, INSTEAD OF THE ENTIRE STRUCTURE AT ONCE
;		TRANSP: TRANSPOSES OUTPUT
;		APPEND: APPENDS STRUCT INFO TO CSV 
;		NO_HEADING: SUPPRESS A TAGNAME HEADING
; EXAMPLE:
;  	STRUCT = CREATE_STRUCT('AA',0B,'BB',1,'CC',2L,'DD',3.0,'EE',4.0D)
;  	STRUCT = REPLICATE(STRUCT,4)
;   CSV_WRITE,'TEST.CSV',STRUCT;
; 	OPEN TEST.CSV FILE WITH EXCEL OR WORD PROCESSOR ... IT SHOULD LOOK LIKE THIS ...
;	AA,BB,CC,DD,EE
;	0,1,2,3.000000,4.000000000000000
;	0,1,2,3.000000,4.000000000000000
;	0,1,2,3.000000,4.000000000000000
;	0,1,2,3.000000,4.000000000000000
;
; CATEGORY:
;	STRUCTURES
; NOTES:
;	WORKS WITH SIMPLE STRUCTURES (SPREADSHEET OR DATABASE TYPES OF STRUCTURES).
;	CALLS CSV_WRITE.PRO
;
; VERSION:
;	JAN 22,2001
; HISTORY:
;	OCT 12,2001	WRITTEN BY:	J.E. O'REILLY, NOAA, 28 TARZWELL DRIVE, NARRAGANSETT, RI 02882
;	DEC 11,2012,JOR, FORMATTING
;	AUG 22,2013,JOR ADDED KEYWORD APPEND
;	AUG 23,2013,JOR, DEFAULT IS  HEADING = 1,ADDED KEYWORD NO_HEADING
; AUG 28,2013,JOR,  IF KEYWORD_SET(APPEND) THEN HEADING = 0
; DEC 9, 2013,KJWH, CHANGED TEXT TO TXT IN ORDER TO NOT CONFLICT WITH IDL'S TEXT PRO
; FEB 11,2014,JOR RENAMED FROM STRUCT_2CSV

;-
;###############################################################################################


;********************************** 
  ROUTINE_NAME  = 'CSV_WRITE'
;**********************************
;OPEN CLOSE  STRUCT

 
 IF KEYWORD_SET(NO_HEADING) EQ 1 THEN  HEADING = 0 ELSE HEADING = 1 
 
 ;===> BUT IF APPEND THEN WE DO NOT WANT A HEADING EVERY TIME STATS ARE APPENDED TO THE CSV FILE
 IF KEYWORD_SET(APPEND) THEN HEADING = 0

OPENW,LUN,FILE,/GET_LUN,APPEND = APPEND


;	===> IF TAGNAMES NOT PROVIDED THEN ASSUME OUTPUT WILL HAVE ALL THE TAGS
	IF N_ELEMENTS(TAGNAMES) GE 1  THEN BEGIN
		TAGS =WHERE_IN(TAG_NAMES(STRUCT),TAGNAMES,COUNT)
		IF COUNT EQ 0 THEN TAGS = -1L
	ENDIF ELSE BEGIN
		TAGS = -1L
	ENDELSE


;	***********************************
	IF NOT KEYWORD_SET(LOOP) THEN BEGIN

		IF TAGS[0] EQ -1L THEN BEGIN
			TXT=STRUCT_2STRING(STRUCT,HEADING=HEADING,ARR=TRANSP)
		ENDIF ELSE BEGIN
	  	TXT=STRUCT_2STRING(STRUCT_COPY(STRUCT,TAGS=TAGS),HEADING=HEADING,ARR=TRANSP)
	  ENDELSE

; 	FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
  	FOR N=0L,N_ELEMENTS(TXT)-1L DO BEGIN
     	PRINTF,LUN,TXT(N)
  	ENDFOR;FOR N=0L,N_ELEMENTS(TXT)-1L DO BEGIN
  	;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF

;	**************************************
  ENDIF ELSE BEGIN

  	IF TAGS[0] EQ -1L THEN BEGIN
			TXT=STRUCT_2STRING(STRUCT(0L),/TAGNAMES)
		ENDIF ELSE BEGIN
			TXT=STRUCT_2STRING(STRUCT_COPY(STRUCT[0],TAGS=TAGS),/TAGNAMES)
		ENDELSE
		PRINTF,LUN,TXT

		IF TAGS[0] EQ -1L THEN BEGIN
; 		LLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLL
  		FOR N=0L,N_ELEMENTS(STRUCT)-1L DO BEGIN
				TXT=STRUCT_2STRING(STRUCT(N) )
     		PRINTF,LUN,TXT
  		ENDFOR

		ENDIF ELSE BEGIN

; 		LLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLLL
  		FOR N=0L,N_ELEMENTS(STRUCT)-1L DO BEGIN
				TXT=STRUCT_2STRING(STRUCT_COPY(STRUCT(N),TAGS=TAGS))
     		PRINTF,LUN,TXT
  		ENDFOR

		ENDELSE
	ENDELSE

  CLOSE,LUN & FREE_LUN,LUN
  PFILE,FILE
 END; #####################  END OF ROUTINE ################################
