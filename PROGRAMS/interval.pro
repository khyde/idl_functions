; $ID:	INTERVAL.PRO,	2020-06-30-17,	USER-KJWH	$
;################################################################################################
FUNCTION INTERVAL, INTERVAL_RANGE, INCREMENT, BASE=BASE, DOUBLE=DOUBLE

;+
; NAME:
;       INTERVAL
;
; PURPOSE:
;		GENERATE AN ARRAY AT INTERVALS BETWEEN THE MIN AND MAX OF THE SPECIFIED RANGE OR THE
;		MIN AND MAX OF THE INPUT ARRAY
;
;		ALSO CAN GENERATE AN ARRAY USING BASE 2, BASE 10 OR ANY OTHER BASE
;
; CATEGORY:
;        ARRAY
;
; EXAMPLES:
;		PRINT, INTERVAL([2,7])
;   PRINT, INTERVAL([2,3,4,7], .1)
;		PRINT, INTERVAL(FINDGEN(11),.1)
;		PRINT, INTERVAL([-4,4],1,BASE=10) ; GIVES 0.0001 TO 10000
;		PRINT, INTERVAL([4,-4],1,BASE=10) ; GIVES 10000  TO 0.0001
;		PRINT, INTERVAL([-4,4],1,BASE=2)  ; GIVES 0.0625 TO 16
;   PRINT, INTERVAL([0]); THIS  -GIVES ZERO
;   PRINT, INTERVAL([0,0]); THIS  -GIVES ZERO
; INPUTS:
;		RANGE:	 		AN ARRAY OF 2 NUMBERS OR A NUMERIC ARRAY
;		INCREMENT:	POSITIVE INTEGER OR DECIMAL FRACTION
;
;		OTHER PROGRAMS CALLED:
;			INTERPOL (IDL)
;
; KEYWORD PARAMETERS:
;       BASE:	FOR A GEOMETRIC SERIES (E.G. BASE = 2  , OR BASE = 10)
;
; OUTPUTS:
;   AN ARRAY RANGING BETWEEN THE INPUT RANGE OR INPUT ARRAY,
;		AND IN INTERVALS (STEPS) EQUAL TO INCREMENT.
;
;
; MODIFICATION HISTORY:
;   WRITTEN BY:  J.E.O'REILLY, SEPTEMBER 8, 1995.
;      NOAA, NMFS, NARRAGANSETT LABORATORY, 28 TARZWELL DRIVE, NARRAGANSETT, RI 02882-1199
;      OREILLY@FISH1.GSO.URI.EDU
;  MODIFIED NOVEMBER 9,1995, J.O'REILLY
;     IF ONLY GIVE RANGE OF ONE NEGATIVE VALUE THEN
;     NEW RANGE IS [RANGE,0];
;	 JUNE 30, 2004 JOR NOW USES INTERPOL
;	 MAY 1, 2014 KJWH CHANGED RANGE TO INTERVAL_RANGE TO AVOID CONFLICT WITH THE RANGE FUNCTION
;	 JUL 09,2016 JEOR: UPDATED EXAMPLES,FORMATTING AND ADDED EXAMPLE FOR A ZERO RANGE
;	                   IF N_ELEMENTS(_RANGE) EQ 1 AND _RANGE[0] EQ 0 THEN _RANGE = [0,_RANGE]
;  APR 03, 2019 - KJWH: Added DOUBLE keyword

;################################################################################################
;-
;************************
ROUTINE_NAME = 'INTERVAL'
;************************

_RANGE = INTERVAL_RANGE
IF N_PARAMS() EQ 0  THEN  MESSAGE,"ERROR: YOU MUST PROVIDE A RANGE OR DATA ARRAY. E.G.  LEVELS=INTERVAL([0,10],.5)"
IF N_ELEMENTS(_RANGE) EQ 1 AND _RANGE[0] EQ 0 THEN _RANGE = [0,_RANGE]
IF N_ELEMENTS(_RANGE) EQ 1 AND _RANGE[0] GT 0 THEN _RANGE = [0,_RANGE]
IF N_ELEMENTS(_RANGE) EQ 1 AND _RANGE[0] LT 0 THEN _RANGE = [_RANGE,0]

IF N_PARAMS() LE 1  THEN INCREMENT = 1.0D

IF KEY(DOUBLE) THEN _RANGE = DOUBLE(_RANGE) ELSE _RANGE = FLOAT(_RANGE)

DELTA = MAX(_RANGE) - MIN(_RANGE)

;	===> ENSURE N IS A POSITIVE WHOLE NUMBER
N    = ULONG64( ROUND(((DELTA/ABS(INCREMENT)) +1 )))

; ===> INTERPOLATE LINEARLY OVER THE RANGE
VALUES =  (INTERPOL( (_RANGE),N))

;	===> IF GEOMETRIC SERIES THEN EXPONENTIATE
IF N_ELEMENTS(BASE) EQ 1 THEN BEGIN
  IF BASE NE 1 AND KEY(DOUBLE)  THEN VALUES = DOUBLE(BASE)^VALUES
  IF BASE NE 1 AND ~KEY(DOUBLE) THEN VALUES = FLOAT(BASE)^VALUES
ENDIF;IF N_ELEMENTS(BASE) EQ 1 THEN BEGIN


RETURN,VALUES

END; #####################  END OF ROUTINE ################################
