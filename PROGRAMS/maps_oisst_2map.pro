; $ID:	MAPS_OISST_2MAP.PRO,	2020-02-07-14,	USER-KJWH	$
;##########################################################################
FUNCTION MAPS_OISST_2MAP, OISST, MAP_OUT, INIT=INIT

; THIS PROGRAM CONVERTS A 1440 X 720 OISST ARRAY TO A VALID MAP
;
; CATEGORY:
;    MAPPING
;    
; UTILITY:
;    REMAPPING
;    
; CALLING SEQUENCE:
;    NEW = MAPS_OISST_2BIN(OISST, 'NEC') 
;    
; INPUTS:
;   OISST   = A 1440 X 720 OISST DATA ARRAY
;   MAP_OUT = ANY OF THE VALID_MAPS
;
; OPTIONAL INPUTS:
;
; KEYWORDS: 
;
; OUTPUTS:
;
; EXAMPLES:
; 
; NOTES:
;  The OISST_1440_720_LAT(LON).SAV files were created using MAPS_L3B_SUBS
;  
; MODIFICATION HISTORY:
;     FEB 07, 2020  Written: by K.J.W.Hyde, 28 Tarzwell Drive, NMFS, NOAA 02882 (kimberly.hyde@noaa.gov)
;     FEB 07, 2020 - KJWH: Adapted from MAPS_AVHRR_2MAP
;               
;- 
  
;********************************* 
  ROUTINE_NAME = 'MAPS_OISST_2MAP'
;*********************************
  COMMON MAPS_OISST_2MAP_, LONS, LATS, OISST_STRUCT
  IF KEY(INIT) THEN BEGIN
    LONS = []
    LATS = []
    OISST_STRUCT = []
  ENDIF  
    
  SZ = SIZEXYZ(OISST)
  IF SZ.PX NE 1440 AND SZ.PY NE 720 THEN RETURN, 'ERROR: input array deminsions must be 1440 x 720'
    
  IF NONE(LONS) THEN LONS = IDL_RESTORE(!S.MAPINFO + 'OISST-PXY_1440_720-LON.SAV')
  IF NONE(LATS) THEN LATS = IDL_RESTORE(!S.MAPINFO + 'OISST-PXY_1440_720-LAT.SAV')
  
  IF IDLTYPE(LONS) EQ 'STRING' OR IDLTYPE(LATS) EQ 'STRING' THEN BEGIN
    LONS = []
    LATS = []
    RETURN, 'ERROR: Unable to read the MASTER LON/LAT files'
  ENDIF
  
  COUNT_MAP_OUT = 0
  IF ANY(OISST_STRUCT) THEN BEGIN
    OK = WHERE(TAG_NAMES(OISST_STRUCT) EQ MAP_OUT, COUNT_MAP_OUT)
    IF COUNT_MAP_OUT EQ 1 THEN REMAP_STRUCT = OISST_STRUCT.(OK) ELSE REMAP_STRUCT = []  
  ENDIF ELSE OISST_STRUCT = []
  
  MDATA = MAPS_LONLAT_GRID(OISST, MAP_OUT=MAP_OUT, LON=LONS, LAT=LATS, STRUCT=REMAP_STRUCT, INIT=INIT)  
  
; ===> Create COMMON structure based on MAP_OUT  
  IF OISST_STRUCT NE [] THEN BEGIN
    IF COUNT_MAP_OUT EQ 0 THEN OISST_STRUCT = CREATE_STRUCT(OISST_STRUCT,MAP_OUT,REMAP_STRUCT)
  ENDIF ELSE OISST_STRUCT = CREATE_STRUCT(MAP_OUT,REMAP_STRUCT)
  
  RETURN, MDATA
  
END
