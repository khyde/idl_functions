; $ID:	READL3BIN_NC.PRO,	2015-10-01	$  GONE
;###################################################################################3
FUNCTION READL3BIN_NC,FILE,PROD,LON,LAT,BINS,NOBS,WTS,NSCENES,SUM,SUM2, $
  QUAL=QUAL,BEG=BEG,EXTENT=EXTENT,NROWS=NROWS,VERBOSE=VERBOSE


;+NAME/ONE LINE DESCRIPTION OF ROUTINE:
;    READL3BIN_NC READS A SPECIFIC PRODUCT FROM A SEAWIFS BIN FILE
;
;  NAME:
;    READL3BIN_NC
;
;  PURPOSE:
;    SIMPLIFIES THE READING OF SEAWIFS STANDARD L3 BIN FILE. RETURNS MEAN
;    BIN VALUES FOR A SPECIFIED PRODUCT, AND CORRESPONDING LON/LAT.
;
;  CALLING SEQUENCE:
;    DATA = READL3HDF(FILENAME,PROD,LON,LAT)
;
;  INPUT:
;    FILENAME - L2 FILENAME STRING.
;    PROD - L2 SDS PRODUCT NAME STRING. IF THIS ENDS IN '_', ALL SDS
;               PRODUCTS OF NAME PROD_NNN (WHERE NNN IS WAVELENGTH)
;               WILL BE RETURNED.
;    KEYWORDS:
;     VERBOSE ALLOWS PRINTING OF INFO           
;
;  OUTPUT:
;    DATA - 1-DIMENSIONAL ARRAY CONTAINING THE REQUESTED PRODUCT.
;    LON  - 1-DEMENSIONAL ARRAY OF BIN LONGITUDE POSITIONS
;    LAT  - 1-DEMENSIONAL ARRAY OF BIN LATITUDE POSITIONS
;
;  SUBROUTINES CALLED:
;    GET_VDS_IN_VG
;    BIN2LL
;
;  WRITTEN BY:
;    J. GALES, FUTURETECH CORP..
;    B. A. FRANZ, SAIC GENERAL SCIENCES CORP..
;    FEBRUARY 2000.
;
; MODIFICATION HISTORY:
;       TUE OCT 29 15:47:19 2002, JOEL GALES <JOEL@SHERPA.DOMAIN.SDPS>
;       TUE APR 03 10:37:00 2009, JOEL GALES <JOEL@SHERPA.DOMAIN.SDPS>
;         - ADD SUPPORT FOR QUALITY FIELD
;       FEB 3,2015,JOR MINOR FORMATTING CHANGES [UPPER CASE WHERE FEASIBLE FOR LEGIBILITY]
;                      NOTE: CALLS TO READ_HDF5 ARE HIGHLY CASE SENSITIVE: 
;                      E.G. binlist = read_hdf5(file, 'BinList', group='level-3_binned_data')
;       SEP 16, 2015 - KJWH: Removed STRLOWCASE(PROD) because not all L3 prods (e.g. Kd_490) are lowercase in the file
;       OCT 01,2015, -  JOR: ADDED   GONE,BININDEX,BINLIST,BINLIST,VAL TO SAVE MEMORY SPACE               
;
;
;###############################################################################################3
;-
;*****************************
ROUTINE_NAME = 'READL3BIN_NC'
;*****************************
IF NONE(PROD) THEN MESSAGE,'ERROR: PROD IS REQUIRED'
;===> ENSURE PROD IS LOWER CASE
; PROD = STRLOWCASE(PROD) - Error with the Kd_490 product

;===> CAREFUL-CASE SENSITIVE >
  BININDEX = READ_HDF5(FILE, 'BinIndex', GROUP='level-3_binned_data')
  BEG = BININDEX._BEGIN
  EXTENT = BININDEX.EXTENT
  START = BININDEX.START_NUM
  MX = BININDEX.MAX
  NROWS = N_ELEMENTS(BININDEX)
  GONE,BININDEX
;===> CAREFUL-CASE SENSITIVE >
  BINLIST = READ_HDF5(FILE, 'BinList', GROUP='level-3_binned_data')
  BINS = BINLIST.BIN_NUM
  NOBS = BINLIST.NOBS
  WTS = BINLIST.WEIGHTS
  NSCENES = BINLIST.NSCENES
  GONE,BINLIST
  N = LONG(TOTAL(DOUBLE(NOBS NE 0)))
  IF KEY(VERBOSE) THEN PRINT,'# OF NON-ZERO BINS:', N

  BINS = BINS[0:N-1]
  NOBS = NOBS[0:N-1]
  WTS = WTS[0:N-1]
  NSCENES = NSCENES[0:N-1]

  IF (PROD EQ '') THEN BEGIN
    DATA = -1
    GOTO, SKIP
  ENDIF;IF (PROD EQ '') THEN BEGIN
    
;===> CAREFUL-CASE SENSITIVE >
  VAL = READ_HDF5(FILE, PROD, GROUP='level-3_binned_data')

  SUM = VAL.SUM
  SUM = SUM[0:N-1]
  SUM2 = VAL.SUM_SQUARED
  SUM2 = SUM2[0:N-1]

  DATA = SUM/WTS
  GONE,VAL
  SKIP:

  IF KEY(VERBOSE) THEN PRINT,'NROWS: ', NROWS
  BIN2LL,NROWS,BINS,LAT,LON

  RETURN, DATA
END
