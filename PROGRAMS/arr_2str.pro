; $ID:	ARR_2STR.PRO,	2020-07-08-15,	USER-KJWH	$
;###################################################################################################
  FUNCTION ARR_2STR,ARR,DELIM=DELIM,PAREN=PAREN,BRACK=BRACK,QUOTE=QUOTE,FORMAT=FORMAT,ROWS=ROWS
  ;+
  ; THIS FUNCTION GENERATES A SINGLE STRING EXPRESSION FROM AN ARRAY
  ; SYNTAX:
  ; RESULT = ARR_2STR(ARR [,FORMAT=FORMAT] [,PAREN=PAREN]   )
  ; OUTPUT: A STRING
  ; ARGUMENTS:
  ;   ARR: ARRAY
  
; KEYWORD PARAMETERS:
;   DELIM     : DELIMITER CHARACTER(S) BETWEEN NUMBERS IN THE OUTPUT STRING, DEFAULT IS ','
;   PAREN     : PARENTHESES ADDED TO ENDS INSTEAD OF SQUARE BRACKETS
;   BRACK     : SQUARE BRACKETS ADDED TO ENDS
;   QUOTE     : ADDS SINGLE QUOTE TO LEFT AND RIGHT OF EACH ELEMENT
;   FORMAT    : IDL FORMAT STATEMENT, ALLOWS CONTROL OVER THE FORMAT OF THE STRING
;   ROWS      : OUTPUTS RESULT IN ROWS WITH EACH ITEM ON A ROW
;   
; EXAMPLES:
; PRINT, ARR_2STR(INDGEN(9))
; PRINT, ARR_2STR(INDGEN(9),/PAREN)
; PRINT, ARR_2STR(INDGEN(9),/BRACK)
; PRINT, ARR_2STR(INDGEN(9),/BRACK,/QUOTE)
; PRINT, ARR_2STR(INDGEN(9),/BRACK,/QUOTE,DELIM = ';')
; PRINT, ARR_2STR(INDGEN(9),/BRACK,/QUOTE,DELIM = ';',FORMAT = '(I3.3)')
; PRINT, ARR_2STR(FINDGEN(9),/BRACK,/QUOTE,DELIM = ';',FORMAT = '(F8.3)')
; THE NEXT EXAMPLE SHOWS HOW TO MAKE A SINGLE STRING WITH '!C' ADDED AFTER EACH ELEMENT
; (THIS ALLOWS XYOUTS TO OUTPUT ON SEVERAL LINES)
; X= STRING( [1,2,3]) & RESULT= ARR_2STR(X,DELIM='!C',/QUOTE) & PRINT,RESULT
; PL, ARR_2STR(INDGEN(9),/ROWS)

; MODIFICATION HISTORY:
;   NOV 26, 1998  WRITTEN BY: J.E. O'REILLY, NOAA, 28 TARZWELL DRIVE, NARRAGANSETT, RI 02882
;   DEC 15, 2006 JOR NOW MUST REQUEST BRACKETS
;   AUG 11,2015,JOR, ADDED KEY ROWS
;#################################################################################
;-
;************************
ROUTINE_NAME='ARR_2STR'
;************************

  IF NONE(DELIM)  THEN _DELIM = ',' ELSE _DELIM = DELIM
  
  IF KEY(ROWS) THEN BEGIN
    _DELIM = ',$#'
    BRACK = 1
    QUOTE = 1
  ENDIF;IF KEY(ROWS) THEN BEGIN

	OUTER = ['','']
  IF KEY(PAREN) THEN OUTER = ['(',')']
  IF KEY(BRACK) OR KEY (ROWS) THEN OUTER= ['[',']']
  TXT = STRTRIM(STRING(ARR,FORMAT=FORMAT),2)
  IF KEY(QUOTE) THEN TXT = "'"+TXT+"'"


;	 FROM IDL HELP
; "THE STRJOIN FUNCTION COLLAPSES A ONE-DIMENSIONAL STRING ARRAY INTO A SINGLE MERGED STRING"
;	"OR TWO-DIMENSIONAL STRING ARRAY INTO A ONE-DIMENSIONAL ARRAY OF MERGED STRINGS."

;	THEREFORE WE MUST STRJOIN(TXT(*)) TO RESULT IN A SINGLE STRING
  TXT = OUTER[0]+STRJOIN(TXT(*),_DELIM)+OUTER[1]

IF KEY(ROWS) THEN BEGIN
  TXT = STR_SEP(TXT,'#')
  
ENDIF;IF KEY(ROWS) THEN BEGIN

  RETURN, TXT

  END; #####################  END OF ROUTINE ################################
