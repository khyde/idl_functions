; $ID:	GRIDS.PRO,	2020-07-08-15,	USER-KJWH	$
;##################################################################
	PRO GRIDS, X=X, Y=Y, NO_X=NO_X, NO_Y=NO_Y, ALL=ALL, _EXTRA=_EXTRA

;+
; NAME:
;		GRIDS
;
; PURPOSE:;
;		THIS PROGRAM DRAWS GRID LINES OF ANY COLOR, THICKNESS, AND LINESYTLE IN THE PLOT WINDOW
 ;
; CATEGORY:
;		GRAPHICS
;
; CALLING SEQUENCE:
;		GRIDS
;
; INPUTS:
;		X:	X COORDINATES FOR THE VERTICAL GRID LINES
;		Y:	Y COORDINATES FOR THE HORIZONTAL GRID LINES
;
 ;
; KEYWORD PARAMETERS:
;		NO_X:	SUPPRESS VERTICAL GRID LINES
;		NO_Y:	SUPPRESS HORIZONTAL GRID LINES
;		ALL:	DRAW ALL GRID LINES
;		_EXTRA: ANY VALID KEYWORDS FOR THE PLOTS ROUTINE USED BY THIS PROGRAM TO DRAW THE GRIDS
;					  E.G. COLOR, LINESTYLE,THICK
;
;
; OUTPUTS:
;		GRID LINES ARE DRAWN ON THE DEFAULT GRAPHICS DEVICE
;
;
; EXAMPLE:
; FIRST;  PLOT,[1,2,3], /NODATA, XTICK_GET=XTICK_GET,YTICK_GET=YTICK_GET
;       THEN:   GRIDS, X=XTICK_GET,Y=YTICK_GET
;       OR:      GRIDS, X=XTICK_GET,Y=YTICK_GET , COLOR=128,THICK=2,LINESTYLE=1
;
;
;
;	NOTES:
;		THE DEFAULT FOR THIS ROUTINE IS NOT TO DRAW GRIDS ATOP THE X AND Y AXES LINES
;		KEYWORD ALL IS USED TO DRAW ALL GRIDS PROVIDED IN X,Y (OR DETERMINED BY THIS ROUTINE IF X,Y
;		ARE NOT PROVIDED), EVEN THOSE ATOP THE X AND Y AXES LINES
;
;

; MODIFICATION HISTORY:
; 	WRITTEN BY:  J.E.O'REILLY, OCT 17, 1997
;		MARCH 21,2003 JOR ELIMINATED FRAME ; NOW USE FRAME,/PLOT,COLOR=0,THICK=1; ETC
;		JAN 4, 2006 JOR STREAMLINED ROUTINE
;		DEC 26,2012,JOR FORMATTING
;		
;##################################################################
;-
;	****************************
	ROUTINE_NAME = 'GRIDS'
; ****************************
 ; STOP
; 
;	===> GET THE MIN,MAX, X,Y FROM THE PLOT AXES
	IF !X.TYPE EQ 0 THEN BEGIN
  	MIN_X = !X.CRANGE[0]
  	MAX_X = !X.CRANGE[1]
  ENDIF ELSE BEGIN
  	MIN_X = 10^!X.CRANGE[0]
  	MAX_X = 10^!X.CRANGE[1]
  ENDELSE
  IF !Y.TYPE EQ 0 THEN BEGIN
  	MIN_Y = !Y.CRANGE[0]
  	MAX_Y = !Y.CRANGE[1]
  ENDIF ELSE BEGIN
  	MIN_Y = 10^!Y.CRANGE[0]
  	MAX_Y = 10^!Y.CRANGE[1]
	ENDELSE

;	*******************************************************************************************
;	*** IF X OR Y NOT PROVIDED THEN GET THEM FROM THE CURRENT PLOT USING THE AXIS PROCEDURE ***
;	*******************************************************************************************

	IF N_ELEMENTS(X) EQ 0 THEN BEGIN
;		===> IF THE X AXIS IS LINEAR GET XX FROM XTICK_GET
  	IF !X.TYPE EQ 0 THEN AXIS,/XAXIS,/NODATA,XSTYLE=5,XTICK_GET=XX
;		===> IF THE X AXIS IS LOG THEN GET XX FROM THE DECADES FUNCTION
  	IF !X.TYPE EQ 1 THEN BEGIN
      DEC = DECADES()
      OK = WHERE(DEC GE MIN_X AND DEC LE MAX_X,COUNT)
      IF COUNT GE 1 THEN XX = DEC[OK] ELSE XX = [MIN_X,MAX_X]
  	ENDIF
  ENDIF ELSE XX = X

	IF N_ELEMENTS(Y) EQ 0 THEN BEGIN
;		===> IF THE Y AXIS IS LINEAR GET YY FROM YTICK_GET
  	IF !Y.TYPE EQ 0 THEN AXIS,/YAXIS,/NODATA,YSTYLE=5,YTICK_GET=YY
;		===> IF THE Y AXIS IS LOG THEN GET YY FROM THE DECADES FUNCTION
  	IF !Y.TYPE EQ 1 THEN BEGIN
      DEC = DECADES()
      OK = WHERE(DEC GE MIN_Y AND DEC LE MAX_Y,COUNT)
      IF COUNT GE 1 THEN YY = DEC[OK] ELSE YY = [MIN_Y,MAX_Y]
  	ENDIF
  ENDIF ELSE YY = Y



;	===> UNLESS ALL KEYWORD IS USED THE DEFAULT FOR THIS ROUTINE
;			 IS NOT TO DRAW GRIDS ATOP X,Y AXES
;			 REMOVE ANY XX,YY THAT ON THE PLOT AXES
  IF NOT KEYWORD_SET(ALL) THEN BEGIN
    OK=WHERE(XX NE MIN_X AND XX NE MAX_X,COUNT)
    IF COUNT GE 1 THEN XX = XX[OK]
		OK=WHERE(YY NE MIN_Y AND YY NE MAX_Y,COUNT)
    IF COUNT GE 1 THEN YY = YY[OK]
  ENDIF


;	===> DRAW X GRIDS UNLESS PREEMPTED BY NO_X
  IF NOT KEYWORD_SET(NO_X) THEN BEGIN
	  FOR I = 0L,N_ELEMENTS(XX)-1L DO BEGIN
	    PLOTS,[XX(I),XX(I)],[MIN_Y,MAX_Y],_EXTRA=_EXTRA
	  ENDFOR
	ENDIF


;	===> DRAW Y GRIDS UNLESS PREEMPTED BY NO_Y
	IF NOT KEYWORD_SET(NO_Y) THEN BEGIN
	  FOR I = 0L,N_ELEMENTS(YY)-1L DO BEGIN
	    PLOTS,[MIN_X,MAX_X],[YY(I),YY(I)],_EXTRA=_EXTRA
	  ENDFOR
  ENDIF

	END; #####################  END OF ROUTINE ################################

