; $ID:	ZEU_MB89.PRO,	2020-07-01-12,	USER-KJWH	$
; #########################################################################; 
FUNCTION ZEU_MB89,CHL,MIXED=MIXED
;+
; PURPOSE: CALCULATE EUPHOTIC CHL ANDEUPHOTIC DEPTH [ZEU] FROM SURFACE CHL USING THE MODEL OF MOREL & BERTHON, 1989
;
; CATEGORY: LIGHT
;
;
; INPUTS:
;       CHL ......... SURFACE CHLOROPHYLL CONCENTRATION [MG/M3]
;
;
; KEYWORDS:
;         MIXED.......... USE COEFFICIENTS FOR MIXED [NON-STRATIFIED WATER]
;         
; OUTPUTS: DEPTH OF THE EUPHOTIC LAYER [1 PERCENT SURFACE RADIANCE]
; 
; 
; NOTES: CODE TAKEN FROM VGPM2.PRO 
;        NOTE THAT TWO CHL_ZEU EQUATIONS FROM MB89 ARE COMBINED BY USING THE LOGICALS [CHL LE 1. AND CHL GT 1.]
; 
; 
; REFERENCES:
; MOREL, A. (1988), OPTICAL MODELING OF THE UPPER OCEAN IN RELATION TO ITS
; BIOGENOUS MATTER CONTENT (CASE 1 WATERS), J. GEOPHYS. RES., 93(C9),
; 10,749– 10,768.  [ 
;
; MOREL, A., AND J. F. BERTHON (1989), SURFACE PIGMENTS, ALGAL BIOMASS PROFILES,
; AND POTENTIAL PRODUCTION OF THE EUPHOTIC LAYER: RELATIONSHIPS REINVESTIGATED
; IN VIEW OF REMOTE-SENSING APPLICATIONS, LIMNOL. OCEANOGR.34(8), 1545–1562.
; 
; COEFFICIENTS FOR MOREL BERTHON 89 IN THIS PROGRAM ARE FROM TABLE 4 IN :
;   UITZ, J, H. CLAUSTRE, A. MOREL, AND S. HOOKER,(2006)
;   VERTICAL DISTRIBUTION OF PHYTOPLANKTON COMMUNITIES IN OPEN OCEAN:
;   AN ASSESSMENT BASED ON SURFACE CHLOROPHYLL;
;   JOURNAL OF GEOPHYSICAL RESEARCH, VOL. 111, C08005, DOI:10.1029/2005JC003207;
;   
; EXAMPLES: 
;           ST,ZEU_MB89([0.00001,0.0001,0.001,0.01,0.1,1.0,10,100])
;           ST,ZEU_MB89([0.00001,0.0001,0.001,0.01,0.1,1.0,10,100],/MIXED)
; NOTES: THE DEMO MOREL_BERTHON_DEMO ]STEP FIGURE_2] REPRODUCES THEIR FIGURE 2, 
;        INDICATING THAT THE MATH AND CODE IN THIS PROGRAM ARE CORRECT        
;
; MODIFICATION HISTORY:
;     MAY 28, 2018  WRITTEN BY: J.E. O'REILLY
;     JUN 07, 2018  VALIDATED CODE [SEE MOREL_BERTHON_DEMO]
; #########################################################################
;-
;********************
ROUTINE = 'ZEU_MB89'
;********************
; =========================================================================================
; CALCULATE CHL_ZEU (MG CHL M-2) FROM SATELLITE SURFACE CHLOROPHYLL CONCENTRATION [MG/M-3] 
; =========================================================================================
;CCCCCCCCCCCCCCCCCCC
CASE (KEY(MIXED)) OF
  0: CHL_ZEU=38.0*FLOAT(CHL LE 1.)*(CHL > 0.)^0.425 + 40.2*FLOAT(CHL GT 1.)*(CHL > 0.)^0.507;
  1: CHL_ZEU=38.0*FLOAT(CHL)^0.551 
ENDCASE;CASE (KEY(MIXED)) OF
;CCCCCCCCCCCCCCCCCCCCCCCCCCC

; =================================================================
; CALCULATE DEPTH OF EUPHOTIC LAYER USING MOREL'S CASE I MODEL, (M)
; =================================================================

ZEU  =  (CHL_ZEU GT 10.0)* 568.2*CHL_ZEU^(-0.746) +  (CHL_ZEU LE 10.0)* 200.*CHL_ZEU^(-0.293)

  RETURN,CREATE_STRUCT('CHL_ZEU',CHL_ZEU,'ZEU',ZEU)


END; #####################  END OF ROUTINE ################################
