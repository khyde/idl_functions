; $ID:	STR_ZERO_TRIM.PRO,	2020-06-30-17,	USER-KJWH	$
;########################################################################################
 FUNCTION STR_ZERO_TRIM, STR, TRIM=TRIM
;+
; NAME:
;       STR_ZERO_TRIM
;
; PURPOSE:
;				THIS FUNCTION REMOVES (TRIMS) ZEROS FROM STRINGS
;
; CATEGORY:
;		STRING
;
; CALLING SEQUENCE:
;		RESULT = STR_ZERO_TRIM(STR)
;
; INPUTS:
;		STR:	A STRING OR STRING ARRAY
;
; KEYWORD PARAMETERS:
;		TRIM:
;					1= REMOVE TRAILING ZEROS
;					2= REMOVE TRAILING ZEROS AND THE DECIMAL IF ALL ZEROS FOLLOW THE DECIMAL
;					3= REMOVE TRAILING ZEROS, TRAILING DECIMALS, LEADING ZEROS
;
; OUTPUTS:
;		A STRING
;
; EXAMPLE:
;		 PRINT, STR_ZERO_TRIM(1.010,TRIM=0) ; DOES NOT TRIM
;		 PRINT, STR_ZERO_TRIM(1.010,TRIM=1) ; TRIMS TRAILING ZEROS
;
;		 PRINT, STR_ZERO_TRIM(1.010,TRIM=2) ; COMPARE THIS WITH THE FOLLOWING EXAMPLE
;		 PRINT, STR_ZERO_TRIM(1.00,TRIM=2)
;		 PRINT, STR_ZERO_TRIM(1.010E-5,TRIM=2) 
;		 PRINT,STR_ZERO_TRIM(30)
;    PRINT,STR_ZERO_TRIM(30,TRIM=2)
;		 PRINT, STR_ZERO_TRIM(1.40)
;
;	NOTES:
;
; MODIFICATION HISTORY:
;       WRITTEN BY:  J.E.O'REILLY, NOV 27, 2004
;       DEC 25,2013,JOR, FORMATTING
;       SEP 27,2017 JEOR :TXT = STRING(COPY[NTH],FORMAT = '(D)') [TO HANDLE EXPONENTIAL NOTATION 
;       FEB 02,2018 JEOR : IF STRPOS(STRUPCASE(STRING(COPY[NTH])),'E') NE -1 THEN BEGIN

;########################################################################################
;-
;************************
ROUTINE= 'STR_ZERO_TRIM'
;************************

	IF N_ELEMENTS(STR) EQ 0 THEN BEGIN
		PRINT,'ERROR: MUST PROVIDE AN INPUT STRING'
		RETURN,''
	ENDIF

;	===> IF TRIM IS ZERO THEN RETURN THE INPUT WITHOUT ANY EDITING
	IF N_ELEMENTS(TRIM) EQ 1 THEN BEGIN
		IF TRIM EQ 0 THEN RETURN, STR
;		===> ENSURE TRIM IS BOUNDED
		TRIM = 1 > TRIM < 3
	ENDIF ELSE BEGIN
		TRIM = 1
	ENDELSE

;	===> MAKE A COPY TO AVOID ALTERING INPUT STR
  COPY= STRTRIM(STR,2)
 
;	FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
FOR NTH = 0L,N_ELEMENTS(COPY)-1 DO BEGIN

IF STRPOS(STRUPCASE(STRING(COPY[NTH])),'E') NE -1 THEN BEGIN
  TXT = STRING(COPY[NTH],FORMAT = '(D)')
ENDIF ELSE BEGIN
  TXT = STRING(COPY[NTH],FORMAT = '(G)')
ENDELSE;IF STRPOS(STRUPCASE(STRING(COPY(NTH))),'E') NE -1 THEN BEGIN


;===> DEFAULT IS TO TRIM TRAILING ZEROS
;		WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW
	WHILE STRMID(TXT,STRLEN(TXT)-1) EQ '0' DO BEGIN
		TXT=STRMID(TXT,0,STRLEN(TXT)-1)
	ENDWHILE;WHILE STRMID(TXT,STRLEN(TXT)-1) EQ '0' DO BEGIN
  ;WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW
  ;
  ;
;		===> ALSO TRIM THE DECIMAL IF FOLLOWED BY ALL ZEROS
		IF TRIM GE 2 THEN BEGIN
	 		IF STRPOS(TXT,'.') EQ STRLEN(TXT)-1L THEN TXT = STRMID(TXT,0L,STRLEN(TXT)-1L)
		ENDIF

;		===> TRIM LEADING ZERO IF FIRST CHARACTER TO LEFT OF DECIMAL IS ZERO
	  IF TRIM EQ 3 THEN BEGIN
	  	IF STRPOS(TXT,'0.') EQ 0 THEN TXT = STRMID(TXT,1,STRLEN(TXT))
	  ENDIF
  	COPY[NTH]=TXT
ENDFOR ; 	FOR NTH = 0L,N_ELEMENTS(COPY)-1 DO BEGIN
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
	RETURN, COPY


END; #####################  END OF ROUTINE ################################



