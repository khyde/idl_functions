; $ID:	SUNANG.PRO,	2020-06-26-15,	USER-KJWH	$

  PRO SUNANG, DOY,HR,RAD,LON,LAT,SUNZ

;  PURPOSE:  THIS PROGRAM COMPUTES SUN AZIMUTH AND ZENITH ANGLES FOR A GIVEN TIME, DATE, LATITUDE AND LONGITUDE.  
;    THIS PROGRAM IS FROM THE NMFS ELAS COMPUTER CODE, MODIFIED FOR STANDARD COORDINATES (W LONG. NEGATIVE).
;    TO CORRECT FOR THE DATELINE PROBLEM AND COEFFICIENTS (TAKEN FROM IQBAL, 1983, AN INTRODUCTION TO SOLAR RADIATION).
;     WATSON GREGG, RESEARCH AND DATA SYSTEMS, CORP.
; 
;   THETA
;   DOY = DAY OF YEAR
;   HR = TIME OF DAY IN HOUR
;   LAT = LATITUDE OF PIXEL
;   LON = LONGITUDE OF PIXEL
;   RLON = LONGITUDE OF PIXELR (FROM LON/RADIANS)
;   SUNZ = SOLAR ZENITH ANGLE IN DEGREES
;   SDEC = SOLAR DECLINATION ANGLE IN DEGREES
;   THEZ = THETA ZERO ORBITAL POSITION IN DEGREES
;   TC = TIME CORRECTION
;   XHA = SOLAR HOUR ANGLE IN DEGREES


; COMPUTE SOLAR DECLINATION ANGLE (CONSERVING THE ORIGINAL UNITS OF LONGITUDE)
  THEZ = 360.0*(DOY-1)/365.0
  RTHEZ = THEZ/RAD
  SDEC = 0.396372-22.91327*COS(RTHEZ) + 4.02543*SIN(RTHEZ)   - 0.387205*COS(2.0*RTHEZ) + 0.051967*SIN(2.0*RTHEZ)  - 0.154527*COS(3.0*RTHEZ) + 0.084798*SIN(3.0*RTHEZ)

  RSDEC = SDEC/RAD
 
; TIME CORRECTION FOR SOLAR HOUR ANGLE, AND SOLAR HOUR ANGLE
  TC = 0.004297 + 0.107029*COS(RTHEZ) - 1.837877*SIN(RTHEZ)   - 0.837378*COS(2.0*RTHEZ) - 2.342824*SIN(2.0*RTHEZ)

  XHA = (HR-12.0)*15.0 + LON + TC
  OK = WHERE(XHA GT  180.0,COUNT) & IF COUNT GE 1 THEN XHA[OK] = XHA[OK]-360.0
  OK = WHERE(XHA LT -180.0,COUNT) & IF COUNT GE 1 THEN XHA[OK] = XHA[OK]+360.0
  _LAT = LAT/RAD;  (TO CONSERVE ORIGINAL UNITS OF LONGITUDE)
  _LON = LON/RAD ; (TO CONSERVE ORIGINAL UNITS OF LONGITUDE)
  RHA = XHA/RAD

; SUN ZENITH
  COSTMP = SIN(_LAT)*SIN(RSDEC) +         COS(_LAT)*COS(RSDEC)*COS(RHA)
  RSUNZ = ACOS(COSTMP)
  SUNZ = RSUNZ*RAD  ; CONVERT TO DEGREES 
  
  RETURN
END ; OF ROUTINE
