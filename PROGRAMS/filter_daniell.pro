; $ID:	FILTER_DANIELL.PRO,	2020-07-08-15,	USER-KJWH	$
;#############################################################################################
 FUNCTION FILTER_DANIELL, DATA, WIDTH , FILT=FILT
;+
; NAME:
;       FILTER_DANIELLE
;
; PURPOSE:
;				SMOOTH A 1-D DATA SERIES USING A MODIFIED DISCRETE DANIELL FILTER (TRAPEZOIDAL)
;
;
; CATEGORY:
;				FILTER
;
;
; KEYWORD PARAMETERS:
;       FILT:  OUTPUT THE DANIELL FILTER WEIGHTS
; OUTPUTS:
;				A SMOOTHED INPUT
;
;	RESTRICTIONS:
;		THIS PROGRAM ENSURES THAT THE WIDTH IS THE NEAREST ODD INTEGER EQUAL TO OR ABOVE THE WIDTH SUPPLIED
;		(IF WIDTH = 4 THEN WIDTH USED WILL BE 5)
;
; PROCEDURE:
;			FOLLOWS APPROACH IN:
;			BLOOMFIELD, PETER. 2000.
;			FOURIER ANALYSIS OF TIME SERIES, AN INTRODUCTION.
;			SECOND EDITION. JOHN WILEY & SONS, INC.
;			PAGE 108 EQUATION (7.8) AND PAGE 157 EQUATION (8.18).

;			FOR EXAMPLE, BLOOMFIELD'S SIMPLE MOVING AVERAGE OF LENGTH 6 (PAGE 157, EQUATION (8.18).
;			S6(F) = 1/6 *(  1/2*I(F-F3) + TOTAL(I(F-FJ)(J=-2,2) + 1/2*I(F+F3)  )
;			YIELDS IDENTICAL RESULTS TO THIS PROGRAM WHEN WIDTH = 7,
;			WHERE THE CENTRAL 4 ARE SUMMED AND THE POINTS TO LEFT AND RIGHT OF CENTRAL 4 ARE AVERAGED
;			(I.E.THE END POINTS HAVE 1/2 THE WEIGHTS OF THE CENTRAL POINTS).
;			THIS PRESERVES THE SYMMETRY OF THE FILTER AND CENTERS THE AVERAGE.
;
;			TREATMENT OF END VALUES:
; 				BLOOMFIELD, 2000, P.103:"TWO SIMPLE RULES ARE TO TREAT UNAVAILABLE VALUES IN THE INPUT SERIES AS EQUAL TO
;														 THE CORRESPONDING END VALUE, AND TO TREAT THE INPUT SERIES AS IF IT WERE
;														 SYMMETRIC ABOUT EACH END VALUE."
;					IDL'S CONVOL IS USED WITH EDGE_TRUNCATE. THEREFORE, FOR EXAMPLE, IF THE WIDTH=5,
;					THE SMOOTHED ESTIMATE AT Y0 IS:  	DATA[0]*0.125 + DATA[0]*0.25+DATA[0]*0.25 + DATA[1]*0.25 +DATA(2)*0.125
;					THE SMOOTHED ESTIMATE AT Y1 IS:   DATA[0]*0.125	+	DATA[0]*0.25+DATA[1]*0.25 + DATA(2)*0.25 +DATA(3)*0.125
;					THE SMOOTHED ESTIMATE AT Y2 IS:   DATA[0]*0.125	+	DATA[1]*0.25+DATA(2)*0.25 + DATA(3)*0.25 +DATA(4)*0.125
;
;
;
; 			MODIFICATION HISTORY:
;       WRITTEN BY:  J.E.O'REILLY, FEB 7, 2004
;       MAY 6,2012,JOR, FORMATTING
;       JUL 14,2014,JOR 
;                   IF NONE(DATA) THEN RETURN,FILT ELSE RETURN, CONVOL(DATA,FILT,/EDGE_TRUNCATE)

;#############################################################################################
;-
;******************************
ROUTINE_NAME='FILTER_DANIELL'
;******************************

IF NONE(WIDTH) THEN WIDTH=5 
;	===> RESTRICTION: WIDTH MUST BE GE 3
WIDTH = WIDTH > 3 ;
;	===> RESTRICTION: WIDTH MUST BE ODD
WIDTH = WIDTH + (WIDTH + 1) MOD 2
	
FILT = REPLICATE(1./(WIDTH-1),WIDTH)
FILT([0,WIDTH-1]) = 0.5*FILT([0,WIDTH-1])


IF NONE(DATA) THEN RETURN,FILT ELSE	RETURN, CONVOL(DATA,FILT,/EDGE_TRUNCATE)

END; #####################  END OF ROUTINE ################################



