; $ID:	GET_POS.PRO,	2017-10-04-13,	USER-JEOR	$
; #########################################################################; 
FUNCTION GET_POS,INPUT,COLS=COLS,ROWS=ROWS,VERBOSE=VERBOSE
;+
; PURPOSE:  RETURN A STRUCTURE WITH THE NORMALIZED POSITIONS ON AN IMAGE PAGE FOR THE INPUT ROWS AND COLUMNS
;
; CATEGORY: GRAPHICS
;
;
; INPUTS: 
;         INPUT.......... EITHER THE NUMBER OF IMAGES TO COMPOSITE ON A PAGE OR THE FILE NAMES TO COMPOSITE
;
;
; KEYWORDS:  
;         COLS......... NUMBER OF COLUMNS ON THE PAGE
;         ROWS......... NUMBER OF ROWS ON THE PAGE
;         VERBOSE...... PRINT,POSITION [BEFORE ADJUSTING FOR SPACE]

; OUTPUTS: STRUCTURE WITH POSITIONS FOR EACH OF THE IMAGES
; 
; 
; NOTES:
; POSITION SET THIS PROPERTY TO A FOUR-ELEMENT VECTOR THAT DETERMINES THE 
;          POSITION OF THE GRAPHIC WITHIN THE WINDOW. 
;          THE COORDINATES [X1, Y1, X2, Y2] DEFINE THE LOWER LEFT AND UPPER RIGHT CORNERS OF THE GRAPHIC. 
;          COORDINATES ARE EXPRESSED IN NORMALIZED UNITS RANGING FROM 0.0 TO 1.0. ON CREATION, 


;
;; EXAMPLES:
;         ST,GET_POS(4,/VERBOSE)
;         ST,GET_POS(9,/VERBOSE)
;
; MODIFICATION HISTORY:
;     MAY 17, 2017  WRITTEN BY: J.E. O'REILLY
;     MAY 20,2017 JOR: IF NUMERIC(INPUT) THEN BEGIN
;     OCT 04,2016, JOR :IF SEQ GE NOF(FILES) THEN RETURN,STRUCT


;-
; #########################################################################

;*******************
ROUTINE = 'GET_POS'
;*******************

LEFT  = 0.00D
RIGHT = 1.00D
BOT   = 0.00D
TOP   = 1.00D
SEQ = -1
IF NONE(INPUT)   THEN MESSAGE,'ERROR:INPUT MUST BE NUMBER OR FILES ARE REQUIRED' 
IF NUMERIC(INPUT) THEN BEGIN
  NUM_ = INPUT 
  ; DUMMY FILE NAMES
  FILES = 'FILE-' + STRTRIM(SINDGEN(NUM_),2)
ENDIF ELSE BEGIN  
  NUM_ = NOF(INPUT)
  FILES = INPUT
ENDELSE;IF NUMERIC(NUM) THEN BEGIN

; ===> DETERMINE COLUMNS AND ROWS
IF NONE(COLS) THEN COLS = FLOOR( SQRT(NUM_+ SQRT(NUM_)))
IF NONE(ROWS) THEN ROWS = CEIL(SQRT(NUM_))
X_WIDTH = (RIGHT-LEFT)/COLS
Y_HEIGHT = (TOP-BOT)/ROWS
;PRINT,COLS,ROWS,X_WIDTH,Y_HEIGHT

;FFFFFFFFFFFFFFFFFFFFFFFFF
FOR R = ROWS,1,-1 DO BEGIN
   Y1 = BOT + Y_HEIGHT*(R-1)
   Y2 =  BOT + Y_HEIGHT*(R)
  ;FFFFFFFFFFFFFFFFFFFFFFF
  FOR C = 1,COLS  DO BEGIN
   X1 = LEFT +X_WIDTH*(C-1)
   X2 = LEFT +X_WIDTH*(C)
   POS = [X1,Y1,X2,Y2]  
   SEQ = SEQ + 1
   IF SEQ GE NOF(FILES) THEN RETURN,STRUCT
   IF KEY(VERBOSE) THEN PRINT,POS

    S = CREATE_STRUCT('C',C,'R',R,'POS',POS,'FILE',FILES(SEQ))
    IF NONE(STRUCT) THEN STRUCT = S ELSE STRUCT = [STRUCT,S]
  ENDFOR;FOR C = 1,COLS  DO BEGIN
  ;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
ENDFOR;FOR R = ROWS,1,-1 DO BEGIN
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFF

RETURN,STRUCT

END; #####################  END OF ROUTINE ################################
