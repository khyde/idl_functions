; $ID:	STRUCT_2FIX.PRO,	2020-07-29-14,	USER-KJWH	$
;###################################################################
	FUNCTION STRUCT_2FIX, STRUCT

;+
; THIS FUNCTION CONVERTS ALL NUMERIC TAGS IN A SIMPLE SPREADSHEET STRUCTURE TO FIX-
; SYNTAX:
;   RESULT = STRUCT_2FIX(STRUCT)
; ARGUMENTS:
;   STRUCT: IDL STRUCTURE

; EXAMPLE:
;		MAKE A SIMPLE SPREADSHEET TYPE STRUCTURE
;		STRUCT = REPLICATE(CREATE_STRUCT('AA',0B,'BB',1L,'CC',0D, 'TXT_ALPHA','CAT', 'TXT_NUMERIC','1.2'),3)
;		STRUCT[1].TXT_ALPHA = '1.2'
;		STRUCT[1].TXT_NUMERIC = '3.333'
;		STRUCT = STRUCT_2FIX(STRUCT)
;
;
;	PROCEDURE:
;		IF A TAG CONTAINS NUMERIC DATA OR STRING DATA FOR WHICH ALL ELEMENTS CAN BE CONVERTED INTO NUMERIC DATA
;		THEN THE RESULT.TAG WILL BE FIX-
;
; NOTES:
;		THIS ROUTINE FIRST CALLS STRUCT_2NUM TO CONVERT ANY STRINGS INTO NUMERIC (WHERE POSSIBLE)
;
;   THIS ROUTINE IS USEFULL FOR CONVERTING STRING STRUCTURES MADE BY USING READ_CSV.PRO INTO FIX 
;		TO FACILLITATE PLOT AND OTHER IDL COMMANDS WHERE NUMERIC DATA ARE PREFERRED.
;
;   THE INPUT STRUCTURE IS NOT ALTERED.

; HISTORY:
;   AUG 11,2001 WRITTEN BY: J.E. O'REILLY, NOAA, 28 TARZWELL DRIVE, NARRAGANSETT, RI 02882
;   OCT 18,2013,JOR, FORMATTING
;###################################################################
;-
;***************************
ROUTINE_NAME='STRUCT_2FIX'
;***************************
  SZ=SIZE(STRUCT,/STRUCT)
  TYPE = SZ.TYPE
  IF TYPE NE 8 THEN BEGIN
    PRINT,'ERROR: STRUCT MUST BE AN IDL STRUCTURE'
    RETURN, -1
  ENDIF


 	NEW = STRUCT_2NUM(STRUCT)
  NTAGS = N_TAGS(NEW)

  TAGNAMES = TAG_NAMES(NEW)
	NUM = N_ELEMENTS(NEW)

; ===> CREATE A NEW STRUCTURE WITH TAG NAMES SAME AS ORIGINAL BUT TYPE=FIX AND SET TO MISSINGS CODE
  FOR N = 0L, NTAGS-1L DO BEGIN
;		IF NUMERIC THEN CONVERT TO FIX OTHERWISE CONSERVE THE DATA TYPE
  	IF NUMERIC(NEW.(N)) THEN TEMPLATE = MISSINGS(0) ELSE TEMPLATE = NEW[0].(N)

  	IF N EQ 0 THEN 	_STRUCT	=	CREATE_STRUCT(TAGNAMES(N),TEMPLATE) ELSE $
  									_STRUCT = CREATE_STRUCT(_STRUCT,TAGNAMES(N),TEMPLATE)
  ENDFOR

  _STRUCT=REPLICATE(_STRUCT,NUM)


;	LLLLLLLLLLLLLLLLLLLLLLLLLLLLLL
	FOR N = 0L, NTAGS-1L DO BEGIN
		TYPE_SOURCE	= IDLTYPE(NEW.(N),/CODE)
		TYPE_DEST		= IDLTYPE(_STRUCT.(N),/CODE)

		IF TYPE_DEST EQ TYPE_SOURCE THEN BEGIN
			_STRUCT.(N) = NEW.(N)
		ENDIF ELSE BEGIN
		  _STRUCT.(N) = STR2NUM(NEW.(N))
;			===> CONVERT MISSING CODES FROM THOSE IN NEW TO THE APPROPRIATE MISSING CODES FOR THE TAGS IN _STRUCT
		  OK=WHERE(NEW.(N) EQ MISSINGS(NEW.(N)) ,COUNT)
		  IF COUNT GE 1 THEN _STRUCT[OK].(N) = MISSINGS(_STRUCT[OK].(N))
		ENDELSE
  ENDFOR

  RETURN,_STRUCT

END; #####################  END OF ROUTINE ################################
