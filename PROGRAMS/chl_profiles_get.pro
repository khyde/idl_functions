; $ID:	CHL_PROFILES_GET.PRO,	2020-06-30-17,	USER-KJWH	$
;+
;;#############################################################################################################
	FUNCTION CHL_PROFILES_GET,LON=LON,LAT=LAT,DOY=DOY,CHL=CHL,NUM=NUM,$
	         WT_METERS=WT_METERS,WT_DAYS=WT_DAYS,WT_CHL=WT_CHL,Z_RES=Z_RES, MAX_DEPTH=MAX_DEPTH,SHOW=SHOW,$
	         PNUMS=PNUMS

; PURPOSE: THIS FUNCTION RETURNS THE MEAN CHL PROFILE 
;          BY AVERGING THE 'CLOSEST' CHL PROFILES IN THE MASTER DATABASE
; 
; CATEGORY:	CHL_PROFILES;		 
;
; CALLING SEQUENCE: RESULT = CHL_PROFILES_GET()
;
; INPUTS: SEE KEYWORDS
;         

; OPTIONAL INPUTS:
; 
; KEYWORDS:
;   LON : LONGITUDE LONGITUDE OF CHL OBSERVATION [DECIMAL DEGREES]
;   LAT : LATITUDE OF CHL OBSERVATION [DECIMAL DEGREES]
;   DOY : DAY OF YEAR OF CHL OBSERVATION [1-366]
;   CHL : CHLOROPHYLL OBSERVATION [MG/M3]
;   NUM : NUMBER OF 'CLOSEST'PROFILES TO FIND [DEFAULT = 5]
;   PNUMS :THE PROFILE NUMBERS USED TO MAKE THE MEAN PROFILE [OUTPUT]
;   NUM : NUMBER OF NEAREST PROFILES TO FIND
;   WT_METERS : WEIGHTING FACTOR FOR DISTANCE [DEFAULT = 10000]
;   WT_DAYS   : WEIGHTING FACTOR FOR TIME  [DEFAULT = 15 DAYS]
;   WT_CHL    : WEIGHTING FACTOR FOR CHLOR_A [DEFAULT = 0.25 MG/M3]
;   MAX_DEPTH: THE MAXIMUM DEPTH FOR COMPUTING THE CHLOROPHYLL PROFILE [DEFAULT = 100]
;   SHOW :     SHOW A PLOT OF THE MEAN OF THE CHL PROFILES VS DEPTH
;	
; OUTPUTS: 
;		
;; EXAMPLES:
;  ST,CHL_PROFILES_GET()

;	NOTES:

;
; MODIFICATION HISTORY:
;     WRITTEN SEP 28, 2014 J.O'REILLY
;     SEP 30,2014,JOR STREAMLINED
;     OCT 1,2014,JOR ADDED KEYS Z_RES, MAX_DEPTH,SHOW; ADDED STRUCT FOR OUTPUT [LIKE CHL_PROFILE_WOZNIAK]
;     OCT 3,2014,JOR TESTED
;     OCT 4,2014,JOR: IF SAME(D.CODE) THEN RETURN,!NULL

;#################################################################################
;-
;**********************************
ROUTINE_NAME  = 'CHL_PROFILES_GET'
;**********************************
ERROR = ''
IF NONE(NUM) THEN NUM = 5
;===> USE PREVIOUSLY GENERATED CHL PROFILES ?
;IF KEY(FILE_CHL_PROFILE) THEN BEGIN
;  
;  
;ENDIF;IF KEY(FILE_CHL_PROFILE) THEN BEGIN

PNUMS = CHL_PROFILES_WEIGHTS(LON=LON,LAT=LAT,DOY=DOY,CHL=CHL,NUM=NUM,WT_METERS=WT_METERS,WT_DAYS=WT_DAYS,WT_CHL=WT_CHL)
D = CHL_PROFILES_READ(PNUMS)
OK = WHERE_IN(ULONG(D.PNUM),PNUMS,COUNT) 
;===> IF ALL RECORDS ARE SURFACE OR THERE ARE NO SURFACE RECORDS IN THE PROFILE 
;     CAN NOT MAKE A REASONABLE PROFILE SO RETURN A NULL  
IF SAME(D.CODE) THEN RETURN,!NULL

NUM= N_ELEMENTS(PNUMS)
IF NONE(Z_RES) THEN Z_RES =  0.01
IF NONE(MAX_DEPTH) THEN MAX_DEPTH = 100.
CHL_PROFILE = REPLICATE(MISSINGS(0.0),NUM,((MAX_DEPTH)/Z_RES)+1)
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
FOR NTH = 0,N_ELEMENTS(PNUMS)-1 DO BEGIN
  PNUM= PNUMS[NTH]
   D = CHL_PROFILES_READ(PNUM)
   DEPTH = FLOAT(D.DEPTH)
   I_DEPTH = INTERVAL([0,MAX_DEPTH],Z_RES)
   CHL = D.CHL
   ;===> INTERPX EXTENDS THE UPPERMOST AND BOTTOMMOST VALUES
   I_CHL = INTERPX(DEPTH,CHL,I_DEPTH)
   CHL_PROFILE(NTH,*)= I_CHL  
ENDFOR;FOR NTH = 0,N_ELEMENTS(DB)-1 DO BEGIN
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF

;===> COMPUTE THE MEAN OF ALL THE CHL PROFILES FOR THESE PNUMS
MEAN_CHL_PROFILE = MEAN(CHL_PROFILE,DIMENSION=1)

;===>PLOT THE CHL PROFILE?
IF KEY(SHOW) THEN BEGIN
  PLT_XY,MEAN_CHL_PROFILE,-I_DEPTH,$
  XTITLE = UNITS('CHLOR_A'),YTITLE =UNITS('DEPTH'),$
  TITLE = 'PNUMS: ' + STRJOIN(ROUNDS(PNUMS),','),/GRID_ADD,SYM_SIZE=2,$
  SYMBOL = 'NONE',/LIN_ADD,LIN_COLOR = 'GREEN'
ENDIF;IF KEY(SHOW) THEN BEGIN

M = MAX(MEAN_CHL_PROFILE,Z_MAX) & Z_MAX = Z_MAX*Z_RES
RETURN, CREATE_STRUCT('METHOD','NEW_PROFILES','Z_MAX',Z_MAX,'Z_RES',Z_RES,'Z',I_DEPTH,'CHL',MEAN_CHL_PROFILE, 'ERROR',ERROR)


DONE:          
	END; #####################  END OF ROUTINE ################################
