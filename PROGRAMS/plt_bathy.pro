; $ID:	PLT_BATHY.PRO,	2020-06-30-17,	USER-KJWH	$
; #########################################################################; 
PRO PLT_BATHY, OBJ, MP, LEVELS=LEVELS, THICKS=THICKS, COLORS=COLORS, LINESTYLES=LINESTYLES, VERBOSE=VERBOSE, FACT=FACT, SMO=SMO
;+
; PURPOSE:  PLOT TOPO BATHY DATA 
;
; CATEGORY: PLOT
;
;
; INPUTS:
;      OBJ ......... GRAPHIC OBJECT TO DRAW ON
;
;
; KEYWORDS:
;     LEVELS .... DEPTH LEVELS TO PLOT [REQUIRED]
;     THICKS..... ARRAY OF LINE THICKNESS FOR LEVELS [DEPTHS]
;     COLORS..... ARRAY OF COLORS FOR LEVELS [DEPTHS]
;     LINESTYLES. ARRAY OF LINESTYLES FOR LEVELS [DEPTHS]
;     VERBOSE.... PRINTS PROGRAM PROGRESS 
;     FACT........USED TO DETERMINE THE PXY SIZE:'PXY_8640_4320','PXY_34560_17280', OR 'PXY_43200_21600' 
;     SMO........ WIDTH TO USE TO SMOOTH BATHY DATA [IF NONE THEN NO SMOOTHING DONE]
;         
; OUTPUTS: 
;
;; EXAMPLES: 
;            PLT_BATHY, LEVELS = [-100,-200,-2000]
;         
;
; MODIFICATION HISTORY:
;     JUN 24, 2018  WRITTEN BY: J.E. O'REILLY [ELEMENTS COPIED FROM PLT_TOPO]
; #########################################################################
;-
;*********************
  ROUTINE = 'PLT_BATHY'
;*********************
;===> DEFAULTS:
  IF NONE(FACT) THEN FACT = 10 ; HIGHEST RESOLUTION AVAILABLE

  IF NONE(LEVELS) THEN MESSAGE,'ERROR: LEVELS ARE REQUIRED'

;===> MAKE SURE ALL LEVELS ARE NEGATIVE CONVENTION FOR TOPO FILES: [LAND = + WATER = -]
  S =  SIGN(LEVELS)
  OK = WHERE(S EQ 1,COUNT)
  IF COUNT GE 1 THEN LEVELS[OK] = -1* LEVELS[OK]
  
  IF NONE(THICKS) THEN THICKS = REPLICATE(1,NOF(LEVELS))
  IF NOF(THICKS) NE NOF(LEVELS) THEN THICKS = REPLICATE(THICKS[0],NOF(LEVELS))
  IF NONE(COLORS) THEN COLORS = REPLICATE(1,NOF(LEVELS))
  IF NOF(COLORS) NE NOF(LEVELS) THEN COLORS = REPLICATE(COLORS[0],NOF(LEVELS))
  IF NONE(LINESTYLES) THEN LINESTYLES = REPLICATE(1,NOF(LEVELS))
  IF NOF(LINESTYLES) NE NOF(LEVELS) THEN LINESTYLES = REPLICATE(LINESTYLES[0],NOF(LEVELS))

  M = MAPS_SIZE('SMI') & PX = M.PX & PY = M.PY

;===> MAKE A PXY STRING TO USE WHEN FINDING THE DEG FILES
  PXY = 'PXY_' + ROUNDS(PX*FACT) + '_' + ROUNDS(PY* FACT)

  FMT = '(2F16.5)'
  NUM = 0.0
  TT = TOPO_TAGS('SMI',LEVELS)
  IF NONE(FILES) THEN FILES = FLS(!S.BATHY , '*'+PXY+ '*.DEG')
  OK = WHERE_STRING(FILES,TT.TAG + '-',COUNT)
  IF COUNT GE 1 THEN FILES = FILES[OK] ELSE RETURN

;===> PLOT THE DEPTHS
  ;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
  FOR NTH = 0,NOF(LEVELS)-1 DO BEGIN
    LEVEL = LEVELS[NTH]
    THICK = THICKS[NTH]
    COLOR = COLORS[NTH]
    LINESTYLE = LINESTYLES[NTH]
    TT = TOPO_TAGS('SMI',LEVEL)
    OK = WHERE_STRING(FILES,TT.TAG + '-',COUNT)
    IF COUNT NE 1 THEN CONTINUE;>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
    FILE = FILES[OK]
    IF KEY(VERBOSE) THEN PFILE,FILE,/R
    OPENR,LUN,FILE,/GET_LUN
    WHILE NOT EOF(LUN) DO BEGIN
      READF,LUN,NUM,LEVEL,FORMAT=FMT
      LONS = DBLARR(NUM) & LONS(*) = MISSINGS(LONS)
      LATS = DBLARR(NUM) & LATS(*) = MISSINGS(LATS)
      READF,LUN,LONS,LATS,FORMAT=FMT
      IF KEY(SMO) THEN BEGIN
        LONS = SMOOTH(LONS,SMO <NOF(LONS)-1)
        LATS = SMOOTH(LATS,SMO <NOF(LATS)-1)
      ENDIF;IF KEY(SMO) THEN BEGIN
      PLT = PLOT(LONS,LATS,COLOR = COLOR,LINESTYLE=LINESTYLE,THICK = THICK ,/DATA,/OVERPLOT,/NO_TOOLBAR)   
    ENDWHILE;WHILE NOT EOF(LUN) DO BEGIN
    CLOSE,LUN
  ENDFOR;FOR NTH = 0,NOF(LEVELS)-1 DO BEGIN
;FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF

END; #####################  END OF ROUTINE ################################
