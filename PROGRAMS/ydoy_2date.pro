; $ID:	YDOY_2DATE.PRO,	2020-06-26-15,	USER-KJWH	$
;##################################################################3
FUNCTION YDOY_2DATE, YEAR, DOY, HOUR, MINUTE, SECOND
;+
; NAME:
;       YDOY_2DATE
;
; PURPOSE:
;				CONVERT YEAR AND DAY OF YEAR TO DATE
;
; CATEGORY:
;
;
;  EXAMPLES:
;      PRINT, YDOY_2DATE('2000',1)
;      PRINT, YDOY_2DATE('2000',[1,2,3,4,5])
;      PRINT, YDOY_2DATE('2000',1, 12, 0, 0)
;      PRINT, YDOY_2DATE('2000',1.5) ; NOTE CAN USE DECIMAL DAYS FOR DOY

;
; INPUTS:
;       YEAR, DAY OF YEAR,
;       HOUR, MINUTE, SECOND ARE OPTIONAL (IF NOT PROVIDED THEN PROGRAM ASSUMES BEGINING OF DAY)
;
; KEYWORD PARAMETERS:
;
; OUTPUTS:
;      A STRING : YYYYMMDDHHMMSS
; SIDE EFFECTS:
;       NONE.
;
; RESTRICTIONS:
;       IF HOUR,MINUTE,SECONDS PROVIDED THEN DOY SHOULD BE WHOLE NUMBERS
;       IF DO NOT PROVIDE HOUR,MINUTE,SECONDS THEN DOY CAN BE DECIMAL DOY  (E.G. 21.5)
;
; PROCEDURE:
;       STRAIGHTFORWARD.
;
; MODIFICATION HISTORY:
;       WRITTEN BY:  J.E.O'REILLY, SEPT 25,2000
;				MAY 27, 2003 JOR, ELIMINATED LOOP BECAUSE JULDAY SUPPORTS ARRAY INPUT
;				JUL 28,2015,JOR: IF NUMERIC(YEAR) EQ 0 OR NUMERIC(DOY) EQ 0 OR NUMERIC(HOUR) EQ 0 OR NUMERIC(MINUTE) EQ 0 OR NUMERIC(SECOND) EQ 0 THEN  JD = MISSINGS(0.D)
;       AUG 04,2015,JOR: IF NUMBER(YEAR) EQ 0 OR NUMBER(DOY) EQ 0 OR NUMBER(HOUR) EQ 0 OR NUMBER(MINUTE) EQ 0 OR NUMBER(SECOND) EQ 0 THEN BEGIN
;                         ===> SINCE '' IS NOT A NUMBER THEN SET ANY '' HOUR,MINUTE,SECONDS  TO ZEROS
;       OCT 05, 2015 - KJWH: FIXED BUG - CHANGED IF NUMBER(YEAR).... TO IF ANY(NUMBER([YEAR,DOY,HOUR,MINUTE,SECOND])) TO ACCOMODATE ARRAYS.
;####################################################################################				
;-

; **************************
  ROUTINE_NAME = 'YDOY_2DATE
; **************************

  IF NONE(YEAR) THEN RETURN,[]
  IF NONE(DOY)  THEN RETURN,[]

  IF N_ELEMENTS(HOUR) 	EQ 1 THEN HOUR   	= REPLICATE(HOUR,N_ELEMENTS(DOY))
  IF N_ELEMENTS(MINUTE) EQ 1 THEN MINUTE  = REPLICATE(MINUTE,N_ELEMENTS(DOY))
  IF N_ELEMENTS(SECOND) EQ 1 THEN SECOND  = REPLICATE(SECOND,N_ELEMENTS(DOY))

  IF N_ELEMENTS(HOUR)   NE N_ELEMENTS(DOY) THEN HOUR   = REPLICATE(0,N_ELEMENTS(DOY))
  IF N_ELEMENTS(MINUTE) NE N_ELEMENTS(DOY) THEN MINUTE = REPLICATE(0,N_ELEMENTS(DOY))
  IF N_ELEMENTS(SECOND) NE N_ELEMENTS(DOY) THEN SECOND = REPLICATE(0,N_ELEMENTS(DOY))

;===> SINCE '' IS NOT A NUMBER THEN SET ANY '' HOUR,MINUTE,SECONDS  TO ZEROS
  OK = WHERE(HOUR   EQ '',COUNT) & IF COUNT GE 1 THEN HOUR[OK]   = 0
  OK = WHERE(MINUTE EQ '',COUNT) & IF COUNT GE 1 THEN MINUTE[OK] = 0
  OK = WHERE(SECOND EQ '',COUNT) & IF COUNT GE 1 THEN SECOND[OK] = 0

; =====> CALCULATE JULIAN DAY FOR FIRST OF YEAR AND ADD DOY -1
;   		JULDAY(MONTH, DAY, YEAR, HOUR, MINUTE, SECOND)
   		
; ===>  WHEN DOY,YEAR,OR DOY OR HOUR OR MINUTE ARE NON-NUMERIC THEN SET JD TO MISSINGS CODE FOR DOUBLE 
  IF ANY(NUMBER([YEAR,DOY,HOUR,MINUTE,SECOND])) EQ 0 THEN BEGIN
    JD = MISSINGS(0.D)
    RETURN,JD_2DATE(JD)
  ENDIF ; IF ANY(NUMBER([YEAR,DOY,HOUR,MINUTE,SECOND]) EQ 0 THEN BEGIN

  JD = DOY -1.0D + JULDAY( 1, 1, YEAR, HOUR,MINUTE,(SECOND + 0.0001))  ;;;; 0.0001 ; NEEDED FOR ROUNDING ERRORS ?
  RETURN,JD_2DATE(JD)


END; #####################  END OF ROUTINE ################################
