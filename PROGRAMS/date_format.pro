; $ID:	DATE_FORMAT.PRO,	2020-07-08-15,	USER-KJWH	$
; ##################################################################################################################
	FUNCTION DATE_FORMAT,  DATE, YEAR=YEAR, MONTH=MONTH, DAY=DAY, HOUR=HOUR, MINUTE=MINUTE, SECOND=SECOND, $
                         STANDARD=STANDARD, BELOW=BELOW, NAME=NAME, COMMA=COMMA, DELIM=DELIM
                        

;+
; NAME:
;		DATE_FORMAT
;
; PURPOSE: RETURN A FORMATTED DATE STRING FROM A DATE STRING
;
; CATEGORY:	DATE
;	
;
; CALLING SEQUENCE:
;		RESULT = DATE_FORMAT(DATE)
;
; INPUTS:
;		DATE:	A DATE STRING FORMATTED AS:
;					YYYYMMDDHHMMSS
;					YYYYMMDDHHMM
;					YYYYMMDDHH
;					YYYYMMDD
;
; KEYWORD PARAMETERS:
;		BELOW..........	FORMAT DATE USING !C SO THAT THE HOUR,MINUTE,SECOND 
;		                PART OF THE DATE ARE BELOW THE YEAR-MONTH-DAY PART

;		YEAR..........	RETURN JUST THE YEAR
;		MONTH.........	RETURN JUST THE YEAR AND MONTH
;		DAY...........	RETURN  THE YEAR, MONTH, DAY
;		HOUR...........	RETURN  THE YEAR, MONTH, DAY, HOUR
;		MINUTE......... RETURN  THE YEAR, MONTH, DAY, HOUR, MINUTE
;		SECOND......... RETURN  THE YEAR, MONTH, DAY, HOUR, MINUTE, SECOND (DEFAULT FOR THIS ROUTINE)
;   COMMA.......... ADDS COMMAS AFTER EACH ELEMENT OF DATE
;   STANDARD....... RETURNS THE FULL DATE AND TIME IN STANDARD NOTATION (see https://www.cl.cam.ac.uk/~mgk25/iso-time.html)
;   NAME........... RETURN THE MONTH NAME IN THE DATE
;
; OUTPUTS:
;		A STRING WITH THE INPUT DATE FORMATTED ACCORDING TO THE KEYWORDS SPECIFIED
;
; RESTRICTIONS:
;		CONTEMPORARY DATES ARE ASSUMED WHERE THE YEAR IS SPECIFIED AS 4 CHARACTERS (YYYY)

; EXAMPLE:
;         PRINT,DATE_FORMAT(DATE_NOW()) ;                    2017-03-02-11:23:18
;         PRINT,DATE_FORMAT(DATE_NOW(),/COMMA);              2017,03,02,11:23:40
;         PRINT,DATE_FORMAT(DATE_NOW(),/STANDARD);           2017,03,02T11:23:40Z
;         PRINT,DATE_FORMAT(DATE_NOW(),/YEAR) ;              2017
;         PRINT,DATE_FORMAT(DATE_NOW(),/MONTH);              2017-03
;         PRINT,DATE_FORMAT(DATE_NOW(),/DAY)  ;              2017-03-02
;         PRINT,DATE_FORMAT(DATE_NOW(),/HOUR) ;              2017-03-02-11
;         PRINT,DATE_FORMAT(DATE_NOW(),/HOUR,/COMMA);        2017,03,02,11
;         PRINT,DATE_FORMAT(DATE_NOW(),/HOUR,/NAME);         2017-MARCH-02-11
;         PRINT,DATE_FORMAT(DATE_NOW(),/HOUR,/NAME,/COMMA);  2017,MARCH,02,11
;         PRINT,DATE_FORMAT(DATE_NOW(),/MINUTE,/NAME,/BELOW);2017-MARCH-02-!C11:32
;	NOTES:
;
;
; MODIFICATION HISTORY:
;			WRITTEN MAR 6, 1999 BY J.O'REILLY, 28 TARZWELL DRIVE, NMFS, NOAA 02882 (JAY.O'REILLY@NOAA.GOV)
;			SEP 28, 2011 - JEOR: ADDED KEYWORD COMMA
;			JUL 14, 2011 - JEOR: ADDED EXAMPLES
;			FEB 24, 2013 - JEOR: ADDED KEYWORD DELIM
;			MAR 26, 2014 - KJWH: REMOVED HOURS+MINUTES+SECONDS FROM THE IF KEY(YMD) OUTPUT
;			JUN 07, 2014 - JEOR: ADDED Y_MDH
;     FEB 28, 2016 - JEOR: FIXED LOGIC FOR KEY Y_MDH
;     FEB 18, 2017 - JEOR: IF NONE(DELIM) THEN DELIM = ';'
;     FEB 21, 2017 - KJWH: FORMATTING
;     MAR 02, 2017 - JEOR: SIMPLIFIED-STREAMLINED
;                          REMOVED UNIT KEY 
;                          REMOVED KEYS NOT USED: DMY,MDY,YMD,Y_MDH ETC. 
;                          REPLACED ALL IF KEY() THEN WITH CASE BLOCK
;     MAR 02, 2017 - KJWH: FORMATTING & REMOVED Y_MDH FROM THE DOCUMENTATION
;     MAR 27, 2019 - KJWH: ADDED STANDARD KEYWORD TO RETURN THE DATE WITH A "T" SEPARATING THE DATE AND TIME AND "Z" TO INDICATE UTC TIME
;-
;	**************************
  ROUTINE_NAME='DATE_FORMAT'
; **************************
  IF NONE(DATE)  THEN DATE = DATE_NOW()
  IF NONE(DELIM) THEN DELIM = '-'
  IF KEY(COMMA)  THEN DELIM = ','
  IF KEY(STANDARD) THEN DELIM = '-'

  YEARS   = STRMID(DATE,0,4)
  MONTHS  = STRMID(DATE,4,2)  
  DAYS    = STRMID(DATE,6,2)
  HOURS   = STRMID(DATE,8,2)
  MINUTES = STRMID(DATE,10,2)
  SECONDS = STRMID(DATE,12,2)
  MINUTES = ':'+MINUTES
  SECONDS = ':'+SECONDS

	IF KEY(NAME) THEN BEGIN
		MONTHS=MONTH_NAMES(MONTHS)
		IF NAME GT 1 THEN MONTHS=STRMID(MONTHS,0,NAME)
	ENDIF;IF KEY(NAME) THEN BEGIN

  IF KEY(BELOW) THEN HOURS = '!C'+HOURS
;CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
  CASE [1] OF
    KEY(YEAR):     RETURN,  STRTRIM(YEARS,2)
    KEY(MONTH):    RETURN,  STRTRIM(YEARS+DELIM+MONTHS,2)
    KEY(DAY):      RETURN,  STRTRIM(YEARS+DELIM+MONTHS+DELIM+DAYS,2) 
    KEY(HOUR):     RETURN,  STRTRIM(YEARS+DELIM+MONTHS+DELIM+DAYS+DELIM+HOURS,2)
    KEY(MINUTE):   RETURN,  STRTRIM(YEARS+DELIM+MONTHS+DELIM+DAYS+DELIM+HOURS+MINUTES,2)
    KEY(SECOND):   RETURN,  STRTRIM(YEARS+DELIM+MONTHS+DELIM+DAYS+DELIM+HOURS+MINUTES+SECONDS,2)
    KEY(STANDARD): RETURN,  STRTRIM(YEARS+DELIM+MONTHS+DELIM+DAYS+'T'+HOURS+MINUTES+SECONDS+'Z',2)
    ELSE:          RETURN,  STRTRIM(YEARS+DELIM+MONTHS+DELIM+DAYS+DELIM+HOURS+MINUTES+SECONDS,2)
  ENDCASE;CASE (1) OF
;CCCCCCCCCCCCCCCCCC


END; #####################  END OF ROUTINE ################################
