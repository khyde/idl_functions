; $ID:	MAPS_OISST_2BIN.PRO,	2020-02-07-14,	USER-KJWH	$
;##########################################################################
FUNCTION MAPS_OISST_2BIN, OISST, MAP_OUT, INIT=INIT

; THIS PROGRAM CONVERTS A 1440 X 720 OISST ARRAY TO EITHER A L3B9 OR L3B4 ARRAY
;
; CATEGORY:
;    MAPPING
;    
; UTILITY:
;    REMAPPING
;    
; CALLING SEQUENCE:
;    L3B = MAPS_OISST_2BIN(OISST, 'L3B9') 
;    L3B = MAPS_OISST_2BIN(OISST, 'L3B4') 
;    
; INPUTS:
;   OISST   = A 1440 X 720 OISST DATA ARRAY
;   MAP_OUT = EITHER 'L3B9' OR 'L3B4'
;
; OPTIONAL INPUTS:
;
; KEYWORDS:
;
; OUTPUTS:
;
; EXAMPLES:
; 
; NOTES:
;  The OISST_1440_720_2L3B9(4).SAV files were created using MAPS_L3B_SUBS
;  
; MODIFICATION HISTORY:
;     FEB 07, 2020  Written: by K.J.W.Hyde, 28 Tarzwell Drive, NMFS, NOAA 02882 (kimberly.hyde@noaa.gov)
;     FEB 07, 2020 - KJWH: Adapted from MAPS_AVHRR_2BIN
;- 
  
;********************************* 
  ROUTINE_NAME = 'MAPS_OISST_2BIN'
;*********************************
  COMMON MAPS_OISST_2BIN_, BINS9, BINS4, BINS2, BINS1 
  IF KEY(INIT) OR NONE(BINS9) THEN BINS9 = []
  IF KEY(INIT) OR NONE(BINS4) THEN BINS4 = []
  IF KEY(INIT) OR NONE(BINS2) THEN BINS2 = []
  IF KEY(INIT) OR NONE(BINS1) THEN BINS1 = []
  
  SZ = SIZEXYZ(OISST)
  IF SZ.PX NE 1440 AND SZ.PY NE 720 THEN RETURN, 'ERROR: INPUT ARRAY DEMINSIONS MUST BE 8640 X 4320'
  IF NONE(MAP_OUT) THEN RETURN, 'ERROR: MAP_OUT MUST BE EITHER L3B1, L3B4 OR L3B9'
  
  FILE  = !S.MAPINFO + 'OISST-PXY_1440_720-2'+MAP_OUT+'.SAV'
  MS = MAPS_SIZE(MAP_OUT)
  NBINS = MS.PY
  
  CASE STRUPCASE(MAP_OUT) OF
    'L3B9': BINS = BINS9
    'L3B4': BINS = BINS4
    'L3B2': BINS = BINS2
    'L3B1': BINS = BINS4
    ELSE: RETURN, 'ERROR: MAP_OUT MUST BE EITHER L3B1, L3B2, L3B4 OR L3B9'  
  ENDCASE
  
  L3B = FLTARR(NBINS) & L3B(*) = MISSINGS(0.0)  ; CREATE BLANK ARRAY
  IF EXISTS(FILE) EQ 0 THEN MESSAGE, 'ERROR: Missing ' + FILE
  IF NONE(BINS) THEN BINS = IDL_RESTORE(FILE)   
  IF NONE(BINS9) AND MAP_OUT EQ 'L3B9' THEN BINS9 = BINS ; Save BINS9 in common for subsequent calls
  IF NONE(BINS4) AND MAP_OUT EQ 'L3B4' THEN BINS4 = BINS ; Save BINS4 in common for subsequent calls
  IF NONE(BINS2) AND MAP_OUT EQ 'L3B2' THEN BINS2 = BINS ; Save BINS2 in common for subsequent calls
  IF NONE(BINS1) AND MAP_OUT EQ 'L3B1' THEN BINS1 = BINS ; Save BINS1 in common for subsequent calls
    
  H = HISTOGRAM(BINS, MIN=0, REVERSE_INDICES=R)
  FOR N=0, N_ELEMENTS(L3B)-1 DO BEGIN
    IF R[N+1]-R[N] GE 1 THEN L3B(N) = MEAN(OISST(R[R[N]:R[N+1]-1]),/NAN)
  ENDFOR
    
  L3B(WHERE(FINITE(L3B) EQ 0)) = MISSINGS(0.0) ; Change all non-finite values to INF
  _L3B = FLTARR(1, NBINS)
  _L3B(0:*) = L3B
  GONE, L3B
  RETURN, _L3B
  
END
