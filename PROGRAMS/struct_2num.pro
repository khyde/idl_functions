; $ID:	STRUCT_2NUM.PRO,	2020-07-29-14,	USER-KJWH	$
;#######################################################################################################
	FUNCTION STRUCT_2NUM, STRUCT, DBL=DBL, EXCLUDE=EXCLUDE, FORCE=FORCE
;+
; THIS FUNCTION CONVERTS TAGS IN A SIMPLE SPREADSHEET STRUCTURE TO NUMERIC TYPE (LONG OR DOUBLE)
; SYNTAX:
;   RESULT = STRUCT_2NUM(STRUCT)
;   RESULT = STRUCT_2NUM(STRUCT,/DBL)
; ARGUMENTS:
;   STRUCT: IDL STRUCTURE (SIMPLE SPREADSHEET TYPE STRUCTURE)
; KEYWORDS:
;   DBL: CONVERT POTENTIAL NUMBERS IN STRING TAGS INTO DOUBLE PRECISION. THE DEFAULT IS 
;				 TO CONVERT TO FLOAT WHEN DECIMAL OR SCIENTIFIC (EXPONENTIAL) NOTATION E.G. 1E-3 .
;		EXCLUDE: TAGNAMES TO EXCLUDE FROM BEING CHANGED TO NUMBERS.
;   FORCE: TAGNAMES TO FORCE TO BEGING CHANGED TO NUMBERS EVEN IF A TEXT STRING IS DETECTED

; EXAMPLE:
;		STRUCT = CREATE_STRUCT('AA',0B,'BB','1','CC','2.1','DD',0.0D,'EE',0L,'FF',' ','GG','CAT','HH',' ')
;		STRUCT = REPLICATE(STRUCT,3)
;		STRUCT[0].AA = 255
;		STRUCT(2).AA = 127
;		STRUCT[0].BB = 'CAT'
;		STRUCT[1].BB = ''
;		STRUCT[1].FF = '0'
;		STRUCT[1].EE = -2
;		STRUCT[1].HH ='1E-3'
;		STRUCT(2).HH ='-1E3'
;	 	HELP,/STRUCT,STRUCT
;		D=STRUCT_2NUM(STRUCT,/DBL)
;		HELP,/STRUCT,D
;		S = STRUCT_2NUM(STRUCT_IT(DATE_NOW())) & ST,S
;
; NOTES:
;
;		1) THE INPUT STRUCTURE IS NOT ALTERED.
;		2) NUMERIC TAGS AND TAGS WHICH ARE NOT STRING ARE NOT CHANGED
;		3) STRING TAGS ARE CONVERTED TO LONG IF NONE OF THE ELEMENTS OF THE TAG HAVE A DECIMAL CHARACTER
;			 AND NONE OF THE ELEMENTS HAVE SCIENTIFIC (EXPONENTIAL) NOTATION E.G. 1E-3
;		4) STRING TAGS ARE CONVERTED TO DOUBLE IF ANY OF THE ELEMENTS OF THE TAG HAVE A DECIMAL CHARACTER
;			 OR ANY OF THE ELEMENTS OF THE TAG HAVE SCIENTIFIC (EXPONENTIAL) NOTATION
;		5) IF THE STRING CAN BE CONVERTED TO LONG OR DOUBLE THEN
;			 ANY NULL '' OR EMPTY '    ' ELEMENTS ARE SET TO THE MISSING DATA CODE FOR LONG OR DOUBLE
;			 AS DEFINED IN MISSINGS.PRO
;		6) IF THE KEYWORD DBL IS USED THEN STRINGS ARE CONVERTED TO DOUBLE INSTEAD OF FLOAT
;
;		6) THIS ROUTINE IS USEFULL FOR CONVERTING A STRUCTURE MADE OF STRING VARIABLES
;				(E.G. FROM READ_CSV.PRO) INTO NUMERIC TYPE DATA
;
; HISTORY:
;   AUG 11, 2001 WRITTEN BY: J.E. O'REILLY, NOAA, 28 TARZWELL DRIVE, NARRAGANSETT, RI 02882
;		OCT 10, 2006 - JEOR:  USES ULONG64 WHEN NECESSARY TO CONSERVE LONG STRINGS SUCH AS DATE
;											    ADDED EXCLUDE TO PREVENT ANY CHANGES TO TAGNAMES SPECIFIED
;	  JUL 05, 2013 - JEOR: FORMATTING,RETURN, ERROR STRING INSTEAD OF -1
;	  APR 11 ,2014 - JEOR: ADDED EXAMPLE FOR A DATE 
;	  OCT 04, 2014 - JEOR: DEFAULT CHANGED TO FLOAT
;	  JUN 20, 2017 - KJWH: Added FORCE keyword to FORCE a variable to be changed to a number even if a STRING is detected.  STRINGS will be converted to MISSINGS
;#######################################################################################################
;
;-
; ***************************
  ROUTINE_NAME  = 'STRUCT_2NUM'
;****************************

; CREATE_STRUCT
  SZ=SIZE(STRUCT,/STRUCT)
  TYPE = SZ.TYPE

;	===> STRUCT MUST BE A STRUCTURE
  IF TYPE NE 8 THEN BEGIN
    ERROR = 'ERROR: STRUCT MUST BE A SIMPLE SPREADSHEET-TYPE IDL STRUCTURE'
    RETURN, ERROR
  ENDIF

	IF N_ELEMENTS(EXCLUDE) GE 1 THEN _EXCLUDE = EXCLUDE ELSE _EXCLUDE = ''
	IF N_ELEMENTS(FORCE)   GE 1 THEN _FORCE   = FORCE   ELSE _FORCE = ''

	IF KEYWORD_SET(DBL) THEN MISSING_CODE_DECIMAL = MISSINGS(0.0D) ELSE MISSING_CODE_DECIMAL = MISSINGS(0.0)
	MISSING_CODE_INT 			= MISSINGS(0)
	MISSING_CODE_LONG 		= MISSINGS(0L)
	MISSING_CODE_ULONG64 	= MISSINGS(ULONG64(0L))
	MISSING_CODE_STRING 	= MISSINGS('')

  NTAGS = N_TAGS(STRUCT)

  TAGNAMES = TAG_NAMES(STRUCT)
	NUM = N_ELEMENTS(STRUCT)
	INDX = LINDGEN(NUM)
	EMPTY_STRINGS = REPLICATE(0B,NUM)


;	******************************************************
; CREATE A NEW STRUCTURE WITH TAG NAMES SAME AS ORIGINAL
; BUT CHANGE THE DATA TYPE TO NUMERIC IF POSSIBLE
  FOR N = 0L, NTAGS-1L DO BEGIN
  	TYPE = IDLTYPE(STRUCT.(N),/CODE)

;		===> CHECK TO SEE IF THIS TAGNAME IS IN THE EXCLUDE OR FORCE LIST
		OK_EXCLUDE=WHERE(STRUPCASE(_EXCLUDE) EQ STRUPCASE(TAGNAMES(N)), COUNT_EXCLUDE)
		OK_FORCE  =WHERE(STRUPCASE(_FORCE)   EQ STRUPCASE(TAGNAMES(N)), COUNT_FORCE)

		IF COUNT_EXCLUDE EQ 1 OR TYPE NE 7 THEN BEGIN
;			===> IF TAG IS ALREADY NUMERIC OR IF TAG IS NOT A STRING THEN DO NOT CHANGE THE DATA TYPE
		  TEMPLATE = STRUCT[0].(N)
		ENDIF ELSE BEGIN

;			===> WHICH ARRAY VALUES ARE POTENTIALLY NUMERIC
  		_NUMBER  = NUMBER(STRUCT.(N))
  		_EMPTY_STRINGS = EMPTY_STRINGS
			OK=WHERE(STRTRIM(STRUCT.(N),2) EQ '',COUNT)
  		IF COUNT GE 1 THEN _EMPTY_STRINGS[OK] = 1
  		VALID_NUMBER    = _NUMBER > _EMPTY_STRINGS

			IF MIN(VALID_NUMBER) EQ 0 AND COUNT_FORCE EQ 0 THEN BEGIN
;				IF ANY ELEMENT OF THE VALID ARRAY IS 0 THEN NOT ALL VALUES IN THE STRING CAN BE CONVERTED INTO
;				NUMBERS SO THE TAG WILL REMAIN AS STRING TYPE
				TEMPLATE = MISSING_CODE_STRING
			ENDIF ELSE BEGIN
			  
;				IF THERE ARE ANY DECIMALS IN THE STRING OR IF THE STRING IS SCIENTIFIC (EXPONENTIAL) NOTATION
;				E.G. 1E-3, THEN CONVERT TO DOUBLE ELSE MAKE LONG, ULONG, OR ULONG64, AS NEEDED
   			IF MAX(STRPOS(STRUCT.(N),'.')) GE 0 OR  MAX(STRPOS(STRUPCASE(STRUCT.(N)),'E')) GE 1 THEN BEGIN
   				TEMPLATE = MISSING_CODE_DECIMAL
   			ENDIF ELSE BEGIN
   				MAX_LEN = MAX(STRLEN(STRUCT.(N)))
   				TEMPLATE = MISSING_CODE_ULONG64 ; START WITH LARGEST POSSIBLE LONG
   				IF MAX_LEN LE 9 THEN TEMPLATE = MISSING_CODE_LONG
   				IF MAX_LEN LE 4 THEN TEMPLATE = MISSING_CODE_INT
   			ENDELSE
			ENDELSE
		ENDELSE

;		===> MAKE THE NEW STRUCTURE
		IF N EQ 0 THEN _STRUCT	=	CREATE_STRUCT(TAGNAMES(N),TEMPLATE) ELSE $
  								 _STRUCT = CREATE_STRUCT(_STRUCT,TAGNAMES(N),TEMPLATE)
  ENDFOR

  _STRUCT=REPLICATE(_STRUCT,NUM)

;	****************************************************
;	FILL EACH TAG, CHECKING IF TYPE IS NUMERIC OR STRING
	FOR N = 0L, NTAGS-1L DO BEGIN
		TYPE_SOURCE	= IDLTYPE(STRUCT.(N),/CODE)
		TYPE_DEST		= IDLTYPE(_STRUCT.(N),/CODE)

		IF TYPE_DEST EQ TYPE_SOURCE THEN BEGIN
			_STRUCT.(N) = STRUCT.(N)
		ENDIF ELSE BEGIN
		  _STRUCT.(N) = STR2NUM(STRUCT.(N))
;			SET ELEMENTS OF THE OUTPUT _STRUCT TO THE MISSING CODE APPROPRIATE FOR THE OUTPUT TAG WHEN
;			ANY ELEMENTS OF THE INPUT STRUCT WHICH ARE EMPTY STRINGS
		  OK=WHERE(STRTRIM(STRUCT.(N),2) EQ '',COUNT)
		  IF COUNT GE 1 THEN _STRUCT[OK].(N) = MISSINGS(_STRUCT[OK].(N))
		ENDELSE
  ENDFOR

  RETURN,_STRUCT


END; #####################  END OF ROUTINE ################################
